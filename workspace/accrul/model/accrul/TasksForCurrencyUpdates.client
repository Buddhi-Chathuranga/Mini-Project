client TasksForCurrencyUpdates;
component ACCRUL;
layer Core;
projection TasksForCurrencyUpdatesHandling;
include fragment ScheduledTasksCommon;
warningsenabled = ExtCurrencyTaskDetail;

----------------------------- NAVIGATOR ENTRIES -----------------------------
navigator {
   entry CurrencyTasksNavEntry parent AccrulNavigator.CurrencyMenu at index 500 {
      label = "Tasks for Currency Updates";
      page Form home ExtCurrencyCompany;
   }
}


-------------------------------- MAIN PAGES ---------------------------------
page Form using ExtCurrencyTaskSet {
   label = "Tasks for Currency Updates";

   selector ExtCurrencyTaskSelector;
   initcontext PageInitContext;
   group ExtCurrencyTaskGroup;
   list ExtCurrencyTaskDetailList(ExtCurrencyTaskDetailsArray);

   command UpdateCurrencyRatesCommand;
}


-------------------------------- ASSISTANTS ---------------------------------
assistant ScheduleCurrencyRateUpdateAssistant using TaskVirtualSet {
   label = "Update Currency Rates";
   savemode = OnLostFocus;
   input(TaskId);

   init command {
      variable ParametersStructVar {
         type = Structure(ParametersStructure);
      }

      execute {
         call DefaultScheduleCurrUpdateParameters() into ParametersStructVar;
         set ScheduleMethodId = ParametersStructVar.ScheduleMethodId;
         set ScheduleName = ParametersStructVar.Description;
         set UpdateOptions = "Now";
      }
   }

   steps {
      step {
         label = "Set Currency Update Parameters";
         group ExecutionOption;
      }
      step {
         label = "Schedule";

         visible = [TaskId != null and UpdateOptions = "Schedule"];
         enabled = [TaskId != null];

         group SchedulingParametersGroup;
         group SchedulingStartAndStopDateGroup;
      }
      step {
         label = "Schedule Options";

         visible = [TaskId != null and UpdateOptions = "Schedule"];
         enabled = [TaskId != null];

         group ScheduledTasksAssistantAdvOpGroup;
         group ScheduledTasksAssistantStreamsGroup;
      }

      final step {
         description = "${UpdateStatus}";
         command NavigateToScheduledTask {
            enabled = [UpdateOptions = "Schedule"];
         }
      }

      cancelled step {
         description = "Task was cancelled";
      }
   }
   finish command {
      label = "OK";
      enabled = [TaskId != null];

      execute {
         if [UpdateOptions = "Now"] {
            call UpdateCurrencyRatesFromWorkFlow(TaskId);
            navigate back;
         }
         else {
            call ScheduleTask() into ScheduleTaskVar;
            set ScheduleId = ScheduleTaskVar.ScheduleId;
            set UpdateStatus = "Task '${ScheduleName}', assigned with the Id '${ScheduleId}', was successfully scheduled to start ${ScheduleTaskVar.ScheduledDate} at ${ScheduleTaskVar.ScheduledTime}.";

            if [UpdateOptions = "Viewschedule"] {
               navigate back;
            }
         }
      }
   }
   cancel command {
      enabled = [true];
   }
   restart command {
      visible = [true];
   }
}

--------------------------------- COMMANDS ----------------------------------
command UpdateCurrencyRatesCommand for ExtCurrencyTask {
   label = "Update Currency Rates";
   enabled = [Workflow = true and CompanyRowExist];

   execute {
      if [not initcontext.WorkflowEnabled] {
         call ValidateWorkFlowEnabled();
      }
      else {
         call ValidateUpdateCurrencyRate();
         assistant ScheduleCurrencyRateUpdateAssistant(TaskId) {
            when YES {
               exit OK;
            }
            when NO {
               exit CANCEL;
            }
         }
      }
   }
}

initcontext PageInitContext {
   parameter WorkflowEnabled Boolean;
   init command {
      execute {
         call InitWorkflowEnabled() into WorkflowEnabled;
      }
   }
}

--------------------------------- SELECTORS ---------------------------------
@Override
selector ExtCurrencyTaskSelector for ExtCurrencyTask {
   orderby = TaskId;
   label = "${TaskId} - ${Description}";
   field TaskId;
   field Description;
}


---------------------------------- GROUPS -----------------------------------
group ExtCurrencyTaskGroup for ExtCurrencyTask {
   label = "";

   field TaskId {
      editable = [isNew];
   }
   field Description {
      required = [true];
   }
   field Workflow {
      visible = [Workflow or initcontext.WorkflowEnabled];
      editable = [Workflow or initcontext.WorkflowEnabled];
   }
   field LastUpdated {
      visible = [Workflow and initcontext.WorkflowEnabled];
   }
}

group ExecutionOption for TaskVirtual {
   label = "";
   radiogroup UpdateOptions {
      required = [true];
   }
}


----------------------------------- LISTS -----------------------------------
list ExtCurrencyTaskDetailList for ExtCurrencyTaskDetail {
   label = "Company for Task ID";
   multiselect = [false];

   lov CompanyRef with ReferenceCompanyFinanceSelector;
   lov CurrencyTypeRef with ReferenceCurrencyType3QuerySelector {
      advancedview CurrencyType3QueryLovList;
   }
   field LastUpdated {
      size = Large;
      columnvisible = [parent.Workflow and initcontext.WorkflowEnabled and parent.LastUpdated != null];
   }
}

@Override
list CurrencyType3QueryLovList for CurrencyType3Query {
   field Company {
      columnexclude = [true];
   }
}

@Override
list CompanyFinanceLovList for CompanyFinance {
   static Description {
      label = "Company Name";
   }
}


----------------------------------- CARDS -----------------------------------


---------------------------------- DIALOGS ----------------------------------


---------------------------------- CHARTS -----------------------------------


--------------------------------- CALENDARS ---------------------------------


---------------------------------- SHEETS -----------------------------------


----------------------------- STATE INDICATORS ------------------------------


----------------------------------- TREES -----------------------------------


---------------------------------- PLUGINS ----------------------------------


------------------------------- IMAGE VIEWERS -------------------------------



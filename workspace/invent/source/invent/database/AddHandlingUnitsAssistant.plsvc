-----------------------------------------------------------------------------
--
--  Fragment:     AddHandlingUnitsAssistant
--  Component:    INVENT
--
--  IFS Developer Studio Template Version 3.0
--
--  Date       Sign    History
--  ---------  ------  ---------------------------------------------------------
-- 2018-12-08  ChFolk  SCUXXW4-4786, Created.
-----------------------------------------------------------------------------

layer Core;
@Override
PROCEDURE CRUD_Default___ (
   rec_  IN OUT Handling_Units_Virtual_Rec )
IS
BEGIN
   rec_.no_of_units := 1;
   super(rec_);
END;   
   
PROCEDURE Connect_To_Handling_Units___ (
   parent_objkey_ IN  VARCHAR2,
   info$_         OUT VARCHAR2) 
IS
   parent_key_                  Add_Handling_Units_Virtual_Key;
   parent_rec_                  Add_Handling_Units_Virtual_Rec;
   child_key_                   Handling_Units_Virtual_Key;
   child_rec_                   Handling_Units_Virtual_Rec;
   new_child_rec_               Handling_Units_Virtual_Rec;
   hu_connected_                BOOLEAN := FALSE;
BEGIN
   parent_key_.objkey := parent_objkey_;
   parent_rec_ := Get_Record___(parent_key_);
   FOR line_rec_ IN Get_Add_Handling_Units_Array___(parent_key_) LOOP
      child_key_.objkey := line_rec_.objkey;
      child_rec_ := Get_Record___(child_key_);
      new_child_rec_ := child_rec_;
      FOR i_ IN 1..child_rec_.no_of_units LOOP
         -- Security
         $IF Component_Shpmnt_SYS.INSTALLED $THEN
         IF (parent_rec_.shipment_id IS NOT NULL) THEN
            Shipment_Initial_Check___(parent_rec_.shipment_id);
         END IF;     
         $END
         -- TODO: maybe we should have initial check against all possible sources also here?
         -- But at the same time there are no exist checks at all at the moment against the sources in Handling_Unit_API 
         -- and the sources seems to be mostly for filtering or reference so the risk seems low here.
         Handling_Unit_API.New(handling_unit_id_          => new_child_rec_.handling_unit_id, 
                               handling_unit_type_id_     => child_rec_.handling_unit_type_id, 
                               parent_handling_unit_id_   => parent_rec_.parent_handling_unit_id, 
                               shipment_id_               => parent_rec_.shipment_id,
                               source_ref_type_db_        => parent_rec_.source_ref_type,
                               source_ref1_               => parent_rec_.source_ref1,
                               source_ref2_               => parent_rec_.source_ref2,
                               source_ref3_               => parent_rec_.source_ref3);
         hu_connected_ := TRUE;                               
      END LOOP;
      IF (hu_connected_) THEN
         CRUD_Update___(child_rec_, new_child_rec_);
      END IF;   
   END LOOP;
   IF (hu_connected_) THEN
      info$_ := Handling_Unit_API.Get_Max_Capacity_Exceeded_Info(parent_rec_.parent_handling_unit_id);
   END IF;
END Connect_To_Handling_Units___;
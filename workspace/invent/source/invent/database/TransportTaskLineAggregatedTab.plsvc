-----------------------------------------------------------------------------
--
--  Fragment:     TransportTaskLineAggregatedTab
--  Component:    INVENT
--
--  IFS Developer Studio Template Version 3.0
--
--  Date        Sign    History
--  ----------  ------  ---------------------------------------------------------
--  2019-05-20  ChFolk  SCUXXW4-20397, Modified Create_Message_Attibute___ to use correct record separator.
--  2019-04-26  ChFolk  SCUXXW4-16439, Added method Create_Message_Attibute___ and modified the usages.
--  2019-03-18  ChFolk  SCUXXW4-14910, Created to move tabs in transport Task to seperate fragments.
---------------------------------------------------------------------------------

layer Core;

FUNCTION Get_Aggregated_Handling_Units_On_Transport_Task___( 
   show_hu_level_            IN VARCHAR2,
   parent_transport_task_id_ IN NUMBER ) RETURN Objid_Arr
IS
   base_collection_ Objid_Arr := Objid_Arr();   
       
   CURSOR get_show_all IS
      SELECT objid FROM TRANSPORT_TASK_HANDLING_UNIT
       WHERE transport_task_id = parent_transport_task_id_;
       
   CURSOR get_show_top_level IS
      SELECT objid FROM TRANSPORT_TASK_HANDLING_UNIT
      WHERE transport_task_id = parent_transport_task_id_      
      AND outermost_db = 'TRUE';   
   
BEGIN  
   IF (show_hu_level_ = 'ShowOnlyTopLevel') THEN
      FOR rec_ IN get_show_top_level LOOP
         base_collection_.extend;
         base_collection_(base_collection_.last) := rec_.objid;
      END LOOP;  
   ELSE  
      FOR rec_ IN get_show_all LOOP
         base_collection_.extend;
         base_collection_(base_collection_.last) := rec_.objid;
      END LOOP;     
   END IF;
   RETURN base_collection_;   
END Get_Aggregated_Handling_Units_On_Transport_Task___;

FUNCTION Get_Agg_Handling_Unit_Data___(
   handling_unit_id_           IN NUMBER,
   sscc_                       IN VARCHAR2,
   alt_handling_unit_label_id_ IN VARCHAR2 ) RETURN Aggregated_Handling_Unit_Data_Structure_Rec
IS
   agg_hu_data_rec_                 Aggregated_Handling_Unit_Data_Structure_Rec;
   temp_handling_unit_id_           NUMBER;
   temp_sscc_                       VARCHAR2(18);
   temp_alt_handling_unit_label_id_ VARCHAR2(25); 
   hu_rec_                          Handling_Unit_API.Public_Rec;
BEGIN
   temp_handling_unit_id_ := handling_unit_id_;
   temp_sscc_ := sscc_;
   temp_alt_handling_unit_label_id_ := alt_handling_unit_label_id_;   
   Handling_Unit_API.Get_Handling_Unit_Identifiers(temp_handling_unit_id_, temp_sscc_, temp_alt_handling_unit_label_id_);
   hu_rec_ := Handling_Unit_API.Get(temp_handling_unit_id_); 
   agg_hu_data_rec_.handling_unit_type_id := hu_rec_.handling_unit_type_id;
   agg_hu_data_rec_.handling_unit_type_desc := Handling_Unit_Type_API.Get_Description(hu_rec_.handling_unit_type_id);
   agg_hu_data_rec_.handling_unit_id := temp_handling_unit_id_;
   agg_hu_data_rec_.sscc := temp_sscc_;
   agg_hu_data_rec_.alt_handling_unit_label_id := temp_alt_handling_unit_label_id_;
   agg_hu_data_rec_.contract := hu_rec_.contract;
   agg_hu_data_rec_.location_no := hu_rec_.location_no;
   RETURN agg_hu_data_rec_;
   
END Get_Agg_Handling_Unit_Data___;

PROCEDURE Pick_Aggregated_Handling_Units___ (
   selection_    IN VARCHAR2 )
IS
   msg_   CLOB;   
BEGIN
   msg_ := Create_Message_Attibute___(selection_);   
   Transport_Task_Handl_Unit_API.Pick_HU_Transport_Task(msg_);
END Pick_Aggregated_Handling_Units___;

PROCEDURE Unpick_Aggregated_Handling_Units___ (
   selection_    IN VARCHAR2 )
IS
   msg_   CLOB;   
BEGIN
   msg_ := Create_Message_Attibute___(selection_);
   Transport_Task_Handl_Unit_API.Unpick_HU_Transport_Task(msg_);
END Unpick_Aggregated_Handling_Units___;

PROCEDURE Execute_Aggregated_Handling_Units___ (
   selection_    IN VARCHAR2 )
IS
   msg_   CLOB;
BEGIN
   msg_ := Create_Message_Attibute___(selection_);
   Transport_Task_Handl_Unit_API.Execute_Transport_Task(msg_);
END Execute_Aggregated_Handling_Units___;

FUNCTION Move_Aggregated_Handling_Units_To_New_Transport_Task___ (
   new_transport_task_id_  NUMBER,
   set_to_fixed_           BOOLEAN,
   print_transport_task_   BOOLEAN,
   selection_              IN VARCHAR2 ) RETURN VARCHAR2
IS
   msg_                 CLOB;  
   task_fixed_          VARCHAR2(5) := 'FALSE';   
   parameter_attr_      VARCHAR2(32000);
   report_attr_         VARCHAR2(1000);
   result_key_          NUMBER;
   result_key_list_     VARCHAR2(5000);
   end_user_            VARCHAR2(200) := Fnd_Session_API.Get_Fnd_User;
BEGIN
   IF set_to_fixed_ THEN
      task_fixed_ := 'TRUE';
   END IF;
   msg_ := Create_Message_Attibute___(selection_);
   Transport_Task_Handl_Unit_API.New_HU_Transport_Task(msg_, new_transport_task_id_, task_fixed_, task_fixed_);
   IF (print_transport_task_) THEN
      Client_SYS.Clear_attr(parameter_attr_);
      Client_SYS.Add_To_Attr('TRANSPORT_TASK_ID', new_transport_task_id_,   parameter_attr_);
      Client_SYS.Clear_Attr(report_attr_);
      Client_SYS.Add_To_Attr('REPORT_ID', 'TRANSPORT_TASK_REP', report_attr_);
      result_key_ := Report_Format_API.Create_New_Report(report_attr_, parameter_attr_, end_user_);
      IF (result_key_list_ IS NULL) THEN
         result_key_list_ := result_key_;
      ELSE
         result_key_list_ := result_key_list_ || ',' || result_key_;
      END IF;
   END IF;  
   RETURN result_key_list_;
 
END Move_Aggregated_Handling_Units_To_New_Transport_Task___;

PROCEDURE Aggregated_Handling_Units_Apply_Drop_Off_Location___ (
   selection_ IN  VARCHAR2,
   info$_     OUT VARCHAR2 )
IS   
   msg_      CLOB;
BEGIN
   msg_ := Create_Message_Attibute___(selection_);  
   info$_ := Transport_Task_Handl_Unit_API.HU_Apply_Drop_Off_Location_Str(msg_);
  
END Aggregated_Handling_Units_Apply_Drop_Off_Location___;

PROCEDURE Convert_Aggregated_Handling_Units_To_Direct_Transport_Task___ (
   selection_    IN  VARCHAR2,
   info$_        OUT VARCHAR2)
IS   
   msg_      CLOB;
BEGIN
   msg_ := Create_Message_Attibute___(selection_);
   info$_ := Transport_Task_Handl_Unit_API.HU_Revoke_Two_Step_Trp_Task(msg_);
  
END Convert_Aggregated_Handling_Units_To_Direct_Transport_Task___;

FUNCTION Create_Message_Attibute___ (
   selection_ IN VARCHAR2 ) RETURN CLOB
IS
   key_list_            VARCHAR2(32000) := CONCAT(selection_, ';');
   msg_                 CLOB;
   transport_task_id_   NUMBER;
   handling_unit_id_    NUMBER;  
  
BEGIN
   WHILE (INSTR(key_list_, ';') > 0) LOOP
      transport_task_id_ := Client_SYS.Get_Key_Reference_Value(key_list_, 'TRANSPORT_TASK_ID');
      Message_SYS.Add_Attribute(msg_, 'TRANSPORT_TASK_ID', transport_task_id_);
      handling_unit_id_ := Client_SYS.Get_Key_Reference_Value(key_list_, 'HANDLING_UNIT_ID');
      Message_SYS.Add_Attribute(msg_, 'HANDLING_UNIT_ID', handling_unit_id_);
      Transport_Task_Handling_Unit_Initial_Check___(transport_task_id_, handling_unit_id_);
      Message_SYS.Add_Attribute(msg_, 'FROM_CONTRACT', Client_SYS.Get_Key_Reference_Value(key_list_, 'FROM_CONTRACT'));
      Message_SYS.Add_Attribute(msg_, 'FROM_LOCATION_NO', Client_SYS.Get_Key_Reference_Value(key_list_, 'FROM_LOCATION_NO'));
      
      key_list_ := SUBSTR(key_list_, (INSTR(key_list_, ';') + 1));
   END LOOP;
   RETURN msg_;
END Create_Message_Attibute___;

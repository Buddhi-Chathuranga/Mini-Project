-----------------------------------------------------------------------------
--
--  Fragment:     ApplyPackingInstructionAssistant
--  Component:    INVENT
--
--  IFS Developer Studio Template Version 3.0
--
--  Date         Sign    History
--  ----------  ------  ---------------------------------------------------------
--  2018-12-13  ChFolk  SCUXXW4-4896, Created
-----------------------------------------------------------------------------

layer Core;

PROCEDURE Validate_Packing_Instruction___ (
   packing_instruction_id_  IN VARCHAR2 )
IS
BEGIN
   Packing_Instruction_API.Exist(packing_instruction_id_);
END Validate_Packing_Instruction___;  
  
@Override
PROCEDURE CRUD_Default___ (
   rec_  IN OUT Packing_Instructions_Virtual_Rec )
IS
BEGIN
   rec_.quantity := 1;
   super(rec_);
END;

FUNCTION Create_Handling_Unit_Structure___ (
   parent_objkey_ IN  VARCHAR2 ) RETURN VARCHAR2
IS
   handling_unit_id_       NUMBER;
   parent_key_             Apply_Packing_Instruction_Virtual_Key;
   parent_rec_             Apply_Packing_Instruction_Virtual_Rec;
   child_key_              Packing_Instructions_Virtual_Key;
   child_rec_              Packing_Instructions_Virtual_Rec;
   handling_unit_id_list_  VARCHAR2(32000);
BEGIN
   parent_key_.objkey := parent_objkey_;
   parent_rec_ := Get_Record___(parent_key_);
   -- Security
   $IF Component_Shpmnt_SYS.INSTALLED $THEN
   IF (parent_rec_.shipment_id IS NOT NULL) THEN
      Shipment_Initial_Check___(parent_rec_.shipment_id);
   END IF;     
   $END      
   FOR line_rec_ IN Get_Apply_Packing_Instruction_Array___(parent_key_) LOOP
      child_key_.objkey := line_rec_.objkey;
      child_rec_ := Get_Record___(child_key_);    
      IF (parent_rec_.parent_handling_unit_id IS NULL AND parent_rec_.shipment_id IS NOT NULL) THEN
         $IF Component_Shpmnt_SYS.INSTALLED $THEN
            Shipment_API.Add_Empty_Handl_Unit_Structure(parent_rec_.shipment_id, child_rec_.packing_instruction_id , child_rec_.quantity);
         $ELSE
            NULL;
         $END   
      ELSE
         FOR i_ IN 1..child_rec_.quantity LOOP
            Packing_Instruction_API.Create_Handling_Unit_Structure(handling_unit_id_ => handling_unit_id_,
                                                                   packing_instruction_id_ => child_rec_.packing_instruction_id,
                                                                   parent_handling_unit_id_ => parent_rec_.parent_handling_unit_id);
            IF (handling_unit_id_list_ IS NULL) THEN
               handling_unit_id_list_ := 'HandlingUnitId eq ' || handling_unit_id_ ;
            ELSE   
               handling_unit_id_list_ := handling_unit_id_list_ || ' or HandlingUnitId eq ' || handling_unit_id_;
            END IF;
         END LOOP;
      END IF;   
   END LOOP;

RETURN handling_unit_id_list_;

END Create_Handling_Unit_Structure___;
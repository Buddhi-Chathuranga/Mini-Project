-----------------------------------------------------------------------------
--
--  Logical unit: HandlingUnitsInStockHandling
--  Component:    INVENT
--
--  IFS Developer Studio Template Version 3.0
--
--  Date    Sign    History
--  ------  ------  ---------------------------------------------------------
--  2021-11-02  SBalLK  SC21R2-2833, Added warehouse_list parameter into the search context list.
--  2020-03-12  SBalLK  SCXTEND-3416, Modified Get_Handling_Unit_In_Stock_Page_Details___() method to filter according requested HU levels.
--  2019-05-31  JaThlk  SCUXXW4-21986, Removed concatenated WHERE clauses to avoid sql injections. 
--  2019-03-31  ChFolk  SCUXXW4-6121, Added methods Get_Handling_Unit_In_Stock_Page_Details___ and Generate_String_Where___ which supports page level filters in the list.
--  2019-03-19  ChFolk  SCUXXW4-6121, Created to get the default values for the page level search context. 
-----------------------------------------------------------------------------

layer Core;

FUNCTION Get_Hu_In_Stock_Page_Search_Context_Defaults___ RETURN Hu_In_Stock_Page_Search_Context_Structure_Rec
IS
   hu_in_stock_page_search_context_struct_    Hu_In_Stock_Page_Search_Context_Structure_Rec;
BEGIN
   hu_in_stock_page_search_context_struct_.contract_list := NULL;
   hu_in_stock_page_search_context_struct_.handling_unit_id_list := NULL;
   hu_in_stock_page_search_context_struct_.warehouse_list := NULL;
   RETURN hu_in_stock_page_search_context_struct_;
END Get_Hu_In_Stock_Page_Search_Context_Defaults___;

FUNCTION Get_Handling_Unit_In_Stock_Page_Details___ (
	show_hu_level_         IN VARCHAR2,
	contract_list_         IN VARCHAR2,
	handling_unit_id_list_ IN VARCHAR2,
   warehouse_list_        IN VARCHAR2 ) RETURN Objid_Arr
IS
   base_collection_           Objid_Arr := Objid_Arr();
   contract_list_in_where_    VARCHAR2(32000);
   hu_id_list_in_where_       VARCHAR2(32000);
   warehouse_list_in_where_   VARCHAR2(32000);
                             
   CURSOR get_handling_unit_record  IS
      SELECT objid 
      FROM HANDLING_UNIT_EXTENDED
      WHERE IS_IN_STOCK = 'TRUE' 
      AND ((show_hu_level_ = 'ShowOnlyTopLevel' AND parent_handling_unit_id IS NULL ) OR (show_hu_level_ = 'ShowAll'))
      AND (contract_list_in_where_ IS NULL OR CONTRACT IN ( SELECT REGEXP_SUBSTR(contract_list_in_where_,'[^,]+', 1, LEVEL) 
 	 		                                                   FROM dual
 	 		                                                   CONNECT BY REGEXP_SUBSTR(contract_list_in_where_, '[^,]+', 1, LEVEL) IS NOT NULL))
      AND (hu_id_list_in_where_ IS NULL OR HANDLING_UNIT_ID IN ( SELECT REGEXP_SUBSTR(hu_id_list_in_where_,'[^,]+', 1, LEVEL) 
 	 		                                                        FROM dual
 	 		                                                        CONNECT BY REGEXP_SUBSTR(hu_id_list_in_where_, '[^,]+', 1, LEVEL) IS NOT NULL))
      AND (warehouse_list_in_where_ IS NULL OR WAREHOUSE_ID IN ( SELECT REGEXP_SUBSTR(warehouse_list_in_where_,'[^,]+', 1, LEVEL) 
                                                                     FROM dual
                                                                     CONNECT BY REGEXP_SUBSTR(warehouse_list_in_where_, '[^,]+', 1, LEVEL) IS NOT NULL));

BEGIN
   IF (contract_list_ IS NOT NULL) THEN
      contract_list_in_where_ := Generate_String_Where___(contract_list_);
   END IF;
   IF (handling_unit_id_list_ IS NOT NULL) THEN    
      hu_id_list_in_where_    := REPLACE(handling_unit_id_list_, ';', ',');
   END IF;
   IF (warehouse_list_ IS NOT NULL ) THEN
      warehouse_list_in_where_ := Generate_String_Where___(warehouse_list_);
   END IF;

   FOR rec_ IN get_handling_unit_record LOOP
      base_collection_.extend;
      base_collection_(base_collection_.last) := rec_.objid;
   END LOOP;
   
   RETURN base_collection_;
END Get_Handling_Unit_In_Stock_Page_Details___;

FUNCTION Generate_String_Where___ (
   item_list_ IN VARCHAR2 ) RETURN VARCHAR2
IS
   current_item_list_    VARCHAR2(32000) := CONCAT(item_list_ , ';');
   item_list_in_where_   VARCHAR2(32000);
   current_item_         VARCHAR2(200);
BEGIN
   WHILE (INSTR(current_item_list_, ';') > 0) LOOP
      current_item_ := SUBSTR(current_item_list_, 0, INSTR(current_item_list_, ';')-1);
      IF (item_list_in_where_ IS NULL) THEN
         item_list_in_where_ := current_item_;
      ELSE
         item_list_in_where_ := item_list_in_where_ || ','||current_item_;
      END IF;
      current_item_list_ := SUBSTR(current_item_list_, (INSTR(current_item_list_, ';') + 1));
   END LOOP;
   RETURN item_list_in_where_;
END Generate_String_Where___;

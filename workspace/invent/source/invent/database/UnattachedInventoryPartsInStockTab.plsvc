-----------------------------------------------------------------------------
--
--  Fragment:     UnattachedInventoryPartsInStockTab
--  Component:    INVENT
--
--  IFS Developer Studio Template Version 3.0
--
--  Date        Sign    History
--  ----------  ------  ---------------------------------------------------------
--  2019-05-31  ChFolk  SCUXXW4-21984, Modified Get_Stocks_To_Attach_Objids___ to get objid_list_ to be matched with current changes.
--  2018-11-24  ChFolk  SCUXXW4-6832, Created 
-----------------------------------------------------------------------------

layer Core;

FUNCTION Get_Stocks_To_Attach_Objids___(
   selection_ IN VARCHAR2 ) RETURN VARCHAR2
IS
   key_list_             VARCHAR2(32000) := CONCAT(selection_, ';');
   current_selection_    VARCHAR2(32000);
   keys_                 Unattached_Inventory_Parts_In_Stock_Key;
   objid_                INVENTORY_PART_IN_STOCK.OBJID%TYPE;
   objversion_           INVENTORY_PART_IN_STOCK.OBJVERSION%TYPE;
   objid_list_           VARCHAR2(32000);
BEGIN
   WHILE (INSTR(key_list_, ';') > 0) LOOP
      current_selection_     := substr(key_list_, 0, INSTR(key_list_, ';'));
      keys_.contract         := Client_SYS.Get_Key_Reference_Value(current_selection_, 'CONTRACT');
      keys_.part_no          := Client_SYS.Get_Key_Reference_Value(current_selection_, 'PART_NO');
      keys_.configuration_id := Client_SYS.Get_Key_Reference_Value(current_selection_, 'CONFIGURATION_ID');
      keys_.location_no      := Client_SYS.Get_Key_Reference_Value(current_selection_, 'LOCATION_NO');
      keys_.lot_batch_no     := Client_SYS.Get_Key_Reference_Value(current_selection_, 'LOT_BATCH_NO');
      keys_.serial_no        := Client_SYS.Get_Key_Reference_Value(current_selection_, 'SERIAL_NO');      
      keys_.eng_chg_level    := Client_SYS.Get_Key_Reference_Value(current_selection_, 'ENG_CHG_LEVEL');
      keys_.waiv_dev_rej_no  := Client_SYS.Get_Key_Reference_Value(current_selection_, 'WAIV_DEV_REJ_NO');
      keys_.activity_seq     := Client_SYS.Attr_Value_To_Number(Client_SYS.Get_Key_Reference_Value(current_selection_, 'ACTIVITY_SEQ'));
      keys_.handling_unit_id := Client_SYS.Attr_Value_To_Number(Client_SYS.Get_Key_Reference_Value(current_selection_, 'HANDLING_UNIT_ID'));
      
      Inventory_Part_In_Stock_API.Get_Id_Version_By_Keys(objid_,
                                                         objversion_,
                                                         keys_.contract,
                                                         keys_.part_no,
                                                         keys_.configuration_id,
                                                         keys_.location_no,
                                                         keys_.lot_batch_no,
                                                         keys_.serial_no,
                                                         keys_.eng_chg_level,
                                                         keys_.waiv_dev_rej_no,
                                                         keys_.activity_seq,
                                                         keys_.handling_unit_id);

      IF (objid_list_ IS NULL) THEN
         objid_list_ := objid_;
      ELSE
         objid_list_ := objid_list_ || ',' ||objid_;
      END IF;
      key_list_ := SUBSTR(key_list_, (INSTR(key_list_, ';') + 1));
   END LOOP;   
   RETURN objid_list_;
END Get_Stocks_To_Attach_Objids___;

FUNCTION Get_Parent_Details___(
   selection_ IN VARCHAR2 )  RETURN Parent_Handling_Unit_Detail_Structure_Rec
IS
   current_selection_  VARCHAR2(2000);
   records_            json_array_t := json_array_t.parse(selection_);
   parent_hu_details_  Parent_Handling_Unit_Detail_Structure_Rec;
BEGIN
   -- it is enough to take only the first record as all of the records gets the same information
   FOR i_ IN 0 .. records_.get_size - 1 LOOP      
      current_selection_ := records_.get_string(i_);      
      parent_hu_details_.parent_handling_unit_id := Client_SYS.Attr_Value_To_Number(Client_SYS.Get_Key_Reference_Value(current_selection_, 'PARENT_HANDLING_UNIT_ID'));
      parent_hu_details_.parent_handling_unit_type_id := Client_SYS.Get_Key_Reference_Value(current_selection_, 'PARENT_HANDLING_UNIT_TYPE_ID');      
      EXIT;
   END LOOP;
   RETURN parent_hu_details_;
END Get_Parent_Details___;
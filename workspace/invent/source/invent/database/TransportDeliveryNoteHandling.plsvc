--------------------------------------------------------------------------------
-- Date        Sign    History
-- ----------  ------  ----------------------------------------------------------
-- 2020-12-09  DiJwlk  SC2020R1-11750, Modified Get_Transport_Delivery_Note_Result_Key___(), combined with deleted method Modify_Date_Applied___()
-- 2020-12-09          Included TransportDeliveryNote state change action Print__() which is removed from client
-- 2020-11-20  JaThlk  GESPRING20-6009, Modified Get_Ship_Address_Information___ to fetch delivery_terms_and ship_via_code_.
-- 2020-07-16  WaSalk  gelr: Added to support Global Extension Functionalities.
---------------------------------------------------------------------------------
layer Core;
-- gelr:transport_delivery_note, begin
FUNCTION Get_Preview_Transport_Delivery_Note_Result_Key___ (
   delivery_note_id_ IN VARCHAR2) RETURN NUMBER
IS
   parameter_attr_       VARCHAR2(32000);
   report_attr_          VARCHAR2(1000);
   result_key_           NUMBER;
BEGIN
	Client_SYS.Clear_Attr(report_attr_);
   Client_SYS.Clear_Attr(parameter_attr_); 
   
   Client_SYS.Add_To_Attr('REPORT_ID', 'TRANSPORT_DELIVERY_NOTE_REP' , report_attr_); 
   Client_SYS.Add_To_Attr('DELIVERY_NOTE_ID', delivery_note_id_ , parameter_attr_); 
   Client_SYS.Add_To_Attr('PRELIMINARY' , 'Y', parameter_attr_);
   result_key_ := Report_Format_API.Create_New_Report(report_attr_, parameter_attr_, Fnd_Session_API.Get_Fnd_User);
   RETURN result_key_;
END Get_Preview_Transport_Delivery_Note_Result_Key___;

FUNCTION Get_Transport_Delivery_Note_Result_Key___ (
   delivery_note_id_    IN VARCHAR2,
   delivery_note_state_ IN VARCHAR2) RETURN NUMBER
IS
   parameter_attr_       VARCHAR2(32000);
   report_attr_          VARCHAR2(1000);
   result_key_           NUMBER;
   delivery_note_key_    Transport_Delivery_Note_Key;
   attr_                 VARCHAR2(2000);
   objid_                VARCHAR2(2000);
   objversion_           VARCHAR2(2000);
   info_                 VARCHAR2(2000);
BEGIN
   IF(delivery_note_state_ = 'Created') THEN
      delivery_note_key_.delivery_note_id := delivery_note_id_;
      Get_Objid_Objversion___(objid_, objversion_, delivery_note_key_);
      Transport_Delivery_Note_API.Print__(info_, objid_, objversion_, attr_, 'DO');
   END IF;
   
   Client_SYS.Clear_Attr(report_attr_);
   Client_SYS.Clear_Attr(parameter_attr_); 
   
   Client_SYS.Add_To_Attr('REPORT_ID', 'TRANSPORT_DELIVERY_NOTE_REP' , report_attr_); 
   Client_SYS.Add_To_Attr('DELIVERY_NOTE_ID', delivery_note_id_ , parameter_attr_); 
   Client_SYS.Add_To_Attr('PRELIMINARY' , 'N', parameter_attr_);
   result_key_ := Report_Format_API.Create_New_Report(report_attr_, parameter_attr_, Fnd_Session_API.Get_Fnd_User);
   
   IF(delivery_note_state_ = 'Created') THEN
      Transport_Delivery_Note_API.Modify_Date_Applied__(delivery_note_id_);
   END IF;
   
   RETURN result_key_;
END Get_Transport_Delivery_Note_Result_Key___;


FUNCTION Get_Ship_Address_Information___ (
   vendor_no_                 IN VARCHAR2,
   recipient_company_         IN VARCHAR2,
   single_occurrence_address_ IN BOOLEAN  ) RETURN Ship_Address_Infomation_Structure_Rec
IS
   rec_              Ship_Address_Infomation_Structure_Rec;
   sup_addr_rec_     Supplier_Info_Address_API.Public_Rec;
   company_addr_rec_ Company_Address_API.Public_Rec;
   delivery_terms_   VARCHAR2(5);
   ship_via_code_    VARCHAR2(3);
BEGIN
   IF (single_occurrence_address_ = FALSE ) THEN 
      IF (vendor_no_ IS NOT NULL)THEN
         rec_.ship_address_no    := Supplier_Info_Address_API.Get_Default_Address(vendor_no_, Address_Type_Code_API.Decode('DELIVERY'));
         sup_addr_rec_          := Supplier_Info_Address_API.Get(vendor_no_, rec_.ship_address_no);   
         $IF Component_Purch_SYS.INSTALLED $THEN
            rec_.ship_address_name := Supplier_API.Get_Vendor_Name(vendor_no_);
         $END 
         rec_.ship_address1     := sup_addr_rec_.address1;
         rec_.ship_address2     := sup_addr_rec_.address2;
         rec_.ship_city         := sup_addr_rec_.city;
         rec_.ship_county       := sup_addr_rec_.county;
         rec_.ship_state        := sup_addr_rec_.state;
         rec_.Ship_zip_code     := sup_addr_rec_.zip_code;
         rec_.ship_country_code := sup_addr_rec_.country;
      END IF; 

      IF (recipient_company_ IS NOT NULL)THEN
         rec_.ship_address_no   := Company_Address_API.Get_Default_Address(recipient_company_, Address_Type_Code_API.Decode('DELIVERY'));
         company_addr_rec_      := Company_Address_API.Get(recipient_company_, rec_.ship_address_no);
         rec_.ship_address_name := Company_API.Get_Name(recipient_company_);
         rec_.ship_address1     := company_addr_rec_.address1;
         rec_.ship_address2     := company_addr_rec_.address2;
         rec_.ship_city         := company_addr_rec_.city;
         rec_.ship_county       := company_addr_rec_.county;
         rec_.ship_state        := company_addr_rec_.state;
         rec_.Ship_zip_code     := company_addr_rec_.zip_code;
         rec_.ship_country_code := company_addr_rec_.country;
      END IF; 
      rec_.ship_country := Iso_Country_API.Get_Description(rec_.ship_country_code);
   END IF;
   IF (recipient_company_ IS NOT NULL) THEN
      delivery_terms_ := Company_Address_Deliv_Info_API.Get_Delivery_Terms(recipient_company_, rec_.ship_address_no);
      ship_via_code_ := Company_Address_Deliv_Info_API.Get_Ship_Via_Code(recipient_company_, rec_.ship_address_no);
   $IF Component_Purch_SYS.INSTALLED $THEN
   ELSIF (vendor_no_ IS NOT NULL) THEN
      Supplier_Address_API.Get_Delivery_Terms_Ship_Via(delivery_terms_, ship_via_code_, vendor_no_, rec_.ship_address_no);
   $END   
   END IF;
   rec_.delivery_terms := delivery_terms_;
   rec_.ship_via_code  := ship_via_code_;
   
   RETURN rec_;
END Get_Ship_Address_Information___;
-- gelr:transport_delivery_note, end


FUNCTION Get_Contract_Information___ (
   contract_ IN VARCHAR2) RETURN Contract_Infomation_Structure_Rec
IS
   rec_  Contract_Infomation_Structure_Rec;
BEGIN

	rec_.company := Site_API.Get_Company(contract_);
   rec_.delivery_address_no   := Site_API.Get_Delivery_Address(contract_);
   rec_.delivery_from_address := Site_API.Get_Description(contract_);
   RETURN rec_;
END Get_Contract_Information___;

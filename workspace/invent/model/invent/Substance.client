----------------------------------------------------------------------------------------------------------
-- Date        Sign    History
-- ----------  ------  -----------------------------------------------------------------------------------
-- 2019-11-04  NiAslk  SCXTEND-1228, Added startup mode as search.
-- 2019-06-27  JaThlk  SCUXXW4-21587, Added the Note in the header.
-- 2019-01-03  MaAtlk  MFUXXW4-3799, Added Environmentel Properties.
-- 2018-12-28  AmPalk  MFUXXW4-131, Added Chemical Properties group.
-- 2018-04-03  ChFolk  SCUXXW4-6487, Converted from frmSubstance using MTG Version: 1.15
----------------------------------------------------------------------------------------------------------

client Substance;
component INVENT;
layer Core;
projection SubstanceHandling;
@DynamicComponentDependency ECOMAN
include fragment EmissionSubstanceCodes;
----------------------------- NAVIGATOR ENTRIES -----------------------------
navigator {
   entry SubstancesNavEntry parent InventNavigator.PartMasterDataSubstance at index 100 {
      label = "Substance";
      page Form home Substance;
   }
}
-------------------------------- MAIN PAGES ---------------------------------
page Form using Substances {
   label = "Substance";
   startupmode = search;
   selector SubstanceSelector;
   group SubstanceGroup;

   @DynamicComponentDependency MFGSTD
   singleton ChemicalSubstanceSingleton(ChemicalSubstanceArray);
   @DynamicComponentDependency MFGSTD
   group ChemicalSubstanceGroup bind ChemicalSubstanceSingleton;

   @DynamicComponentDependency ECOMAN
   singleton EmissionSubstanceCodeSingleton(EmissionSubstanceCodeArray);
   @DynamicComponentDependency ECOMAN
   group SubstanceCodesHeadGroup bind EmissionSubstanceCodeSingleton;
   arrange {
      @DynamicComponentDependency ECOMAN
      group HazardousCriteriaGroup bind EmissionSubstanceCodeSingleton;
      @DynamicComponentDependency ECOMAN
      group EnvironmentalCriteriaGroup bind EmissionSubstanceCodeSingleton;
      @DynamicComponentDependency ECOMAN
      group EquivalenceFactorsGroup bind EmissionSubstanceCodeSingleton;
      @DynamicComponentDependency ECOMAN
      group EnergyGroup bind EmissionSubstanceCodeSingleton;
   }

   commandgroup SubstanceCmdGroup {

      command Translation;
   }
}

--------------------------------- COMMANDS ----------------------------------
command Translation for Substance {
   label = "Translation";
   mode = Global;
   enabled = [true];
   mode = SelectedRecords;
   execute {
      navigate "page/BasicDataTranslation/Form?$filter=Module eq 'INVENT' and Lu eq 'Substance'";
   }
}

--------------------------------- SELECTORS ---------------------------------
@Override
selector SubstanceSelector for Substance {
   label = "${SubstanceNo} - ${Description} - ${CasNo}";
   static SubstanceNo;
   static Description;
   static CasNo;
}

---------------------------------- GROUPS -----------------------------------

group SubstanceGroup for Substance {
   label = "";
   field SubstanceNo;
   field Description {
      size = Large;
   }
   field CasNo {
      size = Small;
   }
   field EuIndexNo;
   field FieldOfApplication {
      size = Large;
   }
   field Notes {
      size = Medium;
      multiline = true;
   }
}



@DynamicComponentDependency MFGSTD
group ChemicalSubstanceGroup for ChemicalSubstance {
   label = "Chemical Properties";
   field Density {
      size = Small;
      required = [SubstanceTypeDb = "SOLVENT" or SubstanceTypeDb = "WATER"];
   }
   lov UomForDensityRef with ReferenceForIsoUnitDensityLovSelector using UomDensityFilter {
      description = UomForDensityRef.Description;
   }
   field SubstanceType {
      size = Small;
      validate command {
         execute {
            if [SubstanceTypeDb = "SOLVENT" or SubstanceTypeDb = "WATER"] {
               set VolatileDb = true;
            }
            else {
               set VolatileDb = false;
            }
            set ColorPigmentDb = false;
         }
      }
   }
   field OilAbsorption {
      size = Small;
   }
   field SolventNonVocEuDb {
      size = Small;
      //visible = [SubstanceTypeDb = "SOLVENT"];
   }
   field ColorPigmentDb {
      size = Small;
      //visible = [SubstanceTypeDb = "PIGMENT"];
   }
   field VolatileDb {
      size = Small;
   }
   field Notes {
      size = Medium;
      multiline = true;
   }
}

--------------------------------- SINGLETON ---------------------------------
@DynamicComponentDependency MFGSTD
singleton ChemicalSubstanceSingleton for ChemicalSubstance {
   crudactions {
      new {
         enabled = [parent.SubstanceNo != null];
      }
      delete {
         enabled = [parent.SubstanceNo != null];
      }
      edit {
         enabled = [parent.SubstanceNo != null];
      }
   }
}

--------------------------------- SELECTORS ---------------------------------
@Override
selector ReferenceForIsoUnitDensityLovSelector for ForIsoUnitDensityLov {
   static UnitCode;
   static Description;
}
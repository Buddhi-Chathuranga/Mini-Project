-----------------------------------------------------------------------------
-- Date        Sign    History
-- ----------  ------  ------------------------------------------------------
-- 2019-08-05  ChFolk  SCUXXW4-23465, Moved from InventoryPartUnitCost.
-----------------------------------------------------------------------------
fragment ModifyLevelCostDetailAssistant;
component INVENT;
layer Core;
description "Assistant which is used to modify level cost details";

@DynamicComponentDependency COST
include fragment CostBucketInventLovSelector;
include fragment InventoryPartInitialCheck;

----------------------------- CLIENT FRAGMENTS ------------------------------
-------------------------------- ASSISTANTS ---------------------------------
assistant ModifyLevelCostDetailAssistant using ModifyLevelCostDetailSet {
   label = "Modify Level Cost Details";
   input(PartNo, Contract, ConfigurationId, ConditionCode, LotBatchNo, SerialNo, UnitCost);
   init command {
      variable TempStruct;
      execute {
         call GetDetails(Contract, PartNo, ConfigurationId, LotBatchNo, SerialNo, ConditionCode) into TempStruct;
         set PartDescription = "${PartNo} - ${TempStruct.PartDescription}";
         set ConditionCodeDescription = "${ConditionCode} - ${TempStruct.ConditionCodeDescription}";
         set InventoryPartCostLevel = TempStruct.InventoryPartCostLevel;
         set Company = TempStruct.Company;
         set CostDetailId = TempStruct.CostDetailId;
      }
   }

   steps {
      step {
         group ModifyLevelCostDetailGroup;
         list CostDetailList(CostDetailArray);
      }
      final step {
      }
      cancelled step {
      }
   }
   finish command {
      execute {
         call ModifyLevelCost(Contract, PartNo, ConfigurationId, LotBatchNo, SerialNo, ConditionCode, CostDetailId);
         exit OK;
      }
   }
   cancel command {
      execute {
         call RemoveCostDetails(CostDetailId);
         exit CANCEL;
      }
   }
   restart command {
      visible = [false];
   }
}

---------------------------------- GROUPS -----------------------------------
group ModifyLevelCostDetailGroup for ModifyLevelCostDetailVirtual {
   label = "";
   field PartDescription {
      label = "Part No";
      editable = [false];
   }
   field Contract {
      editable = [false];
   }
   field ConfigurationId {
      editable = [false];
   }
   field ConditionCodeDescription {
      label = "Condition Code";
      editable = [false];
   }
   field LotBatchNo {
      editable = [false];
   }
   field SerialNo {
      editable = [false];
   }
   field InventoryPartCostLevel {
      editable = [false];
   }
   computedfield UnitCost {
      value = "#{CostDetailArray.sum(UnitCost)}";
   }
}

----------------------------------- LISTS -----------------------------------
list CostDetailList for TemporaryPartCostDetail {
   editmode = SingleCellEdit;
   label = "Cost Details";

   @DynamicComponentDependency COST
   lov CostBucketIdRef with ReferenceCostBucketInventLovSelector {
      validate command {
         execute {
            call GetAccountingYear(Company, Contract, PartNo, CostBucketId) into AccountingYear;
         }
      }
   }
   field CostSourceIdRef;
   field AccountingYear;
   field UnitCost;

}


--------------------------- PROJECTION FRAGMENTS ----------------------------
----------------------------- MAIN ENTRY POINTS -----------------------------
entityset ModifyLevelCostDetailSet for ModifyLevelCostDetailVirtual;

------------------------------ ENTITY DETAILS -------------------------------
@Override
entity TemporaryPartCostDetail {
//   The fields are should not be editable in client, but they are required and are fetched automatically. Therefore, made them required false.
   attribute CostBucketId Text {
      required = [true];
      editable = [ETag = null];
   }
   attribute CostSourceId Text {
      required = [true];
      editable = [ETag = null];
   }
   attribute AccountingYear Text {
      required = [true];
      editable = [ETag = null];
   }
   attribute UnitCost Number {
      required = [false];
      format = ifscurrency;
   }
   attribute Contract Text {
      required = [false];
   }
   attribute Company Text {
      required = [false];
   }
   attribute PartNo Text {
      required = [false];
   }
   @DynamicComponentDependency COST
   reference CostBucketIdRef(Contract, CostBucketId) to CostBucketInventLov(Contract, CostBucketId) {
      label = "Cost Bucket ID";
   }
   reference CostSourceIdRef(Company, CostSourceId) to CostSource(Company, CostSourceId) {
      label = "Cost Source ID";
   }
}

--------------------------------- VIRTUALS ----------------------------------
virtual ModifyLevelCostDetailVirtual {
   attribute PartNo Text {
      label = "Part No";
      maxlength = 25;
      format = uppercase;
   }
   attribute PartDescription Text {
      label = "Description";
      maxlength = 200;
   }
   attribute Contract Text {
      label = "Site";
      maxlength = 5;
      format = uppercase;
   }
   attribute ConfigurationId Text {
      label = "Configuration ID";
      maxlength = 50;
   }
   attribute LotBatchNo Text {
      label = "Lot/Batch No";
      maxlength = 20;
      format = uppercase;
   }
   attribute SerialNo Text {
      label = "Serial No";
      maxlength = 50;
      format = uppercase;
   }
   attribute ConditionCode Text {
      label = "Condition Code";
      maxlength = 100;
   }
   attribute ConditionCodeDescription Text {
      label = "Condition Code Description";
      maxlength = 100;
   }
   attribute OldCost Number {
      label = "Old Level Cost";
      format = ifscurrency;
   }
   attribute UnitCost Number {
      label = "New Level Cost";
      format = ifscurrency;
   }
   attribute InventoryPartCostLevel Text {
      label = "Cost Level";
      maxlength = 2000;
   }
   attribute CostDetailId Number {
      maxlength = 100;
   }
   attribute Company Text;

   array CostDetailArray(CostDetailId, Contract, PartNo, Company) to TemporaryPartCostDetail(CostDetailId, Contract, PartNo, Company);
}

---------------------------------- ACTIONS ----------------------------------
action GetDetails Structure(GetModifyLevelDetailStruct) {
   initialcheck UserAllowedSite(Contract);
   parameter Contract Text;
   parameter PartNo Text;
   parameter ConfigurationId Text;
   parameter LotBatchNo Text;
   parameter SerialNo Text;
   parameter ConditionCode Text;
}

action ModifyLevelCost {
   initialcheck InventoryPartInitialCheck(Contract, PartNo);
   ludependencies = InventoryPartConfig, InventoryPartUnitCost;

   parameter Contract Text;
   parameter PartNo Text;
   parameter ConfigurationId Text;
   parameter LotBatchNo Text;
   parameter SerialNo Text;
   parameter ConditionCode Text;
   parameter CostDetailId Number;
}

action RemoveCostDetails {
   initialcheck none;
   parameter CostDetailId Number;
}


--------------------------------- FUNCTIONS ---------------------------------
function GetAccountingYear Text {
   parameter Company Text;
   parameter Contract Text;
   parameter PartNo Text;
   parameter CostBucketId Text;
}

-------------------------------- STRUCTURES ---------------------------------
structure GetModifyLevelDetailStruct {
   attribute OldCost Number {
      label = "Old Level Cost";
      format = ifscurrency;
   }
   attribute PartDescription Text {
      label = "Description";
      maxlength = 200;
   }
   attribute ConditionCodeDescription Text {
      label = "Description";
      maxlength = 100;
   }
   attribute InventoryPartCostLevel Text {
      label = "Cost Level";
      maxlength = 2000;
   }
   attribute Company Text;
   attribute CostDetailId Number;
}

-------------------------------------------------------------------------------------------------------
-- Date        Sign    History
-- ----------  ------  --------------------------------------------------------------------------------
-- 2019-05-16  DaZase  SCUXXW4-19624, Added initialcheck to all unbounded actions.
-- 2019-04-04  milise  SCUXXW4-18022, Replacing of label = "" with showlabel = false
-- 2019-01-25  ChFolk  SCUXXW4-6101, Created the fragment to support creating new handling unit either from given handling unit type or packing instruction.
-------------------------------------------------------------------------------------------------------
fragment CreateNewHandlingUnitAssistant;
component INVENT;
layer Core;
description "Assistant use to create new Handling Unit either from given Handling Unit Type or Packing Instructions";

include fragment AddHandlingUnitEnumeration;

----------------------------- CLIENT FRAGMENTS ------------------------------
-------------------------------- ASSISTANTS ---------------------------------
assistant CreateNewHandlingUnitAssistant using CreateNewHandlingUnitSet {
   label = "Create New Handling Unit";
   savemode = OnLostFocus;
   output(HandlingUnitIdList);

   init command {
      execute {
         set AddHandlingUnitOption = "UsingHuType";
      }
   }
   steps {
      step {
         group CreateNewHandlingUnitOptionGroup;
         group CreateNewHandlingUnitGroup {
            visible = [AddHandlingUnitOption = "UsingHuType"];
         }
         list PackingInstructionForNewHuList(PackingInstructionForNewHuArray) {
            visible = [AddHandlingUnitOption = "UsingPackingInstruction"];
         }
      }
      final step {

      }
      cancelled step {

      }
   }
   finish command {
      enabled = [(HandlingUnitTypeId != null) or(component.PackingInstructionForNewHuList.RowCount > 0)];
      variable HandlingUnitIdListVar Text;

      execute {
         call CreateNewHandlingUnit(HandlingUnitTypeId, AltHandlingUnitLabelId, Sscc, AddHandlingUnitOption, Objkey) into HandlingUnitIdList;
         exit OK;
      }
   }
   cancel command {
      execute {
         exit CANCEL;
      }
   }
   restart command {
      visible = [false];
   }
}

---------------------------------- GROUPS -----------------------------------
group CreateNewHandlingUnitOptionGroup for CreateNewHandlingUnitVirtual {
   label = "";
   radiogroup AddHandlingUnitOption {
      showlabel = false;
      size = FullWidth;
      validate command {
         execute {
            if [AddHandlingUnitOption = "UsingPackingInstruction"] {
               set AltHandlingUnitLabelId = null;
               set HandlingUnitTypeId = null;
               set Sscc = null;
            }
            if [AddHandlingUnitOption = "UsingHuType" and component.PackingInstructionForNewHuList.RowCount > 0] {
               call ClearPackingDetails(Objkey);
            }
         }
      }
   }
}

group CreateNewHandlingUnitGroup for CreateNewHandlingUnitVirtual {
   label = "";

   field HandlingUnitIdList {
      visible = [false];
   }
   lov HandlingUnitTypeIdRef with ReferenceHandlingUnitTypeSelector {
      label = "Handling Unit Type";
      description = HandlingUnitTypeIdRef.Description;
   }
   field AltHandlingUnitLabelId {
      label = "Alt Handling Unit Label ID";
   }
   field Sscc {
      label = "SSCC";
   }
}

----------------------------------- LISTS -----------------------------------
list PackingInstructionForNewHuList for PackingInstructionForNewHuVirtual {
   label = "";
   lov PackingInstructionIdRef with ReferencePackingInstructionWithRootSelector {
      label = "Packing Instruction";
      description = PackingInstructionIdRef.PackingInstructionDesc;
      validate command {
         execute {
            call ValidatePackingInstruction(PackingInstructionId);
         }
      }
   }
   field Quantity {
      size = Small;
   }
}

--------------------------------- COMMANDS ----------------------------------

--------------------------- PROJECTION FRAGMENTS ----------------------------
----------------------------- MAIN ENTRY POINTS -----------------------------
entityset CreateNewHandlingUnitSet for CreateNewHandlingUnitVirtual;

---------------------------------- ACTIONS ----------------------------------
action CreateNewHandlingUnit Text {
   initialcheck none;
   supportwarnings = [true];
   parameter HandlingUnitTypeId Text;
   parameter AltHandlingUnitLabelId Text;
   parameter Sscc Text;
   parameter AddHandlingUnitOption Text;
   parameter ParentObjkey Text;
}

action ClearPackingDetails {
   initialcheck none;
   parameter ParentObjkey Text;
   ludependencies = PackingInstructionForNewHuVirtual;
}

--------------------------------- FUNCTIONS ---------------------------------

-------------------------------- STRUCTURES ---------------------------------

------------------------------- ENUMERATIONS --------------------------------

--------------------------------- VIRTUALS ----------------------------------
virtual CreateNewHandlingUnitVirtual {
   crud = Create, Update, Delete;
   attribute AddHandlingUnitOption Enumeration(AddHandlingUnitOptionEnum);
   attribute HandlingUnitIdList Text;
   attribute HandlingUnitTypeId Text {
      format = uppercase;
      required = [true];
   }
   attribute AltHandlingUnitLabelId Text;
   attribute Sscc Text;
   array PackingInstructionForNewHuArray() to PackingInstructionForNewHuVirtual();

   reference HandlingUnitTypeIdRef(HandlingUnitTypeId) to HandlingUnitType(HandlingUnitTypeId);
}

virtual PackingInstructionForNewHuVirtual {
   crud = Create, Update, Delete;
   attribute PackingInstructionId Text {
      maxlength = 50;
      required = [true];
      format = uppercase;
   }
   attribute Quantity Number {
      label = "Quantity";
      editable = [true];
   }
   reference PackingInstructionIdRef(PackingInstructionId) to PackingInstructionWithRoot(PackingInstructionId);
}



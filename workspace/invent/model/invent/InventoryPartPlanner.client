--------------------------------------------------------------------------------------
-- Date        Sign    History
-- ----------  ------  ---------------------------------------------------------------
-- 2020-12-30  BudKLk  SC2020R1-11932, Replaced depreciated property personwidget to contactwidget.
-- 2018-04-09  SURBLK  SCUXXW4-6880, Converted from tbwInvBuyerList using MTG Version: 1.15
--------------------------------------------------------------------------------------

client InventoryPartPlanner;
component INVENT;
layer Core;
projection InventoryPartPlannerHandling;
include fragment InventoryPartPlannerLovSelector;

----------------------------- NAVIGATOR ENTRIES -----------------------------
navigator {
   entry InventoryPartPlannersNavEntry parent MpccomNavigator.WarehouseManagementBasicData at index 1 {
      label = "Inventory Part Planners";
      page List home InventoryPartPlanner;
   }
}

-------------------------------- MAIN PAGES ---------------------------------
page List using InventoryPartPlanners {
   label = "Inventory Part Planners";
   list InventoryPartPlannersList;
}

--------------------------------- COMMANDS ----------------------------------
@Override
command SetBlocked for InventoryPartPlanner {
   enabled = [Objstate in("Active", "Hidden")];
   variable IsPlannerExistStr;

   execute {
      if [Objstate = "Active"] {
         call ExistAsDefaultPlanner(BuyerCode) into IsPlannerExistStr;
         if [IsPlannerExistStr = "TRUE"] {
            dialog UpdateDefaultPlannerDialog into(PlannerBuyer) {
               when OK {
                  if [PlannerBuyer != null] {
                     if [BuyerCode != PlannerBuyer] {
                        call ModifyDefaultPlanner(PlannerBuyer);
                        call SetBlocked();
                        success("Updated ${PlannerBuyer} as default planner ID for all users.");
                     }
                     else {
                        info("A different Planner ID should be entered.");
                     }
                  }
               }
               when CANCEL {
                  exit;
               }
            }
         }
         else {
            call SetBlocked();
         }
      }
      else {
         call SetBlocked();
      }
      refresh;
   }
}

----------------------------- VISUAL COMPONENTS -----------------------------
list InventoryPartPlannersList for InventoryPartPlanner {
   field BuyerCodeRef {
      contactwidget {
         enabled = [BuyerCode != ""];
         source = Person;
      }
      validate command {
         execute {
            call GetBuyerExtension(BuyerCode) into PhoneNo;
         }
      }
   }
   field PersonInfoRef.Name {
      label = "Name";
   }
   field BuyerTitle;
   field PhoneNo;
   field Objstate {
      label = "Validity";
   }

   commandgroup DataValidityCmdGroup {
      label = "Validity";
      command SetActive;
      command SetHidden;
      command SetBlocked;
   }
}

---------------------------------- DIALOGS ----------------------------------
-- dlgUpdateDefaultPlanner being converted
dialog UpdateDefaultPlannerDialog for StructureUpdateDefaultPlanner {
   label = "Update Default Planner";
   output(PlannerBuyer);

   group UpdateDefaultPlannerGroup;
   markdowntext {
      text = "Note The value entered here will be used to update the default planner ID for all users, and/or the default planner for engineering transfers for all sites, whose current default value is the same as the one you have selected to be blocked for use.";
   }

   commandgroup UpdateDefaultPlannerCmdGroup {
      label = "Update Default Planner";
      command Ok;
      command Cancel;
   }
}

group UpdateDefaultPlannerGroup for StructureUpdateDefaultPlanner {
   label = "";

   lov PlannerBuyerRef with ReferenceInventoryPartPlannerLovSelector using GetActivePlanners {
      label = "Default Planner ID";
   }
}

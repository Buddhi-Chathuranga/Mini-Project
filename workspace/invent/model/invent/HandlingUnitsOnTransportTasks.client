---------------------------------------------------------------------------------
-- Date        Sign    History
-- ----------  ------  ----------------------------------------------------------
-- 2019-11-13  FRBESE  SCXTEND-1158, Changed HandlingUnitsOnTransportTasksNavEntry navigator entry index from 660 to 800.
-- 2019-04-24  YasMlk  SCUXXW4-14969, Changed enable condition in NewTransportTaskCommand.
-- 2019-04-10  ChFolk  SCUXXW4-18424, Added OrderBy to HandlingUnitOnTransportTaskList.
-- 2019-04-05  ChFolk  SCUXXW4-18830, Added commands to HandlingUnitOnTransportTaskList which was missed while moving to fragment and using clone feature.
-- 2018-12-17  ChFolk  SCUXXW4-6124, Added HandlingUnitOnTransportTaskList cloned from HandlingUnitsList and
-- 2018-12-17          TransportTasklinesOnHandlingUnitsList cloned from TransportTaskLineList.
-- 2018-12-11  Sudelk  SCUXXW4-6124,  Binded the TransportTaskLinesList to the parent List.
-- 2018-12-01  ChFolk  SCUXXW4-14695, Renamed the HandlingUnitsOnTransportTasksSearchContext to be used more generic names.
-- 2018-07-09  ChFolk  SCUXXW4-6502, Moved the common code to the new fragment HandlingUnitsOnTransportTasksList.
-- 2018-05-28  MWerse  SCUXXW4-6124, Converted from frmHandlingUnitsOnTransportTasks using MTG Version: 1.18
---------------------------------------------------------------------------------

client HandlingUnitsOnTransportTasks;
component INVENT;
layer Core;
projection HandlingUnitsOnTransportTasksHandling;

include fragment HandlingUnitsOnTransportTasksList;
include fragment NewTransportTaskDialog;
include fragment HandlingUnitsSearchContext;

----------------------------- NAVIGATOR ENTRIES -----------------------------
navigator {
   entry HandlingUnitsOnTransportTasksNavEntry parent InventNavigator.WarehouseManagementPartHandling at index 800 {
      label = "Handling Units on Transport Tasks";
      page Form;
   }
}

-------------------------------- MAIN PAGES ---------------------------------
page Form using HandlingUnitStockSnapshots {
   label = "Handling Units on Transport Tasks";
   list HandlingUnitOnTransportTaskList using GetHandlingUnitsOnTransportTask(HandlingUnitsSearchContext.ShowHuLevel) {
      label = "";
      searchcontext HandlingUnitsSearchContext {
         defaults = GetHandlingUnitsSearchContextDefaults();
      }
   }
   list TransportTasklinesOnHandlingUnitsList(HandlingUnintsOnTransportTaskArray)bind HandlingUnitOnTransportTaskList {
      label = "";
      display = Nested;
   }
}

----------------------------------- LISTS -----------------------------------

list HandlingUnitOnTransportTaskList clone HandlingUnitsList {
   orderby = TransportTaskId;

   field CreatedLinesExist {
      columnexclude = [true];
   }
   field PickedLinesExist {
      columnexclude = [true];
   }
   command ExecuteCommand;
   command PickCommand;
   command UnpickCommand;
   command NewTransportTaskCommand;
   command TransportTaskCommand;
}

list TransportTasklinesOnHandlingUnitsList clone TransportTaskLineList {

}

--------------------------------- COMMANDS ----------------------------------
command ExecuteCommand for HandlUnitStockSnapshot {
   label = "Execute";
   mode = SelectedRecords;
   enabled = [(count(*, CreatedLinesExist) > 0) or(count(*, PickedLinesExist) > 0)];
   bulkexecute {
      bulkcall Execute(TransportTaskId, HandlingUnitId);
   }
}

command PickCommand for HandlUnitStockSnapshot {
   label = "Pick";
   mode = SelectedRecords;
   enabled = [count(*, CreatedLinesExist) > 0];
   bulkexecute {
      bulkcall Pick(TransportTaskId, HandlingUnitId);
   }
}

command UnpickCommand for HandlUnitStockSnapshot {
   label = "Unpick";
   mode = SelectedRecords;
   enabled = [count(*, PickedLinesExist) > 0];
   bulkexecute {
      bulkcall Unpick(TransportTaskId, HandlingUnitId);
   }
}

command NewTransportTaskCommand for HandlUnitStockSnapshot {
   label = "Move to New Transport Task";
   mode = SelectedRecords;
   enabled = [((count(*, PickedLinesExist) = 0) and(count(*, CreatedLinesExist) > 0)) and not(isDirty)];
   variable NewTransportTaskId;
   variable ResultKeysStr;
   bulkexecute {
      call GetNewTransportTaskId() into NewTransportTaskId;
      if [NewTransportTaskId != null] {
         dialog MoveToNewTransportTaskDialog(NewTransportTaskId) into(SetToFixed, PrintTransportTask, OpenTransportTask) {
            when OK {
               call MoveHuToNewTransportTask(NewTransportTaskId, SetToFixed, PrintTransportTask, Selection) into ResultKeysStr;
               if [PrintTransportTask] {
                  printdialog ResultKeysStr;
               }
               if [OpenTransportTask] {
                  navigate "page/TransportTask/List?$filter=TransportTaskId eq $[NewTransportTaskId]";
               }
            }
         }
      }
   }
}

command TransportTaskCommand for HandlUnitStockSnapshot {
   label = "Details";
   mode = SelectedRecords;
   enabled = [true];
   bulkexecute {
      bulknavigate "page/TransportTask/Form?$filter=TransportTaskId eq $[TransportTaskId]";
   }
}

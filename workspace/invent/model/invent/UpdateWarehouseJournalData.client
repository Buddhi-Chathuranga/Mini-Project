--  Date    Sign    History
--  ------  ------  ---------------------------------------------------------
--  210817   KiSalk  SCZ-15946, In the finish command, the value assigned to ReturnInfo was made translatable, because it is displayed in final step.
--  200217   DiHelk  GESPRING20-1803, Generate Warehouse Journal Data
-----------------------------------------------------------------------------
client UpdateWarehouseJournalData;
component INVENT;
layer Core;

projection UpdateWarehouseJournalDataHandling;

include fragment ScheduledTasksCommon;

--------------------------------- FRAGMENTS ---------------------------------

----------------------------- NAVIGATOR ENTRIES -----------------------------
-- gelr: warehouse_journal, begin
navigator {
   entry UpdWarehouseJournalDataNav parent InventNavigator.WarehouseJournalMenu at index 200 {
      label = "Update Warehouse Journal Data";
      assistant Assistant;
   }
}

-------------------------------- ASSISTANTS ---------------------------------
assistant Assistant using TaskVirtualSet {
   label = "Update Warehouse Journal Data";
   savemode = OnLostFocus;

   steps {
      step {
         label = "Parameters";
         group ProcessOptionGroup;
         group CriteriaGroup;
         next command {
            enabled = [ProcessOption = "Schedule" and Company != null];
         }
      }
      step {
         label = "Schedule";
         group SchedulingParametersGroup;
         group SchedulingStartAndStopDateGroup;
      }
      step {
         label = "Schedule Options";
         group ScheduledTasksAssistantAdvOpGroup;
         group ScheduledTasksAssistantStreamsGroup;
      }

      final step {
         markdowntext {
            text = "${ReturnInfo}";
         }
         command NavigateToScheduledTask {
            visible = [ProcessOption = "Schedule"];
         }

         command NavigateWarehouseJournalData {
            visible = [ProcessOption = "Online"];
            execute {
               navigate "page/WarehouseJournalData/List?$filter=Company eq '${Company}'";
            }
         }
      }

   }
   finish command {
      enabled = [((component.Assistant.ActiveStep = 1 and ProcessOption = "Online")
      or((component.Assistant.ActiveStep = 2)or(component.Assistant.ActiveStep = 3) and ProcessOption = "Schedule"))and Company != null];

      execute {
         if [ProcessOption = "Online"] {
            call GenerateJournalData();
            set ReturnInfo = "Warehouse journal data updated successfully for company ${Company}." {
               translatable = true;
            }

         }
         else {
            call ScheduleTask() into ScheduledTask;
            set ScheduleId = ScheduledTask.ScheduleId;
            set ReturnInfo = "Task '${ScheduleName}', assigned with the Id '${ScheduleId}', was successfully scheduled to start ${ScheduledTask.ScheduledDate} at ${ScheduledTask.ScheduledTime}." {
               translatable = true;
            }
         }
      }
   }
   cancel command {
      execute {
         navigate back;
      }
   }
}
--------------------------------- COMMANDS ----------------------------------
command NavigateWarehouseJournalData {
   label = "View Warehouse Journal Data";
}

---------------------------------- GROUPS -----------------------------------
group CriteriaGroup for TaskVirtual {
   lov CompanyRef with ReferenceCompanyFinanceSelector {
      description = CompanyRef.Name;
   }
}

group ProcessOptionGroup for TaskVirtual {
   label = "";
   radiogroup ProcessOption {
      validate command {
         execute {
            if [ProcessOption = "Schedule"] {
               call GetParameters() into Params;
               set ScheduleMethodId = Params.ScheduleMethodId;
               set ScheduleName = Params.Description;
            }
         }
      }
   }
}
-- gelr: warehouse_journal, end


----------------------------------------------------------------------------------------------------------
-- Date        Sign    History
-- ----------  ------  -----------------------------------------------------------------------------------
-- 2019-05-29  fandse  SCUXXW4-14435, SystemShortageFlag is loaded only once via initcontext
-- 2019-03-19  fandse  SCUXXW4-15907, State indicator and badge material requisition.
-- 2019-02-14  milise  SCUXXW4-6340, Added Savmode Buffer.
-- 2019-01-09  fandse  SCUXXW4-6340, Changed to correct entity ShortageNotificationCommand from MaterialRequisLine to InventoryTransactionHist.
-- 2018-12-07  Sudelk  SCUXXW4-6340, Made fields Handling Uni Type ID, Program ID, Project ID,Sub Project ID and Activity Seq uneditable.
-- 2018-11-23  fandse  SCUXXW4-6340, Adjustments after code review.
-- 2018-11-21  fandse  SCUXXW4-6340, Removed label for group, changed label for list, removed OrderClass from search.
-- 2018-11-20  fandse  SCUXXW4-6340, Initial version of frmMaterialRequisitionUnissue converted to Aurena with MTG Version: 1.32.
----------------------------------------------------------------------------------------------------------

client UnissueInventoryPartFromMaterialRequisition;
component INVENT;
layer Core;
projection UnissueInventoryPartFromMaterialRequisition;
include fragment InventoryPartLovMrpSelector;
include fragment MaterialRequisitionLineStateIndicator;
include fragment ProjectDetailsFieldSet;

warningsenabled = MaterialRequisLine;

----------------------------- NAVIGATOR ENTRIES -----------------------------
navigator {
   entry UnissueInventoryPartForMaterialRequisitionNavEntry parent InventNavigator.WarehouseManagementRequisition at index 400 {
      label = "Unissue Inventory Part from Material Requisition";
      page Form;
   }
}

-------------------------------- MAIN PAGES ---------------------------------
page Form using MaterialRequisLineSet {
   label = "Unissue Inventory Part from Material Requisition";
   startupmode = search;
   stateindicator MaterialRequisitionLineStateIndicator;
   selector MaterialRequisLineSelector;
   initcontext FetchSystemParamsInitContext;
   group MaterialRequisitionUnissueGroup;
   list MaterialRequisitionUnissueList(MaterialRequisitionUnissueArray);

   commandgroup CommandGroupCmdGroup {

      command ResolveShortagesCommand;
   }
}

initcontext FetchSystemParamsInitContext {
   parameter SystemShortageFlag Text;
   init command {
      execute {
         call GetSystemShortageFlag() into SystemShortageFlag;
      }
   }
}

-------------------------------- ASSISTANTS ---------------------------------

--------------------------------- COMMANDS ----------------------------------
command ResolveShortagesCommand for MaterialRequisLine {
   label = "Resolve Shortages";
   enabled = [not component.MaterialRequisitionUnissueList.IsDirty and ShortageExist and PartShortageFlag and initcontext.SystemShortageFlag];
   execute {
      navigate "page/ResolveShortagesForInventoryPart/Form?$filter=Contract eq $[Contract] and PartNo eq $[PartNo]";
   }
}

command ShortageNotificationCommand for InventoryTransactionHist {
   execute {
      if [parent.ShortageExist and parent.PartShortageFlag and initcontext.SystemShortageFlag] {
         alert("There are shortages for part ${parent.PartNo}");
      }
   }
}

--------------------------------- SELECTORS ---------------------------------
@Override
selector MaterialRequisLineSelector for MaterialRequisLine {
   label = "${OrderNo} - ${LineNo} - ${ReleaseNo} - ${PartNo}";
   static OrderNo;
   static LineNo;
   static ReleaseNo;
   static PartNo;
   static OrderClass {
      visible = [false];
   }
}

---------------------------------- GROUPS -----------------------------------
group MaterialRequisitionUnissueGroup for MaterialRequisLine {
   label = "";
   field OrderClass {
      searchable = false;
      visible = [false];
   }
   field OrderNo {
      size = Small;
   }
   field LineNo {
      size = Small;
   }
   field ReleaseNo {
      size = Small;
   }
   field Contract {
      size = Small;
   }
   lov PartNoRef with ReferenceInventoryPartLovMrpSelector {
      description = PartNoRef.Description;
   }
   @DynamicComponentDependency PROJ
   field ProjectIdRef {
      visible = [ProjectId != null];
   }
   @DynamicComponentDependency PROJ
   field ActivitySeqRef {
      visible = [ActivitySeq != null];
   }
   field UnitMeasRef {
      size = Small;
   }
   field CatchUom {
      size = Small;
      visible = [CatchUom != null];
   }
}

----------------------------------- LISTS -----------------------------------
list MaterialRequisitionUnissueList for InventoryTransactionHist {
   label = "Issued Lines";
   savemode = Buffer;
   editmode = SingleCellEdit;
   multiselect = [true];
   defaultfilter = [IssuedQty > 0];
   crudactions {
      before command ShortageNotificationCommand;
   }
   field UnissuedQty;
   field IssuedQty;
   field UnissuedCatchQty {
      columnvisible = [parent.CatchUom != null];
   }
   field IssuedCatchQty {
      columnvisible = [parent.CatchUom != null];
   }
   field LocationNo {
      size = Small;
   }
   field HandlingUnitRef;
   lov HandlingUnitTypeIdRef with ReferenceHandlingUnitSelector {
      description = HandlingUnitTypeIdRef.Description;
      editable = [false];
   }
   field HandlingUnitRef.Sscc {
      size = Small;
      label = "SSCC";
   }
   field HandlingUnitRef.AltHandlingUnitLabelId {
      size = Small;
      label = "Alt Handling Unit Label ID";
   }
   field Warehouse {
      size = Small;
   }
   field Bay {
      size = Small;
   }
   field RowId {
      size = Small;
   }
   field Tier {
      size = Small;
   }
   field Bin {
      size = Small;
   }
   field LotBatchNo {
      size = Small;
   }
   field SerialNo {
      size = Small;
   }
   lov ConditionCodeRef with ReferenceConditionCodeSelector {
      description = ConditionCodeRef.Description;
      label = "Condition Code";
   }
   field WaivDevRejNo {
      size = Small;
   }
   lov AvailabilityControlIdRef with ReferencePartAvailabilityControlSelector {
      description = AvailabilityControlIdRef.Description;
      label = "Availability Control";
   }
   field EngChgLevel {
      size = Small;
   }
   field DateCreated;
   field TransactionId;
   fieldset ProjectDetailsFieldSet;
   @DynamicComponentDependency PROJ
   field ActivitySeqRef {
      editable = [false];
   }
   lov TopParentHandlingUnitTypeIdRef with ReferenceHandlingUnitTypeSelector {
      description = TopParentHandlingUnitTypeIdRef.Description;
      label = "Top Parent Handling Unit Type";
   }
   field TopParentSscc {
      size = Small;
   }
   field TopParentAltHandlingUnitLabelId {
      size = Small;
   }
   fieldranking UnissuedQty, IssuedQty, UnIssuedCatchQty, IssuedCatchQty, LocationNo, LotBatch, SerialNo, HandlingUnitId, ConfigurationId, WaivDevRejNo, EngChgLevel, ActivitySeq;
}
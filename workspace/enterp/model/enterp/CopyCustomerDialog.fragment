fragment CopyCustomerDialog;
component ENTERP;
layer Core;
description "Copy customer, by using already existing customer as source.";

----------------------------- CLIENT FRAGMENTS ------------------------------

---------------------------------- DIALOGS ----------------------------------
dialog CopyCustomerDialog for CopyCustomerDialogStructure {
   label = "Copy Existing Customer";
   input(Identity, Name, CustomerCategory);
   output(NewIdentity);
   group CopyFromCustomerGroup;
   group CopyToCustomerGroup;

   command Ok {
      enabled = [NewName != null and NewCustomerCategory != null];
      variable ResultVar Text;
      execute {
         call AssociationNoExist(NewAssocNo) into ResultVar;
         if [ResultVar = "TRUE"] {
            confirm("Another business partner with the same association number is already registered. Do you want to use the same Association No?") {
               when OK {
                  set ResultVar = "FALSE";
               }
            }
         }
         if [ResultVar = "FALSE"] {
            call CopyExistingCustomer(Identity, NewIdentity, NewName, NewCustomerCategory, NewAssocNo) into NewIdentity {
               when FAIL {
                  error("${error.Message}");
               }
               when SUCCESS {
                  success("Customer ${NewIdentity} was created.");
                  exit OK;
               }
            }
         }
         else {
            exit;
         }
      }
   }
   command Cancel;
}


----------------------------- VISUAL COMPONENTS -----------------------------
group CopyFromCustomerGroup for CopyCustomerDialogStructure {
   label = "Copy From";

   field IdentityRef {
      label = "Customer";
      contactwidget {
         enabled = [true];
         source = Customer;
      }

   }
   field IdentityRef.Name {
      size = Large;
      label = "Name";
   }
   field IdentityRef.CustomerCategory {
      size = Small;
      label = "Category";
   }
}


group CopyToCustomerGroup for CopyCustomerDialogStructure {
   label = "Copy To";

   field NewIdentity {
      label = "Customer";
   }
   field NewName {
      size = Large;
      label = "Name";
   }
   field NewCustomerCategory {
      label = "Category";
   }
   field NewAssocNo {
      size = Small;
      label = "Association No";
   }
}


--------------------------- PROJECTION FRAGMENTS ----------------------------

---------------------------------- ACTIONS ----------------------------------
-- No Company is passed for this action, but user default company is taken in the method body.
-- Check_Insert___ takes care of CustomerInfo entity related security
-- Security handling related to DbRmcomAccess is handled
action CopyExistingCustomer Text {
   initialcheck implementation;
   ludependencies = CustomerInfo;

   parameter CustomerId Text;
   parameter NewId Text;
   parameter NewName Text;
   parameter NewCategory Enumeration(CustomerCategory);
   parameter AssociationNo Text;
}


--------------------------------- FUNCTIONS ---------------------------------
function AssociationNoExist Text {
   parameter AssociationNo Text;
}


-------------------------------- STRUCTURES ---------------------------------
structure CopyCustomerDialogStructure {
   attribute Identity Text {
      label = "Customer";
      maxlength = 20;
      required = [true];
      format = uppercase;
   }
   attribute Name Text {

      maxlength = 100;
      required = [true];
   }
   attribute Category Text {
      label = "Category";
      maxlength = 32767;
   }
   attribute NewIdentity Text {
      label = "Customer";
      maxlength = 20;
      format = uppercase;
   }
   attribute NewName Text {
      label = "Name";
      maxlength = 100;
      required = [true];
   }
   attribute NewAssocNo Text {
      label = "Association No";
      maxlength = 30;
   }
   attribute NewCustomerCategory Enumeration(CustomerCategory) {
      label = "Category";
      required = [true];
   }
//TODO: If needed: this section should be moved to client file or updated so the referenced fields are alined (like CustomerId)
   reference IdentityRef(Identity) to CustomerInfo(CustomerId) {
      label = "Customer";
   }
}






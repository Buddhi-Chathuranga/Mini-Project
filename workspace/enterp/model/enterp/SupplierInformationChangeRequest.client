-----------------------------------------------------------------------------
-- Date        Sign    History
-- ----------  ------  ------------------------------------------------------
-- 2021-08-18  Ssuslk  PR21R2-597, Added reference to Supplier page from Supplier in the SupplierInfoChangeRequestList list.
-- 2021-08-03  NaLrlk  PR21R2-582, Modified to display client values for change requests.
-- 2021-07-31  NaLrlk  PR21R2-399, Modified currentdatafilter label and description.
-- 2021-07-15  NaLrlk  PR21R2-396, Added ChangeReference to SupplierInfoChangeRequestList list.
-- 2021-07-10  NaLrlk  PR21R2-400, Created.
-----------------------------------------------------------------------------
client SupplierInformationChangeRequest;
component ENTERP;
layer Core;
projection SupplierInformationChangeRequestHandling;
description "Client to review and approval of supplier request changes and additions to the data";

warningsenabled = ApproveChangeRequest, RejectChangeRequest;

--------------------------------- FRAGMENTS ---------------------------------


----------------------------- NAVIGATOR ENTRIES -----------------------------
navigator {
   entry SupplierInfoChangeRequestNavEntry parent EnterpNavigator.SupplierMenu at index 400 {
      label = "Supplier Information Change Requests";
      page List;
   }
   @DynamicComponentDependency PURCH
   entry ProcurementSupplierInfoChangeRequestNavEntry parent PurchNavigator.ProcurementSupplier at index 300 {
      label = "Supplier Information Change Requests";
      page List;
   }
   @DynamicComponentDependency RMCOM
   entry SRMSupplierInfoChangeRequestNavEntry parent RmcomNavigator.SRMSupplier at index 300 {
      label = "Supplier Information Change Requests";
      page List;
   }
}

-------------------------------- MAIN PAGES ---------------------------------
page List using SupplierInfoChangeRequestSet {
   label = "Supplier Information Change Requests";
   currentdatafilter {
      label = "Show only Preliminary";
      description = "Show only Preliminary";
   }
   list SupplierInfoChangeRequestList;
}

-------------------------------- ASSISTANTS ---------------------------------
assistant SupplierInfoChangeRequestLineAssistant for SupplierInfoChangeRequestLine {
   label = "Supplier Information Change Request - ${RequestId}";
   input(RequestId);

   steps {
      step {
         list SupplierInfoChangeRequestLineList using GetSupplierInfoChangeRequestLine(RequestId);
      }
      final step {
      }
   }
   finish command {
      label = "Close";
      execute {
         exit OK;
      }
   }
   cancel command {
      visible = [false];
   }
}

--------------------------------- COMMANDS ----------------------------------
command ApproveCommand for SupplierInfoChangeRequest {
   label = "Approve";
   enabled = [Objstate = "Preliminary"];
   mode = SelectedRecords;
   variable ApproverCommentsVar Text;
   bulkexecute {
      dialog ChangeRequestApproverCommentDialog into(ApproverCommentsVar) {
         when OK {
            call ApproveChangeRequest(Selection, ApproverCommentsVar);
         }
      }
   }
}

command RejectCommand for SupplierInfoChangeRequest {
   label = "Reject";
   enabled = [Objstate = "Preliminary"];
   mode = SelectedRecords;
   variable ApproverCommentsVar Text;
   bulkexecute {
      dialog ChangeRequestApproverCommentDialog into(ApproverCommentsVar) {
         when OK {
            call RejectChangeRequest(Selection, ApproverCommentsVar);
         }
      }
   }
}

command ViewChangeInformationCommand for SupplierInfoChangeRequest {
   label = "View Changes";
   mode = SingleRecord;
   execute {
      assistant SupplierInfoChangeRequestLineAssistant(RequestId) {
         when OK {
            exit OK;
         }
      }
   }
}

--------------------------------- SELECTORS ---------------------------------


---------------------------------- GROUPS -----------------------------------
group ChangeRequestApproverCommentGroup for ChangeRequestApproverCommentStructure {
   label = "Comment";
   field ApproverComments {
      label = "Comment";
      multiline = true;
   }
}

----------------------------------- LISTS -----------------------------------
list SupplierInfoChangeRequestList for SupplierInfoChangeRequest {
   label = "Supplier Information Change Requests";
   editmode = SingleCellEdit;
   multiselect = [true];

   orderby = RequestId desc;

   field RequestId {
      size = Small;
   }
   lov SupplierRef with ReferenceSupplierInfoGeneralSelector {
      label = "Supplier";
      contactwidget {
         enabled = [true];
         source = Supplier;
      }
   }
   field ChangeArea;
   field ChangeKeyRef;
   field ChangeType;
   badge Objstate {
      label = "Status";
      emphasis StatePreliminary = [Objstate = "Preliminary"];
      emphasis StateApproved = [Objstate = "Approved"];
      emphasis StateRejected = [Objstate = "Rejected"];
      style = TextOnly;
      size = Small;
   }
   lov RequestedByRef with ReferenceFndUserSelector {
      label = "Requested By";
      description = RequestedBy.Description;
      contactwidget {
         enabled = [true];
         source = User;
      }
   }
   field RequestedDate;
   field Comments {
      type = LongText;
      multiline = true;
   }
   lov ApprovedByRef with ReferenceFndUserSelector {
      label = "Approver";
      description = ApprovedBy.Description;
      contactwidget {
         enabled = [true];
         source = User;
      }
   }
   field ApprovedDate;
   field ApproverComments {
      editable = [Objstate = "Approved" or Objstate = "Rejected"];
      size = Medium;
      type = LongText;
      multiline = true;
   }
   command RejectCommand;
   command ApproveCommand;
   command ViewChangeInformationCommand;
}

list SupplierInfoChangeRequestLineList for SupplierInfoChangeRequestLine {
   label = "Supplier Information Change Request Lines";
   orderby = LineNo;

   field ChangeInfoDescription;
   field OldValueClient {
      size = Large;
      multiline = true;
   }
   field NewValueClient {
      size = Large;
      multiline = true;
   }
}

---------------------------------- DIALOGS ----------------------------------
dialog ChangeRequestApproverCommentDialog for ChangeRequestApproverCommentStructure {
   label = "Approver Comment";
   output(ApproverComments);
   group ChangeRequestApproverCommentGroup;
   command Ok;
   command Cancel;
}

-------------------------------- SINGLETONS ---------------------------------


---------------------------------- CHARTS -----------------------------------


--------------------------------- CALENDERS ---------------------------------


---------------------------------- SHEETS -----------------------------------


----------------------------- STATE INDICATORS ------------------------------


----------------------------------- TREES -----------------------------------


---------------------------------- PLUGINS ----------------------------------


------------------------------- IMAGE VIEWERS -------------------------------

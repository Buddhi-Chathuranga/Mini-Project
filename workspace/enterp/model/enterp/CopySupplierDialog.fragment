fragment CopySupplierDialog;
component ENTERP;
layer Core;
description "Copy supplier, by using already existing supplier as source.";

----------------------------- CLIENT FRAGMENTS ------------------------------

---------------------------------- DIALOGS ----------------------------------
dialog CopySupplierDialog for CopySupplierDialogStructure {
   label = "Copy Existing Supplier";
   input(Identity, Name);
   output(NewIdentity);
   group CopyFromSupplierGroup;
   group CopyToSupplierGroup;

   command Ok {
      enabled = [NewName != null];
      variable ResultVar Text;
      execute {
         call AssociationNoSuppExist(NewAssocNo) into ResultVar;
         if [ResultVar = "TRUE"] {
            confirm("Another business partner with the same association number is already registered. Do you want to use the same Association No?") {
               when OK {
                  set ResultVar = "FALSE";
               }
            }
         }
         if [ResultVar = "FALSE"] {
            call CopyExistingSupplier(Identity, NewIdentity, NewName, NewAssocNo) into NewIdentity {
               when FAIL {
                  error("${error.Message}");
               }
               when SUCCESS {
                  success("Supplier ${NewIdentity} was created.");
                  exit OK;
               }
            }
         }
         else {
            exit;
         }
      }
   }
   command Cancel;
}


----------------------------- VISUAL COMPONENTS -----------------------------
group CopyFromSupplierGroup for CopySupplierDialogStructure {
   label = "Copy From";
   field IdentityRef {
      label = "Supplier";
      contactwidget {
         enabled = [true];
         source = Supplier;
      }
   }
   field IdentityRef.Name {
      size = Large;
      label = "Name";
   }
}


group CopyToSupplierGroup for CopySupplierDialogStructure {
   label = "Copy To";
   field NewIdentity {
      label = "Supplier";
   }
   field NewName {
      size = Large;
      label = "Name";
   }
   field NewAssocNo {
      size = Small;
      label = "Association No";
   }
}


--------------------------- PROJECTION FRAGMENTS ----------------------------

---------------------------------- ACTIONS ----------------------------------
-- No Company is passed for this action, but user default company is taken in the method body.
-- Check_Insert___ takes care of SupplierInfo and SupplierInfoProspect entity related security
action CopyExistingSupplier Text {
   initialcheck implementation;
   ludependencies = SupplierInfo, SupplierInfoProspect, SupplierInfoGeneral;

   parameter SupplierId Text;
   parameter NewId Text;
   parameter NewName Text;
   parameter AssociationNo Text;
}


--------------------------------- FUNCTIONS ---------------------------------
function AssociationNoSuppExist Text {
   parameter AssociationNo Text;
}


-------------------------------- STRUCTURES ---------------------------------
structure CopySupplierDialogStructure {
   attribute Identity Text {
      label = "Supplier";
      maxlength = 20;
      required = [true];
      format = uppercase;
   }
   attribute Name Text {
      maxlength = 100;
      required = [true];
   }
   attribute NewIdentity Text {
      label = "Supplier";
      maxlength = 20;
      format = uppercase;
   }
   attribute NewName Text {
      label = "Name";
      maxlength = 100;
      required = [true];
   }
   attribute NewAssocNo Text {
      label = "Association No";
      maxlength = 30;
   }
//TODO: If needed: this section should be moved to client file or updated so the referenced fields are alined (like SupplierId)
   reference IdentityRef(Identity) to SupplierInfoGeneral(SupplierId) {
      label = "Supplier";
   }
}
fragment ChangeSupplierCategoryDialog;
component ENTERP;
layer Core;
description "Change Supplier Category Dialog";

@DynamicComponentDependency PURCH
include fragment SupplierTemplateLovSelector;

----------------------------- CLIENT FRAGMENTS ------------------------------

--------------------------------- COMMANDS ----------------------------------
command InitCommand for ChangeSupplierCategoryStructure {
   execute {
      call FetchDefaultCompany() into DefaultCompany;
      set Category = "Supplier";
      set AssociationNo = PrevAssociationNo;
   }
}


---------------------------------- GROUPS -----------------------------------
group SupplierGroup for ChangeSupplierCategoryStructure {
   label = "Supplier";

   field Category;
   field SupplierId {
      contactwidget {
         enabled = [true];
         source = Supplier;
         key = SupplierId;
      }
   }
   field SupplierName;
   field AssociationNo;
}


group TemplateSupplierGroup for ChangeSupplierCategoryStructure {
   label = "Template Supplier";

   @DynamicComponentDependency PURCH
   lov TempSupplierIdRef with ReferenceSupplierTemplateLovSelector {
      search = TemplateSupplierId, SupplierTemplateDesc;
      description = TempSupplierName;
      size = Large;
      validate command {
         execute {
            if [TempSupplierId = null] {
               set TempOverwritePurchData = false;
               set TempSupplierIdRef = null;
               set TempCompany = null;
            }
            else {
               call TempSupplierInfo(TempSupplierId) into TempSupplierName;
               set TempCompany = DefaultCompany;
            }
         }
      }
   }
   @DynamicComponentDependency ACCRUL
   lov TempCompanyRef with ReferenceCompanyFinanceSelector;
   field TempOverwritePurchData {
      editable = [TempSupplierId != null];
   }
}


---------------------------------- DIALOGS ----------------------------------
dialog ChangeSupplierCategoryDialog for ChangeSupplierCategoryStructure {
   label = "Change Supplier Category";
   input(SupplierId, SupplierName, PrevAssociationNo) {
      command InitCommand;
   }

   group SupplierGroup;
   group TemplateSupplierGroup;

   command Ok {
      execute {
         call ChangeSupplierCategory(SupplierId, SupplierName, PrevAssociationNo, AssociationNo, TempSupplierId, TempCompany, TempOverwritePurchData);
         exit OK;
      }
   }
   command Cancel;
}


--------------------------- PROJECTION FRAGMENTS ----------------------------

---------------------------------- ACTIONS ----------------------------------
-- Company Security is hanlded by Supplier_Info_General_API.Change_Supplier_Category__
action ChangeSupplierCategory {
   initialcheck implementation;
   supportwarnings = [true];

   parameter NewSupplierId Text;
   parameter NewSupplierName Text;
   parameter PrevAssociationNo Text;
   parameter NewAssociationNo Text;
   parameter TemplateSupplierId Text;
   parameter Company Text;
   parameter OverwritePurchData Boolean;

   ludependencies = SupplierInfo, SupplierInfoGeneral, SupplierInfoProspect;
}


--------------------------------- FUNCTIONS ---------------------------------
function FetchDefaultCompany Text;


function TempSupplierInfo Text {
   parameter TempSupplierId Text;
}


-------------------------------- STRUCTURES ---------------------------------
structure ChangeSupplierCategoryStructure {
   attribute SupplierId Text {
      label = "Supplier ID";
      editable = [false];
   }
   attribute SupplierName Text;
   attribute AssociationNo Text;
   attribute Category Enumeration(SupplierInfoCategory) {
      editable = [false];
   }
   attribute DefaultCompany Text;
   attribute TempSupplierId Text;
   attribute TempSupplierName Text;
   attribute TempCompany Text;
   attribute TempOverwritePurchData Boolean("TRUE", "FALSE") {
      label = "Overwrite Purchase Related Data";
      required = [true];
   }
   attribute PrevAssociationNo Text;

   @DynamicComponentDependency ACCRUL
   reference TempCompanyRef(TempCompany) to CompanyFinance(Company) {
      label = "Company";
   }
   @DynamicComponentDependency PURCH
   reference TempSupplierIdRef(TempSupplierId) to SupplierTemplateLov(TemplateSupplierId) {
      label = "Template Supplier";
   }

}


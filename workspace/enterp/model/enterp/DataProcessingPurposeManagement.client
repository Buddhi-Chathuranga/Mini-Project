--  Date    Sign    History
--  ------  ------  ---------------------------------------------------------
--  181119  thjilk  Bug 145282, Enabled warnings for action SaveAll
--          thjilk  Bug 145282, Changed finish command definition of the Assistant to support warnings
-----------------------------------------------------------------------------

client DataProcessingPurposeManagement;
component ENTERP;
layer Core;
projection DataProcessingPurposeManagementHandling;
include fragment DataSubjectsSelector;
warningsenabled = SaveAll;

----------------------------- NAVIGATOR ENTRIES -----------------------------


-------------------------------- MAIN PAGES ---------------------------------


-------------------------------- ASSISTANTS ---------------------------------
assistant DataProcessingPurposeManagementAssistant using PersonalDataConsentSet {
   label = "Data Processing Purpose Management";
   savemode = OnLostFocus;

   setup InitPersonalConsents {
      variable KeyRefVar Text;
      variable DataSubjectIdVar Text;
      variable DataSubjectNameVar Text;
      variable DataSubjectVar Text;
      execute {
         set KeyRef = KeyRefVar;
         set DataSubjectId = DataSubjectIdVar;
         set DataSubjectName = DataSubjectNameVar;
         set DataSubject = DataSubjectVar;
      }
   }

   steps {
      step {
         group PersonalDataConsentGroup;
         list DataProcessingUpdateHistoryVirtualList(DataProcessingUpdateHistories);
         list DataProcessingPurposeVirtualList(DataProcessingPurposes) bind DataProcessingUpdateHistoryVirtualList;
      }
      final step {

      }
      cancelled step {

      }
   }
   finish command {
      enabled = [PurposesUpdated = true];

      execute {
         call SaveAll(Action);
         navigate back;
      }
   }
   cancel command {
      execute {
         navigate back;
      }
   }

   restart command {
      visible = [false];
   }
}


--------------------------------- COMMANDS ----------------------------------
command WithdrawAllCommand for DataProcessingPurposesVirtual {
   label = "Withdraw All";

   execute {
      call ChangeModeType(SuperParentKey, ParentObjkey, "PurposesWithdrawn");
   }
}


command UpdatePurposesCommand for DataProcessingPurposesVirtual {
   label = "Update Purposes";

   execute {
      call ChangeModeType(SuperParentKey, ParentObjkey, "UpdatedPurposes");
   }
}


command NewPurposeSetCommand for DataProcessingPurposesVirtual {
   label = "New Purpose Set";

   execute {
      call ChangeModeType(SuperParentKey, ParentObjkey, "NewPurpose");
   }
}


command PrintCommand for DataProcessingUpdateHistoryVirtual {
   label = "Print";
   visible = [Action != "DataErased"];
   variable ResultKeyVar Text;

   execute {
      call PrintAll(KeyRef, DataSubjectDb, OperationDate, Action, parent.DataSubjectId, parent.DataSubjectName, Remark) into ResultKeyVar;
      if [ResultKeyVar != null] {
         printdialog ResultKeyVar;
      }
   }
}


--------------------------------- SELECTORS ---------------------------------


---------------------------------- GROUPS -----------------------------------
group PersonalDataConsentGroup for PersonalDataConsentVirtual {
   label = "";

   field DataSubjectId {
      label = "Data Subject ID";
   }
   field DataSubjectName;
   field UpdateDate {
      size = Small;
      label = "Update Date";
      visible = [PurposesUpdated = true];

   }
   field Remark {
      label = "Remark";
      visible = [PurposesUpdated = true];

   }
}


----------------------------------- LISTS -----------------------------------
list DataProcessingUpdateHistoryVirtualList for DataProcessingUpdateHistoryVirtual {
   label = "Data Processing Purposes Update History";
   multiselect = [false];

   field DataSubjectId {
      size = Large;
      label = "Data Subject ID";
   }
   lov DataSubjectsRef with ReferenceDataSubjectsMainSelector {
      label = "Data Subject";
   }
   field OperationDate;
   field UpdateDate;
   badge Action {
      emphasis Progress3 = [Action = "NewPurpose"];
      emphasis Progress6 = [Action = "UpdatedPurposes"];
      emphasis Progress9 = [Action = "PurposesWithdrawn"];
      emphasis Progress12 = [Action = "DataErased"];
      style = TextOnly;
   }
   field Remark {
      size = Large;
   }
   field PerformedBy {
      size = Small;
      contactwidget {
         enabled = [true];
         source {
            User = [true];
         }
      }
   }
   field ValidColumnName {
      columnvisible = [false];
   }
   field LastUpdatedAction {
      columnvisible = [false];
   }
   field PurposesUpdated {
      columnvisible = [false];
   }

   command PrintCommand;
}


list DataProcessingPurposeVirtualList for DataProcessingPurposesVirtual {
   label = "Data Processing Purposes";
   editmode = SingleRowEdit;
   preselect = [true];
   crudactions {
      edit {
         enabled = [(parent.LastUpdatedAction = true or parent.LastUpdatedAction = null) and parent.PurposesUpdated = true];
      }
   }

   field Valid {
      label = "${parent.ValidColumnName}";
      validate command {
         execute {
            if [Valid != true and parent.LastUpdatedAction = true] {
               set EffectiveOn = null;
               set EffectiveUntil = null;
            }
         }
      }
   }
   field Purpose;
   field Description {
      size = Large;
   }
   daterangefield {
      startdate EffectiveOn;
      enddate EffectiveUntil;

      label = "Effective Period";
      editable = [(parent.LastUpdatedAction = true or parent.KeyRef = null) and Valid = true];
   }
   field AssociatedPersonalData;


   commandgroup DataProcessingPurposeCmdGroup {
      command NewPurposeSetCommand {
         visible = [parent.LastUpdatedAction = true or parent.LastUpdatedAction = null];
      }
      command UpdatePurposesCommand {
         visible = [parent.LastUpdatedAction = true];
      }
      command WithdrawAllCommand {
         visible = [parent.LastUpdatedAction = true or parent.LastUpdatedAction = null];
      }
   }
}


----------------------------------- CARDS -----------------------------------


---------------------------------- DIALOGS ----------------------------------


---------------------------------- CHARTS -----------------------------------


--------------------------------- CALENDARS ---------------------------------


---------------------------------- SHEETS -----------------------------------


----------------------------- STATE INDICATORS ------------------------------


----------------------------------- TREES -----------------------------------


---------------------------------- PLUGINS ----------------------------------


------------------------------- IMAGE VIEWERS -------------------------------








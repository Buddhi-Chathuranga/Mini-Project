client ExportCompanyTemplateToFileAssistant;
component ENTERP;
layer Core;
projection ExportCompanyTemplateToFileAssistantHandling;


----------------------------- NAVIGATOR ENTRIES -----------------------------
navigator {
   entry CompanyTemplatesMenu parent EnterpNavigator.CompanyTemplateMenu at index 300 {
      label = "Export Company Template";
      assistant Assistant;
   }
}
-------------------------------- MAIN PAGES ---------------------------------

-------------------------------- ASSISTANTS ---------------------------------
assistant Assistant using ExportCompanyTemplateVirtualSet {
   label = "Export Company Template to File Assistant";
   savemode = OnLostFocus;

   setup ExportCompanyTemplateSetup {
      variable InputTemplateIdVar Text;
      execute {
         set TemplateId = InputTemplateIdVar;
      }
   }

   steps {
      step {
         label = "Enter Template Identity";
         markdowntext {
            text = "Welcome to the Export Company Template to File Assistant. This assistant will help you to export a template to a file.";
         }
         markdowntext {
            text = "1) Enter a Template Identity.";
         }
         markdowntext {
            text = "2) Choose if you want to Create a File per Component or only one File.";
         }
         markdowntext {
            text = "3) Choose if you also want to export language file(s).";
         }

         group ExportCriteriaGroup;
      }
      step {
         // Step: Step2
         label = "Select Components";
         group IncludeExcludeComponentsGroup;

         next command {
            enabled = [ComponentList != null];
            execute {
               if [ExportLanguages = false]
                  {
                  call GenerateSummary();
               }
            }
         }
      }
      step {
         enabled = [ExportLanguages = true];

         // Step: Step3
         label = "Select Languages";
         group IncludeExcludeTemplateLanguageQuery;

         next command {
            enabled = [LanguageList != null];
            execute {
               call GenerateSummary();
            }
         }
      }
      step {
         // Step: Step4
         label = "Export Company Template Summary";
         group SummaryGroup;
         list ComponentsSummaryList(ActiveComponentArray);
         list LanguageSummaryList(ExportCompanyLangArray);
      }
      final step {
      }
      cancelled step {
      }
   }
   finish command {
      enabled = [component.Assistant.ActiveStep > 3 and ComponentListGenerated = true and((LanguageListGenerated = false and ExportLanguages = false) or(LanguageListGenerated = true and ExportLanguages = true))];
      variable TestVar Text;
      execute {
         call GenerateTemplateFiles()into TestVar;
         if [TestVar != null] {
            download "ExportCompanyTemplateVirtualSet(Objkey=$[Objkey])/ExportFile";
            exit OK;
         }
         else {
            info("No Company Template Files to Export");
         }
      }
   }
   cancel command {
      execute {
         navigate back;
      }
   }
   restart command {
      visible = [true];
   }
}
--------------------------------- COMMANDS ----------------------------------

----------------------------- VISUAL COMPONENTS -----------------------------

--------------------------------- SELECTORS ---------------------------------

---------------------------------- GROUPS -----------------------------------
group ExportCriteriaGroup for ExportCompanyTemplateVirtual {
   label = "Export Company Template Criterias";
   lov CompanyTemplateRef with ReferenceCreateCompanyTemSelector {
      search = TemplateId, Description;
      label = "Template ID";
      description = CompanyTemplateRef.Description;
   }
   field TemplatePerFile;
   field ExportLanguages;
}

group IncludeExcludeComponentsGroup for ExportCompanyTemplateVirtual {
   label = "";
   fieldset CriteriaGroupReadOnly;
   itempicker ComponentList using IncludeExcludeComponentQuerySet {
      orderby = Component;
      displayvalue = Component;
      availableitemslabel = "Select Components";
      selecteditemslabel = "Selected Components";
      filter = [TemplateId = TemplateId];
   }
}

group IncludeExcludeTemplateLanguageQuery for ExportCompanyTemplateVirtual {
   label = "";

   fieldset CriteriaGroupReadOnly;
   itempicker LanguageList using IncludeExcludeTemplateLanguageQuerySet {
      orderby = Description;
      displayvalue = Description;
      availableitemslabel = "Select Languages";
      selecteditemslabel = "Selected Languages";
      filter = [KeyName = "TemplKeyLu" and KeyValue = TemplateId];
   }
}

group SummaryGroup for ExportCompanyTemplateVirtual {
   label = "Export Company Template Summary";

   fieldset CriteriaGroupReadOnly;

}
----------------------------------- LISTS -----------------------------------

list ComponentsSummaryList for ActiveTemplateCompVirtual {
   label = "Selected Components";
   disable = Create, Delete, Update;
   preselect = [false];
   field Component;
}

list LanguageSummaryList for ExportCompanyLang {
   label = "Selected Languages";
   disable = Create, Delete, Update;
   preselect = [false];
   field Description;
}

--------------------------------- FIELDSETS ---------------------------------
fieldset CriteriaGroupReadOnly for ExportCompanyTemplateVirtual {
   lov CompanyTemplateRef with ReferenceCreateCompanyTemSelector {
      search = TemplateId, Description;
      label = "Template ID";
      editable = [false];
      description = CompanyTemplateRef.Description;
   }
   field TemplatePerFile {
      editable = [false];
   }
   field ExportLanguages {
      editable = [false];
   }
}

----------------------------------- CARDS -----------------------------------

---------------------------------- DIALOGS ----------------------------------

---------------------------------- CHARTS -----------------------------------

--------------------------------- CALENDARS ---------------------------------

---------------------------------- SHEETS -----------------------------------

----------------------------- STATE INDICATORS ------------------------------

----------------------------------- TREES -----------------------------------

---------------------------------- PLUGINS ----------------------------------

------------------------------- IMAGE VIEWERS -------------------------------


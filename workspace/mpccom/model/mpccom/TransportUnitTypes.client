-----------------------------------------------------------------------------
--  Date    Sign    History
--  ------  ------  ---------------------------------------------------------
-- 180329   Lufelk  SCUXXW4-8040,  Created.
-----------------------------------------------------------------------------
client TransportUnitTypes;
component MPCCOM;
layer Core;
projection TransportUnitTypesHandling;

----------------------------- NAVIGATOR ENTRIES -----------------------------
navigator {
   entry TransportUnitTypesNavEntry parent MpccomNavigator.ApplicationBaseSetupGeneralData at index 500 {
      label = "Transport Unit Types";
      page List home TransportUnitType;
   }
}


-------------------------------- MAIN PAGES ---------------------------------
page List using TransportUnitTypes {
   label = "Transport Unit Types";
   list TransportUnitTypesList;
}


--------------------------------- COMMANDS ----------------------------------
command TranslationCommand {
   label = "Translation";
   mode = Global;
   enabled = [true];
   execute {
      navigate "page/BasicDataTranslation/Form?$filter=Module eq 'MPCCOM' and Lu eq 'TransportUnitType'";
   }
}


--------------------------------- SELECTORS ---------------------------------
@Override
selector IsoUnitWeightSelector for IsoUnitWeights {
   field UnitCode;
   field Description;
   field PresentFactor;
   field BaseUnit;
   field UnitType;
}

@Override
selector IsoUnitVolumeSelector for IsoUnitVolumes {
   field UnitCode;
   field Description;
   field PresentFactor;
   field BaseUnit;
   field UnitType;
}


----------------------------------- LISTS -----------------------------------
list TransportUnitTypesList for TransportUnitType {
   label = "";
   field TransportUnitTypeId {
      label = "Transport Unit Type";
   }
   field Description;
   field WeightCapacity {
      validate command {
         variable WeightStr;
         execute {
            @DynamicComponentDependency INVENT
            call GetUomForWeightFunction() into WeightStr;
            set UomForWeight = WeightStr;
         }
      }
   }
   lov UomForWeightRef with IsoUnitWeightSelector {
      label = "UoM for Weight";
   }
   field VolumeCapacity {
      validate command {
         variable VolumeStr;
         execute {
            @DynamicComponentDependency INVENT
            call GetUomForVolumeFunction() into VolumeStr;
            set UomForVolume = VolumeStr;
         }
      }
   }
   lov UomForVolumeRef with IsoUnitVolumeSelector {
      label = "UoM for Volume";
   }
   command TranslationCommand;
}
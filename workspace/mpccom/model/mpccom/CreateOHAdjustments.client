--------------------------------------------------------------------------------------
-- Date        Sign    History
-- ----------  ------  ---------------------------------------------------------------
-- 2020-09-29  Dinklk  Removed format decimal of AdjustmentPercentage in AdjustmentsBasedOnCostSourceList and AdjustmentsBasedOnPostingGroupList.
-- 2020-09-29          Aligned the validate message of CreateOhAdjustmentsAssistant.Finish same as in old IEE client.
-- 2020-09-25  Dinklk  MF2020R1-7324, Instead of one list earlier, introduced two lists AdjustmentsBasedOnCostSourceList and AdjustmentsBasedOnPostingGroupList
-- 2020-09-25          to conditionally show correct list according to the value of CreateOhAdjustmentBasedOn.
-- 2020-09-10  Dinklk  MF2020R1-7147, Added radiogroup CreateOhAdjustmentBasedOn to CreateOhAdjustmentsGroup.
-- 2020-09-10          Added field OhTypeBasedOnPostingGrp, lov PostingGroupIdRef to AdjustmentsList. Also did some required and columnexclude modifications.
-- 2020-03-19  JaThlk  SCXTEND-4039, Added the virtual, CreateOhAdjustmentsVirtual as data source for the group.
-- 2018-05-24  KoDelk  SCUXXW4-21488, Added missing functionality.
-- 2018-05-09  Lufelk  SCUXXW4-7605, Converted from dlgCreateOhAdjustments using MTG Version: 1.16
--------------------------------------------------------------------------------------
client CreateOHAdjustments;
component MPCCOM;
layer Core;
projection CreateOHAdjustmentsHandling;

-------------------------------- ASSISTANTS ---------------------------------
navigator {
   entry CreateOHAdjustmentsNavEntry parent MpccomNavigator.FinancialControlPeriodicOverheadAdjustments at index 100 {
      label = "Create OH Adjustments";
      assistant CreateOhAdjustmentsAssistant;
   }
}


-------------------------------- ASSISTANTS ---------------------------------
assistant CreateOhAdjustmentsAssistant using CreateOhAdjustments {
   label = "Create OH Adjustments";
   init command {
      variable DefaultValuesVar {
         type = DefaultValuesStruct;
      }
      execute {
         call GetDefaultValues() into DefaultValuesVar;
         set Company = DefaultValuesVar.Company;
         set DatingOfPostings = DefaultValuesVar.DatingOfPostings;
      }
   }

   steps {
      step {
         group CreateOhAdjustmentsGroup;
         list AdjustmentsBasedOnCostSourceList(DetailAdjustments) {
            visible = [CreateOhAdjustmentBasedOn = "CostSource"];
         }
         list AdjustmentsBasedOnPostingGroupList(DetailAdjustments) {
            visible = [CreateOhAdjustmentBasedOn = "PostingGroup"];
         }
      }

      final step {

      }
      cancelled step {

      }
   }
   finish command {
      variable ValidateResultVar Structure(AdjustmentWarningStruct);
      variable OhType Enumeration(PerOhAdjustmentOhType);
      execute {
         call ValidateOhTypes() into ValidateResultVar;
         if [ValidateResultVar.ValidateOk = false] {
            set OhType = ValidateResultVar.OhTypeBasedOnCostSource;
            if [OhType = null] {
               set OhType = ValidateResultVar.OhTypeBasedOnPostingGrp;
            }
            confirm("The adjustment percentage for ${OhType} is set to -100%.
            This means that all cost details with this OH Type on company ${ValidateResultVar.Company} will be set to 0 on transactions and part cost details.
            This will not possible to update with additional OH adjustments. Do you wish to proceed with this update?") {
               when CANCEL {
                  exit CANCEL;
               }
               when OK {
                  call StartCreateAdjustments();
               }
            }
         }
         else {
            call StartCreateAdjustments();
         }
         navigate back;
      }
   }
   cancel command {
      execute {
         navigate back;
      }
   }
   restart command {
      visible = [false];
   }
}


---------------------------------- GROUPS -----------------------------------
group CreateOhAdjustmentsGroup for CreateOhAdjustmentsVirtual {
   label = "Create OH Adjustments";
   lov CompanyFinanceRef with CompanyFinanceSelector {
      description = CompanyFinanceRef.Description;
   }
   field AccountingYear;
   field DatingOfPostings;
   radiogroup CreateOhAdjustmentBasedOn;

}


--------------------------------- SELECTORS ---------------------------------
@Override
selector CompanyFinanceSelector for CompanyFinance {
   field Company;
   field Description;
   field CurrencyCode;
   field CorrectionType;
   field ParallelAccCurrency;
   field DefAmountMethod;
   field CreationFinished;
   field MasterCompany;
}

selector CostSourceIdSelector for CostSource {
   field CostSourceId;
   field Description;
}

selector AdjustmentReasonIdSelector for PerOhAdjustmentReason {
   field AdjustmentReasonId;
   field Description;

}


----------------------------------- LISTS -----------------------------------
list AdjustmentsBasedOnCostSourceList for PerOhAdjustmentHistoryVirtual {
   label = "Adjustments";
   defaultfilter = [CreateOhAdjustmentBasedOn = "CostSource"];

   field OhTypeBasedOnCostSource {
      required = [true];
   }
   lov CostSourceIdRef with CostSourceIdSelector {
      required = [true];
      description = CostSourceIdRef.Description;
   }
   field AdjustmentPercentage {
      required = [true];
   }
   lov AdjustmentReasonIdRef with AdjustmentReasonIdSelector {
      required = [true];
      description = AdjustmentReasonIdRef.Description;
   }
   field Note {
      size = Large;
   }
}

list AdjustmentsBasedOnPostingGroupList for PerOhAdjustmentHistoryVirtual {
   label = "Adjustments";
   defaultfilter = [CreateOhAdjustmentBasedOn = "PostingGroup"];

   field OhTypeBasedOnPostingGrp {
      required = [true];
   }
   @DynamicComponentDependency COST
   lov PostingGroupIdRef with CostBucketPostingGroupOhSelector1 {
      required = [true];
      description = PostingGroupIdRef.Description;
   }
   field AdjustmentPercentage {
      required = [true];
   }
   lov AdjustmentReasonIdRef with AdjustmentReasonIdSelector {
      required = [true];
      description = AdjustmentReasonIdRef.Description;
   }
   field Note {
      size = Large;
   }
}

@DynamicComponentDependency COST
selector CostBucketPostingGroupOhSelector1 for CostBucketPostingGroupOh {
   static PostingGroupId;
   static Description;
}
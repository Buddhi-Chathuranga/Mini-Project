-----------------------------------------------------------------------------
--  Date    Sign    History
--  ------  ------  ---------------------------------------------------------
-- 180329   Lufelk  SCUXXW4-7820,  Created.
-----------------------------------------------------------------------------
projection PhrasesHandling;
component MPCCOM;
layer Core;
description "PhrasesHandling";
category Users;

@DynamicComponentDependency PURCH
include fragment ContractClauseTypeSelector;
@DynamicComponentDependency PURCH
include fragment PropagateRevisionDialog;


----------------------------- MAIN ENTRY POINTS -----------------------------
entityset MpccomPhrases for MpccomPhrase;


------------------------------ ENTITY DETAILS -------------------------------
@Override
entity MpccomPhrase {
   attribute ContractClause Boolean("TRUE", "FALSE") {
      required = [true];
   }
   attribute IsContractClauseEditable Text {
      fetch = "Mpccom_Phrase_API.Is_Contract_Clause_Editable(phrase_id)";
   }
   array DetailPhraseRevisionText(PhraseId) to MpccomPhraseRevision(PhraseId);

   @DynamicComponentDependency PURCH
   reference ContractClauseTypeRef(ContractClauseType) to ContractClauseTypeLov(ContractClauseType);

}

@Override
entity MpccomPhraseRevision {
   ludependencies = MpccomPhrase, MpccomPhraseText;

   attribute ApprovalStatus Enumeration(ContractClauseStatus) {
      editable = [false];
   }
   attribute ApprovalStatusDb Text {
      editable = [false];
   }
   attribute IsPhraseTextNull Text {
      fetch = "Mpccom_Phrase_Revision_API.Phrase_Text_Exists(phrase_id, revision_no)";
   }

   array DetailPhraseText(PhraseId, RevisionNo) to MpccomPhraseText(PhraseId, RevisionNo);

   action Activate {
      ludependencies = MpccomPhrase;
   }
   action Obsolete {
      supportwarnings = [true];
      ludependencies = MpccomPhrase;
   }

   action PropagateRevision Boolean {
      parameter PhaseInDate Date;
      parameter PhaseOutDate Date;
      parameter ReleaseStatusCheck Boolean;
      parameter ConfirmedStatusCheck Boolean;
      parameter SharedStatusCheck Boolean;
   }
}

@Override
entity MpccomPhraseText {
   ludependencies = MpccomPhraseRevision;
   attribute LanguageCode Text {
      label = "Language";
      format = lowercase;
   }
   reference LanguageCodeRef(LanguageCode) to IsoLanguage(LanguageCode) {
      label = "Language";
   }
   reference PhraseIdRef(PhraseId) to MpccomPhrase(PhraseId) {
      label = "Phrase ID";
   }
}

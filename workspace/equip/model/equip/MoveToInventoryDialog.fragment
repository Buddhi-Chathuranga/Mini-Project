--------------------------------------------------------------------------------------
-- Date        Sign    History
-- ----------  ------  ---------------------------------------------------------------
-- 2018-10-23  SHEPLK  SAUXXW4-1591, dlgMoveToInventory - Move To Inventory.
-- 2020-01-06  LASSLK  SAXTEND-1244, Implement LOV Filtering
--------------------------------------------------------------------------------------
fragment MoveToInventoryDialog;
component EQUIP;
layer Core;
description "Use this dialog to enter the basic information required for creating a work order.";
// Required fragment OrgCodeAllowedSiteLovSelector.

----------------------------- CLIENT FRAGMENTS ------------------------------
dialog DlgMoveToInventory for StructDlgMoveToInventory {
   label = "Move To Inventory";
   input(MchCode, Contract, WoContract);
   output(MchCode, Contract, WoNo, WoContract, WoMaintOrg, WoDirective);
   group MoveToInventory;

   commandgroup ButtonCommandGroup {

      command Ok {
         enabled = [WoContract != null and WoMaintOrg != null and WoDirective != null];
         variable WoNoVar Number;
         execute {

            call MoveToInventory(MchCode, Contract, WoContract, WoMaintOrg, WoDirective) into WoNoVar;
            inquire("Work Order - ${WoNoVar} is created and the Serial to be moved is placed in the returns tab. Do you want to view the Work Order?") {
               when YES {
                  navigate "page/PrepareWorkOrder/Form?$filter=WoNo eq $[WoNoVar]";
               }
               when NO {
                  exit CANCEL;
               }
            }

         }

      }
      command Cancel;
   }
}

group MoveToInventory for StructDlgMoveToInventory {
   label = "Move To Inventory";
   lov WoContractRef with ReferenceUserAllowedSiteLovSelector {
      search = Contarct, ContractDesc;
      size = Medium;
      label = "WO Site";
      description = WoContractRef.ContractDesc;
   }
   lov WoMaintOrgRef with ReferenceOrgCodeAllowedSiteLovSelector {
      search = OrgCode, Description;
      size = Medium;
      label = "Maint Org";
      description = WoMaintOrgRef.Description;
   }
   field WoDirective {
      size = Large;
      label = "Directive";
   }
}

--------------------------- PROJECTION FRAGMENTS ----------------------------
structure StructDlgMoveToInventory {
   attribute MchCode Text {
      label = "Object ID";
   }
   attribute Contract Text {
      label = "Site";
   }
   attribute WoContract Text {
      label = "WO Site";
      maxlength = 5;
      format = uppercase;
   }
   attribute WoMaintOrg Text {
      label = "Maint Org";
      maxlength = 8;
      format = uppercase;
   }
   attribute WoDirective Text {
      label = "Directive";
      maxlength = 60;
   }
   attribute WoNo Text {
      label = "WoNo";
   }
   reference WoContractRef(WoContract) to UserAllowedSiteLov(Contract) {
      label = "WO Site";
   }
   reference WoMaintOrgRef(WoContract, WoMaintOrg) to OrgCodeAllowedSiteLov(Contract, OrgCode) {
      label = "Maint Org";
   }
}

---------------------------------- ACTIONS ----------------------------------
action MoveToInventory Number {
   initialcheck implementation;
   ludependencies = EquipmentSerial;
   parameter MchCode Text;
   parameter MchContract Text;
   parameter WoContract Text;
   parameter WoMaintOrg Text;
   parameter WoDirective Text;
}
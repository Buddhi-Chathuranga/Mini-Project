
-------------------------------------------------------------------------------------------------
-- Date        Sign    History
-- ----------  ------  --------------------------------------------------------------------------
-- 2022-06-20  RasDlk  SCDEV-10342, Modified the command PrintDeliveryNoteCommand by passing ReceiverAddrId, SenderId, and SenderType to GetShipmentResultKeys method.
-- 2021-03-11  SBalLK  SC2020R1-12873, Added dynamic dependency for navigator entries.
-- 2021-03-02  WaSalk  SC2020R1-12582, Removed fragment LocalizationControlCenterHandler and modified MiscDeliveryInfoCommand and PrintDeliveryNoteCommand
-- 2021-03-02          to support LCC concept
-- 2021-02-23  Aabalk  SC2020R1-12430, Replaced SenderGroup with ShipmentSenderGroup.
-- 2020-12-03  WaSalk  GESPRING20-6039, Added DelnoteNo to GetShipmentResultKeys() to support gelr functionalities.
-- 2020-11-18  WaSalk  GESPRING20-6002, Modified MiscDeliveryInfoCommand get correct value to QtyEurPallets.
-- 2020-10-23  MaEelk  SC2020R1-10866, Modified the command MiscDeliveryInfoCommand and passed null to QtyEurPalletsVar when calling the dialog ModifyMiscDeliveryInfoDialog.
-- 2020-10-21  RasDlk  SC2020R1-10680, Removed CustomerNo from the DeliveryNoteSelector.
-- 2020-09-10  RasDlk  SC2020R1-9757, Removed the ampersand from the label and replaced it with and in the group WeightAndVolumeTotalsGroup.
-- 2020-06-11  WaSalk  SC2020R1-7346, Addded AltDelnoteNoChronologicEnabled in PrintDeliveryNoteCommand to enable in Invalid state when
-- 2020-06-11          gelr functionalities applicable for Italy.
-- 2020-02-11  Aabalk  SCSPRING20-1465, Added contact widget for ReceiverId.
-- 2020-01-21  Dihelk  GESPRING20-3671, Added delivery_reason_id MiscDeliveryInfoCommand to navigate MiscDeliveryInfoDialog.
-- 2020-01-21  WaSalk  GESPRING20-1656, Added command MiscDeliveryInfoCommand to navigate MiscDeliveryInfoDialog.
-- 2020-01-23  Dipelk  GESPRING20-1774, Added contract on the action GetShipmentResultKeys to support modify_date_applied functionality.
-- 2019-11-22  MeAblk  SCSPRING20-176, Added SenderInfoGroup and ReceiverInfoGroup.
-- 2019-11-01  Aabalk  SCXTEND-1333, Changed startup mode to search.
-- 2019-06-12  RasDlk  SCUXXW4-21668, Added UoM in the Weight and Volume Totals Group Box.
-- 2019-05-28  ErRalk  SCUXXW4-21846, Added ShipReceiverAddrSelector for reference ReceiverIdRef
-- 2019-05-25  ErRalk  SCUXXW4-21662, Added OrderDeliveryTermLovSelector fragment.
-- 2019-05-25  ErRalk  SCUXXW4-21663, Removed OrderNo from overridden DeliveryNoteSelector.
-- 2019-04-23  Hpetse  SCUXXW4-4889, Changed status badges
-- 2019-01-02  RasDlk  SCUXXW4-14732, Implemented Total Net Weight, Gross Weight and Volume.
-- 2018-12-06  ErRalk  SCUXXW4-14588, Implement Address tab.
-- 2018-09-13  RaVdlk  SCUXXW4-4889, Converted from tbwShipmentDelivLine using MTG Version: 1.26
-------------------------------------------------------------------------------------------------

client ShipmentDeliveryNoteAnalysis;
component SHPMNT;
layer Core;
projection ShipmentDeliveryNoteAnalysis;
include fragment UserAllowedSiteLovSelector;
@DynamicComponentDependency ORDER
include fragment CustOrdCust1Selector;
include fragment DeliveryNote;
include fragment OrderDeliveryTermLovSelector;
include fragment ShipReceiverAddrSelector;
include fragment ShipmentReceiverSelector;
include fragment ShipmentSenderSelector;
-- gelr:modify_date_applied, begin
include fragment ModifyMiscDeliveryInfoDialog;
-- gelr:modify_date_applied, end

----------------------------- NAVIGATOR ENTRIES -----------------------------
navigator {
   @DynamicComponentDependency ORDER
   entry SalesQueryDeliveryNoteNavEntry parent OrderNavigator.ShippingShipmentDelivery at index 1200 {
      label = "Shipment Delivery Note Analysis";
      page Form;
   }
}
navigator {
   entry ShipmentQueryDeliveryNoteNavEntry parent ShpmntNavigator.OutboundShipmentDelivery at index 100 {
      label = "Shipment Delivery Note Analysis";
      page Form;
   }
}
-------------------------------- MAIN PAGES ---------------------------------
page Form using DeliveryNoteSet {
   label = "Shipment Delivery Note Analysis";
   startupmode = search;
   stateindicator DeliveryNoteStateIndicator;
   selector DeliveryNoteSelector;
   group DeliveryNoteGroup;
   arrange {
      group SenderInfoGroup;
      group ReceiverInfoGroup;
   }
   group WeightAndVolumeTotalsGroup;
   tabs {
      tab {
         label = "Shipment Lines";
         list ShipmentDelivLineList(DeliveryNoteShipmentLineLineArray);
      }
      tab {
         label = "Address";
         group ReceiverGroup;
         group ShipmentSenderGroup;
         group ForwarderGroup;
      }
   }
   commandgroup DeliveryNoteCmdGroup {
      label = "Delivery Note Command Group";
      command PrintDeliveryNoteCommand;
   }
   -- gelr:modify_date_applied, begin
   command MiscDeliveryInfoCommand;
   -- gelr:modify_date_applied, end
}

--------------------------------- SELECTORS ---------------------------------
@Override
selector DeliveryNoteSelector for DeliveryNote {
   label = "";
   static DelnoteNo;
   static Contract {
      label = "Site";
   }
   static ShipmentId;
   static ReceiverId;
   badge Objstate {
      label = "Status";
      emphasis Progress6 = [DelnoteNoRef.Objstate = "Created"];
      emphasis Progress12 = [DelnoteNoRef.Objstate = "Printed"];
      emphasis StateCancelled = [DelnoteNoRef.Objstate = "Invalid"];
      style = TextOnly;
   }
   fieldorder = DelnoteNo, Contract, ShipmentId, ReceiverId, Objstate;
}

---------------------------------- GROUPS -----------------------------------
group WeightAndVolumeTotalsGroup for DeliveryNote {
   label = "Weight and Volume Totals";
   lov DeliveryNoteWeightAndVolumeTotalRef with ReferenceDeliveryNoteWeightAndVolumeTotalSelector {
      label = "Total Net Weight";
      description = "${DeliveryNoteWeightAndVolumeTotalRef.TotalNetWeight} ${CompanyInventInfoRef.UomForWeight}";
      hidekey = [true];
   }
   lov DeliveryNoteWeightAndVolumeTotalRef with ReferenceDeliveryNoteWeightAndVolumeTotalSelector {
      label = "Total Gross Weight";
      description = "${DeliveryNoteWeightAndVolumeTotalRef.TotalGrossWeight} ${CompanyInventInfoRef.UomForWeight}";
      hidekey = [true];
   }
   lov DeliveryNoteWeightAndVolumeTotalRef with ReferenceDeliveryNoteWeightAndVolumeTotalSelector {
      label = "Total Volume";
      description = "${DeliveryNoteWeightAndVolumeTotalRef.TotalVolume} ${CompanyInventInfoRef.UomForVolume}";
      hidekey = [true];
   }
}

@Override
group DeliveryNoteGroup for DeliveryNote {
   exclude ReceiverId, ReceiverDescription;
}

group SenderInfoGroup for DeliveryNote {
   label = "Sender";

   field SenderType;
   field SenderIdRef;
   field SenderDescription;

}

group ReceiverInfoGroup for DeliveryNote {
   label = "Receiver";

   field ReceiverType;
   field ReceiverRef {
      contactwidget {
         enabled = [ReceiverId != null and ReceiverType = "Customer"];
         source = Customer;
      }
   }
   field ReceiverDescription;
}

group ShipmentSenderGroup for DeliveryNote {
   label = "Sender";
   lov CompanyAddressRef with ReferenceCompanyAddressSelector {
      label = "Address ";
      filterlabel = "Sender Address";
      description = SenderAddressName;
   }
   addressfield {
      address1 SenderAddress1;
      address2 SenderAddress2;
      address3 SenderAddress3;
      address4 SenderAddress4;
      address5 SenderAddress5;
      address6 SenderAddress6;
      city SenderCity;
      country SenderCountry;
      county SenderCounty;
      state SenderState;
      zipcode SenderZipCode;
      countrycode SenderCountryName;
      label = "Address Details";
      size = Medium;
   }
}

----------------------------- STATE INDICATORS ------------------------------
@Override
stateindicator DeliveryNoteStateIndicator for DeliveryNote {
   state "Created" {
      completed = 50%;
      emphasis Progress6 = [DelnoteNoRef.Objstate = "Created"];
   }
   state "Printed" {
      completed = 100%;
      emphasis Progress12 = [DelnoteNoRef.Objstate = "Printed"];
   }
   state "Invalid" {
      completed = 100%;
      emphasis StateCancelled = [DelnoteNoRef.Objstate = "Invalid"];
   }
}
--------------------------------- COMMANDS ----------------------------------

command PrintDeliveryNoteCommand for DeliveryNote {
   label = "Print";
   enabled = [(DelnoteNoRef.Objstate != "Invalid" and not "ALT_DELNOTE_NO_CHRONOLOGIC" in EnabledLccParams) or("ALT_DELNOTE_NO_CHRONOLOGIC" in EnabledLccParams)];
   execute {
      call GetShipmentResultKeys(Contract, ShipmentId, CustomerNo, ReceiverId, ReceiverTypeDb, DelnoteNo, ReceiverAddrId, SenderId, SenderType)into ResultKeysStr;
      printdialog ResultKeysStr;
   }
}

-- gelr:modify_date_applied, begin
command MiscDeliveryInfoCommand for DeliveryNote {
   label = "Misc Delivery Info";
   enabled = ["MODIFY_DATE_APPLIED" in EnabledLccParams and EnabledLccParams != ""];
   execute {
      dialog ModifyMiscDeliveryInfoDialog(Company, DelnoteNo, DelNotePrintDate, TransportDate, DeliveryReasonId, QtyEurPallets) {
         when CANCEL {
            exit CANCEL;
         }
      }
   }
}
-- gelr:modify_date_applied, end

@Override
list ShipmentLovList for Shipment {
   label = "";
   static ShipmentId;
   static ReceiverType;
   static ReceiverId;
   static SourceRefType;
   static Contract;
   static SenderName;
   static ShipmentType;
   static ParentConsolShipmentId {
      label = "Consolidate Shipment ID";
   }
}
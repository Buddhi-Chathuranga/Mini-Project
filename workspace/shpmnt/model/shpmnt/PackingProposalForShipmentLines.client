--  Date       Sign    History
-- ----------  ------  ---------------------------------------------------------
-- 2021-07-26  RasDlk  SC21R2-1023, Renamed the field PackByReservation by PackByReservationLine and ExcludeFullyReservedHu by ExclFullyReservedHu.
-- 2021-07-12  Aabalk  SC21R2-1027, Added PackingProposalMethodGroup and PackingProposalSortingGroup.
-- 2021-05-28  Aabalk  SC21R2-1019, Created.
-- ----------  ------  ---------------------------------------------------------

client PackingProposalForShipmentLines;
component SHPMNT;
layer Core;
projection PackingProposalForShipmentLinesHandling;

----------------------------- NAVIGATOR ENTRIES -----------------------------
navigator {
   entry PackingProposalForShipmentLinesNavEntry parent ShpmntNavigator.OutboundShipmentBasicData at index 150 {
      label = "Packing Proposal for Shipment Lines";
      page Form;
   }
}


-------------------------------- MAIN PAGES ---------------------------------
page Form using PackingProposals {
   label = "Packing Proposal for Shipment Lines";
   selector ShipPackProposalSelector;
   group PackingProposalGroup;
   list PackingProposalHuTypeList(PackingProposalHuTypeArray);
   group PackingProposalMethodGroup;
   group PackingProposalSortingGroup;
}


--------------------------------- SELECTORS ---------------------------------
@Override
selector ShipPackProposalSelector for ShipPackProposal {
   label = "${PackingProposalId} - ${Description}";
   static PackingProposalId;
   static Description;
}


---------------------------------- GROUPS -----------------------------------
group PackingProposalGroup for ShipPackProposal {
   label = "";
   field PackingProposalId;
   field Description {
      size = Large;
   }
}

group PackingProposalMethodGroup for ShipPackProposal {
   label = "Packing Methods";

   field PackBySourceObject {
      size = Small;
   }
   field PackByReservationLine {
      size = Small;
   }
   field PackByPiece {
      size = Small;
   }
   field ExclFullyReservedHu;
   field AllowMixSourceObject {
      visible = [PackBySourceObject];
   }
}

group PackingProposalSortingGroup for ShipPackProposal {
   label = "Sorting";

   field SortPriority1;
   field SortPriority2;
}


----------------------------------- LISTS -----------------------------------
list PackingProposalHuTypeList for ShipPackProposalHuType {
   editmode = SingleCellEdit;
   label = "Handling Unit Types to use";
   field HandlingUnitTypeRef;
   field MaxVolumeUtilization;
   computedfield OperativeVolumeUtilization {
      value = "#{MaxVolumeUtilization * HandlingUnitTypeRef.MaxVolumeCapacity}";
   }
   field HandlingUnitTypeRef.MaxVolumeCapacity;
   field HandlingUnitTypeRef.UomForVolume {
      label = "UoM for Volume";
   }
   measure HandlingUnitTypeRef.MaxWeightCapacity(HandlingUnitTypeRef.UomForWeight);
   field HandlingUnitTypeRef.Width;
   field HandlingUnitTypeRef.Height;
   field HandlingUnitTypeRef.Depth;
   field HandlingUnitTypeRef.UomForLength {
      label = "UoM for Length";
   }
}


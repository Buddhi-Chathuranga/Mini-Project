---------------------------------------------------------------------------------------
-- Date       Sign   History
-- ---------- ------ ------------------------------------------------------------------
-- 2019-10-01 Tajalk SAXTEND-208, Created
-- 2020-09-23 MDAHSE Change projection description for better discoverability.
-- 2021-09-27 DEEKLK AMZDOC-1186, Removed AttributeType.
---------------------------------------------------------------------------------------
projection TechnicalObjectReferenceAttachmentHandling;
component APPSRV;
layer Core;
description "Object connection service for handling technical data/characteristics in Attachment panel";
category Users;

--------------------------------- FRAGMENTS ---------------------------------
include fragment TechnicalSpecificationList;
include fragment TechnicalAttribBothSelector;

----------------------------- MAIN ENTRY POINTS -----------------------------
entityset TechnicalObjectVirtualSet for TechnicalObjectVirtual;

------------------------------ ENTITY DETAILS -------------------------------
@Override
entity TechnicalObjectReference {
   attribute ClassName Text {
      fetch = "Technical_Class_API.Get_Description(technical_class)";
   }
}
@Override
entity TechnicalSpecification {
   crud = Read, Delete, Update;
   from = "technical_specification_both";

   attribute ValueText Text {
      fetch = "value_text";
      label = "Value Text";
      maxlength = 20;
      editable = [true];
   }
   attribute ValueNo Number {
      fetch = "value_no";
      label = "Value Number";
      maxlength = 0;
      editable = [true];
   }
   attribute ObjTypeMod Text {
      fetch = "CASE WHEN OBJTYPE = 'TechnicalSpecNumeric' THEN '#' ELSE 'A#' END";
      editable = [false];
   }
   attribute ValueBoth Text {
      fetch = "CASE WHEN VALUE_TEXT IS NOT NULL THEN VALUE_TEXT ELSE TO_CHAR(VALUE_NO) END";
      editable = [true];
   }
   attribute GroupName Text {
      fetch = "GROUP_NAME";
      editable = [false];
   }
   attribute GroupDescription Text {
      fetch = "Technical_Group_API.Get_Description(GROUP_NAME)";
      editable = [false];
   }
   attribute GroupOrder Number {
      fetch = "Technical_Group_API.Get_Group_Order(TECHNICAL_CLASS, GROUP_NAME)";
      editable = [false];
   }

   reference ValueTextRef(TechnicalClass, Attribute, ValueBoth) to TechnicalAttribText(TechnicalClass, Attribute, ValueText) {
      label = "Value Text";
   }
}

--------------------------------- VIRTUALS ----------------------------------
virtual TechnicalObjectVirtual {
   attribute LuName Text;
   attribute KeyRef Text;
   attribute TechnicalClass Text;
   attribute TechnicalClassDes Text;
   attribute TechnicalSpecNo Number;
   attribute Status Text;

   reference TechnicalClassRef(TechnicalClass) to TechnicalClass(TechnicalClass);

   array AttrArray(TechnicalSpecNo) to TechnicalSpecification(TechnicalSpecNo);

   action RefreshAttributes {
      ludependencies = TechnicalSpecification, TechnicalObjectVirtual;
   }
}

-------------------------------- STRUCTURES ---------------------------------
structure SelectTechClassStructure {
   attribute LuName Text;
   attribute KeyRef Text;
   attribute TechnicalClass Text;

   reference TechnicalClassRef(TechnicalClass) to TechnicalClass(TechnicalClass);
}

--------------------------------- FUNCTIONS ---------------------------------
function GetResultSet List<Entity(TechnicalObjectReference)> {
   parameter LuName Text;
   parameter KeyRef Text;
   where = "lu_name = :LuName AND key_ref = :KeyRef";
}

---------------------------------- ACTIONS ----------------------------------
action CreateModifyTechnicalClass {
   initialcheck implementation;
   ludependencies = TechnicalObjectReference;

   parameter LuName Text;
   parameter KeyRef Text;
   parameter TechnicalClass Text;
}
action RemoveTechnicalClass {
   initialcheck implementation;
   ludependencies = TechnicalObjectReference;

   parameter LuName Text;
   parameter KeyRef Text;
}
action ChangeStatusTechnicalClass {
   initialcheck implementation;
   ludependencies = TechnicalObjectReference;

   parameter TechnicalSpecNo Number;
}


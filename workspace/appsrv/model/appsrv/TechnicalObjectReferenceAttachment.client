---------------------------------------------------------------------------------------
-- Date       Sign   History
-- ---------- ------ ------------------------------------------------------------------
-- 2019-10-01 Tajalk SAXTEND-208, Created
-- 2020-01-21 LASSLK SAXTEND-1244,Implement LOV Filtering
---------------------------------------------------------------------------------------
client TechnicalObjectReferenceAttachment;
component APPSRV;
layer Core;

projection TechnicalObjectReferenceAttachmentHandling;
--------------------------------- FRAGMENTS ---------------------------------
include fragment TechnicalSpecificationList;
include fragment TechnicalAttribBothSelector;

----------------------------- NAVIGATOR ENTRIES -----------------------------

-------------------------------- MAIN PAGES ---------------------------------
objectconnection TechnicalObjectReference using TechnicalObjectReferenceSet {
   label = "Characteristics";
   list TechnicalObjectReferenceList using GetResultSet(luname, keyref);

   command NewModifyTechnicalObject;
   command ClearTechnicalObject;
}

--------------------------------- COMMANDS ----------------------------------
command NewModifyTechnicalObject for TechnicalObjectReference {
   mode = Global;
   label = "Select Technical Class";
   execute {
      dialog AttachTechnicalObjAssistant(luname, keyref) {
         when OK {
            success("Technical class info added");
         }
      }
   }
}
command ClearTechnicalObject for TechnicalObjectReference {
   mode = Global;
   label = "Clear Technical Class";
   execute {
      call RemoveTechnicalClass(luname, keyref);
      info("Technical class removed");
   }
}
command ShowAttributes for TechnicalObjectReference {
   label = "Attributes";

   execute {
      assistant TechClassAttrAssistant(TechnicalSpecNo, TechnicalClass, ClassName, OkYesNo) {
         when OK {
            exit;
         }
         when CANCEL {
            exit;
         }
      }
   }
}
command ApproveTechnicalClass for TechnicalObjectReference {
   enabled = [OkYesNo = "NotApproved"];
   label = "Set to Approved";
   execute {
      call ChangeStatusTechnicalClass(TechnicalSpecNo);
   }
}
command UnApproveTechnicalClass for TechnicalObjectReference {
   enabled = [OkYesNo = "Approved"];
   label = "Set to Not Approve";
   execute {
      call ChangeStatusTechnicalClass(TechnicalSpecNo);
   }
}
command RefreshAttributes for TechnicalObjectVirtual {
   mode = Global;
   label = "Refresh Attributes";
   execute {
      call RefreshAttributes();
   }
}

----------------------------- VISUAL COMPONENTS -----------------------------

----------------------------------- LISTS -----------------------------------
list TechnicalObjectReferenceList for TechnicalObjectReference {
   card TechnicalObjectAttachmentCard;

   command ShowAttributes;
   command ApproveTechnicalClass;
   command UnApproveTechnicalClass;
}

@Override
list TechnicalSpecAllList for TechnicalSpecification {
   command RefreshAttributes;
}

---------------------------------- DIALOGS ----------------------------------
dialog AttachTechnicalObjAssistant for SelectTechClassStructure {
   label = "Select Technical Class";

   input(LuName, KeyRef);

   group TechnicalClassInfo;

   command Ok {
      label = "OK";
      enabled = [TechnicalClassRef != null];
      execute {
         call CreateModifyTechnicalClass(LuName, KeyRef, TechnicalClass);
         exit OK;
      }
   }
   command Cancel {
      execute {
         exit CANCEL;
      }
   }
}

-------------------------------- ASSISTANTS ---------------------------------
assistant TechClassAttrAssistant using TechnicalObjectVirtualSet {
   label = "Techncial Class Attributes";

   input(TechnicalSpecNo, TechnicalClass, TechnicalClassDes, Status);

   steps {
      step {
         group TechnicalClassVirtualInfo;
         list TechnicalSpecAllList(AttrArray);

      }
      final step {

      }
      cancelled step {

      }
   }
   finish command {
      execute {
         exit OK;
      }
   }
   cancel command {
      execute {
         exit CANCEL;
      }
   }
}

---------------------------------- GROUPS -----------------------------------
group TechnicalClassInfo for SelectTechClassStructure {
   label = "";
   lov TechnicalClassRef with ReferenceTechnicalClassSelector {
      search = TechnicalClass, Description;
      description = TechnicalClassRef.Description;
      label = "Technical Class";
      size = FullWidth;
   }
}
group TechnicalClassVirtualInfo for TechnicalObjectVirtual {
   label = "";
   field TechnicalClass {
      editable = [false];
   }
   field TechnicalClassDes {
      label = "Description";
      editable = [false];
   }
   badge Status {
      size = Small;
      style = TextOnly;
      showlabel = false;
      editable = [false];
      emphasis StatePreliminary = [Status = "NotApproved"];
      emphasis StateActive = [Status = "Approved"];
   }
}

----------------------------------- CARDS -----------------------------------
card TechnicalObjectAttachmentCard for TechnicalObjectReference {
   label = "${TechnicalClass} - ${ClassName}";
   badge OkYesNo {
      size = Small;
      style = TextOnly;
      showlabel = false;
      emphasis StatePreliminary = [OkYesNo = "NotApproved"];
      emphasis StateActive = [OkYesNo = "Approved"];
   }
   field OkSign;
   field DtOk;
}





-----------------------------------------------------------------------------
--
--  Logical unit: CreatePickListForCustomerOrders
--  Component:    ORDER
--
--  IFS Developer Studio Template Version 3.0
--
--  Date        Sign    History
--  ------      ------  ---------------------------------------------------------
--  2018-04-10  IzShlk  SCUXXW4-9647, Introduced Start_Create_Pick_List___() to create pick list for selected records.
-----------------------------------------------------------------------------

layer Core;

PROCEDURE Start_Create_Pick_List___ (
   full_selection_ IN VARCHAR2 ) 
IS
   selection_                 VARCHAR2(4000) := CONCAT(full_selection_, ';');
   current_selection_         VARCHAR2(200);
   current_order_no_          customer_order_tab.order_no%TYPE; 
   attr_                      VARCHAR2(4000);
BEGIN
   Client_SYS.Clear_Attr(attr_);
   -- Get all the selected Order numbers to the attr_
   WHILE (INSTR(selection_, ';') > 0) LOOP
      current_selection_  := substr(selection_, 0, INSTR(selection_, ';'));
      current_order_no_   := Client_SYS.Get_Key_Reference_Value(current_selection_, 'ORDER_NO');
      Customer_Order_Initial_Check___(current_order_no_);
      Client_SYS.Add_To_Attr('START_EVENT', '70', attr_);
      Client_SYS.Add_To_Attr('ORDER_NO', current_order_no_, attr_);
      Client_SYS.Add_To_Attr('END', '', attr_); 
      selection_ := substr(selection_, INSTR(selection_, ';')+1);  
   END LOOP;
   -- Create pick list for all the selected order numbers.
   Customer_Order_Flow_API.Start_Create_Pick_List__(attr_); 
END Start_Create_Pick_List___;

FUNCTION Check_All_Lines_Connected___(
   selection_ IN VARCHAR2 )  RETURN License_Info_Rec
IS
   selected_records_     VARCHAR2(32000) := CONCAT(selection_, ';');
   current_selection_    VARCHAR2(32000); 
   order_no_             VARCHAR2 (200);
   license_connected_    NUMBER ;
   license_info_rec_     License_Info_Rec := NULL;
BEGIN
    WHILE (INSTR(selected_records_, ';') > 0) LOOP
      license_connected_ :=0;
      current_selection_  := substr(selected_records_, 0, INSTR(selected_records_, ';'));
      order_no_           := Client_SYS.Get_Key_Reference_Value(current_selection_, 'ORDER_NO');

      Customer_Order_Flow_API.Check_All_License_Connected(license_connected_,order_no_);

      IF (license_connected_ = 1)THEN
         license_info_rec_.license_connected := 1;
      ELSIF (license_connected_ = 2) THEN
         license_info_rec_.non_export_connected := 2;
      ELSIF (license_connected_ = 3) THEN
         license_info_rec_.export_controlled := 3;
      END IF;
      selected_records_ := substr(selected_records_, INSTR(selected_records_, ';')+1);   
   END LOOP;
   RETURN license_info_rec_; 
END Check_All_Lines_Connected___;
-----------------------------------------------------------------------------
--
--  Logical unit: CustomerOrderLinesCommissionsHandling
--  Component:    ORDER
--
--  IFS Developer Studio Template Version 3.0
--
--  Date       Sign    History
--  ------     ------  ---------------------------------------------------------
-- 2018-08-28  SeJalk  SCUXXW4-971, created.
-----------------------------------------------------------------------------

layer Core;


FUNCTION Calc_Com_From_Cust_Ord_Line___ (
   selection_             IN VARCHAR2,
   overwrite_manual_data_ IN NUMBER ) RETURN VARCHAR2
IS
   selection_in_        VARCHAR2(32000) := CONCAT(selection_, ';');
   current_selection_   VARCHAR2(32000);
   order_no_            Order_Line_Commission_tab.order_no%TYPE;
   line_no_             Order_Line_Commission_tab.line_no%TYPE;
   rel_no_              Order_Line_Commission_tab.rel_no%TYPE;
   line_item_no_        Order_Line_Commission_tab.line_item_no%TYPE;
   info_                VARCHAR2(2000);
BEGIN
   WHILE (INSTR(selection_in_, ';') > 0) LOOP
      current_selection_     := substr(selection_in_, 0, INSTR(selection_in_, ';'));
      order_no_              := Client_SYS.Get_Key_Reference_Value(current_selection_, 'ORDER_NO');
      line_no_               := Client_SYS.Get_Key_Reference_Value(current_selection_, 'LINE_NO');
      rel_no_                := Client_SYS.Get_Key_Reference_Value(current_selection_, 'REL_NO');
      line_item_no_          := Client_SYS.Get_Key_Reference_Value(current_selection_, 'LINE_ITEM_NO');
      
      Customer_Order_Line_Initial_Check___(order_no_, line_no_, rel_no_, line_item_no_);
      Commission_Calculation_API.Calc_Com_From_Cust_Ord_Line(info_, order_no_, line_no_, rel_no_, line_item_no_, overwrite_manual_data_);
      selection_in_          := substr(selection_in_, INSTR(selection_in_, ';')+1);
   END LOOP;
   RETURN info_;
END Calc_Com_From_Cust_Ord_Line___;


FUNCTION Check_Calculated_Com___(
   selection_ IN VARCHAR2 ) RETURN BOOLEAN
IS
   selection_in_        VARCHAR2(32000) := CONCAT(selection_, ';');
   current_selection_   VARCHAR2(32000);
   order_no_            Order_Line_Commission_tab.order_no%TYPE;
   line_no_             Order_Line_Commission_tab.line_no%TYPE;
   rel_no_              Order_Line_Commission_tab.rel_no%TYPE;
   line_item_no_        Order_Line_Commission_tab.line_item_no%TYPE;
   commission_line_no_  Order_Line_Commission_tab.commission_line_no%TYPE;
   rowstate_             Order_Line_Commission_tab.rowstate%TYPE;
   comm_recalc_flag_    Order_Line_Commission_tab.commission_recalc_flag%TYPE;
   
   CURSOR Get_Row_Status IS
   SELECT ROWSTATE, COMMISSION_RECALC_FLAG FROM ORDER_LINE_COMMISSION_TAB WHERE 
   ORDER_NO = order_no_ AND
   LINE_NO = line_no_ AND
   REL_NO = rel_no_ AND
   LINE_ITEM_NO = line_item_no_ AND
   COMMISSION_LINE_NO = commission_line_no_;
   
BEGIN
  WHILE (INSTR(selection_in_, ';') > 0) LOOP
      current_selection_     := substr(selection_in_, 0, INSTR(selection_in_, ';'));
      order_no_              := Client_SYS.Get_Key_Reference_Value(current_selection_, 'ORDER_NO');
      line_no_               := Client_SYS.Get_Key_Reference_Value(current_selection_, 'LINE_NO');
      rel_no_                := Client_SYS.Get_Key_Reference_Value(current_selection_, 'REL_NO');
      line_item_no_          := Client_SYS.Get_Key_Reference_Value(current_selection_, 'LINE_ITEM_NO');
      commission_line_no_    := Client_SYS.Get_Key_Reference_Value(current_selection_, 'COMMISSION_LINE_NO'); 
      OPEN Get_Row_Status;
      FETCH Get_Row_Status INTO rowstate_, comm_recalc_flag_;
      CLOSE Get_Row_Status;
      IF  rowstate_ = 'Created' OR comm_recalc_flag_ = 'NEEDCALCULATION' THEN
         RETURN TRUE;
      END IF;
      selection_in_          := substr(selection_in_, INSTR(selection_in_, ';')+1);
   END LOOP;
   RETURN FALSE;
END Check_Calculated_Com___;

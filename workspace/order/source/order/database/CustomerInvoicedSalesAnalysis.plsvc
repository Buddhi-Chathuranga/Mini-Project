-----------------------------------------------------------------------------
--
--  Logical unit: CustomerInvoicedSalesAnalysis
--  Component:    ORDER
--
--  IFS Developer Studio Template Version 3.0
--
--  Date       Sign     History
--  ------     ------   ---------------------------------------------------------
-- 2018-08-15  KhVese   SCUXXW4-12122, Renamed and modified methods Unique_Currency_For_Selected_Rec___, Get_Amount_For_Selected_Rec___ and removed methods UniqueCurrencyForAllRec and GetAmountForAllRec
-- 2018-05-24  KhVese   SCUXXW4-924, Implemented methods Unique_Currency_For_All_Rec___ and Get_Amount_For_All_Rec___. Also renamed and
-- 2018-05-24           modified methods Get_Record_Set___ and Get_Net_Dom_Amount___.
-- 2018-05-17  RaVdlk   SCUXXW4-924, Created the funstions Get_Record_Set___,Get_Net_Dom_Amount___,Get_Total_Cost___
-- 2018-05-14  RaVdlk   SCUXXW4-924, Removed the function Catalog_No_Lov___
-- 2018-05-03  RaVdlk   SCUXXW4-924, Created the function Catalog_No_Lov___
-----------------------------------------------------------------------------

layer Core;

FUNCTION Get_Unique_Currency_Code___ (
   selection_ IN Text_Arr) RETURN VARCHAR2
IS
   current_selection_    VARCHAR2(32000);
   currency_code_        VARCHAR2(3);
   current_currency_     VARCHAR2(3);
   invoice_id_           NUMBER;
   company_id_           VARCHAR2(20);
BEGIN
   currency_code_ := NULL;
   FOR i_ IN selection_.first..selection_.last LOOP
      current_selection_  := selection_(i_);
      invoice_id_   := Client_SYS.Attr_Value_To_Number(Client_SYS.Get_Key_Reference_Value(current_selection_, 'INVOICE_ID'));
      company_id_   := Client_SYS.Get_Key_Reference_Value(current_selection_, 'COMPANY');
      current_currency_ := Company_Finance_API.Get_Currency_Code(company_id_);

      IF (NVL(currency_code_, '*') != NVL(current_currency_, '*'))THEN
         IF (currency_code_ IS NULL)THEN
            currency_code_ := current_currency_; 
         ELSE
            currency_code_ := NULL;
            EXIT;
         END IF;
      END IF;
   END LOOP;
   RETURN currency_code_;
END Get_Unique_Currency_Code___ ; 


FUNCTION Get_Total_Cost_And_Dom_Amount___(
   selection_ IN Text_Arr) RETURN Total_Cost_And_Dom_Amount_Struc_Rec
IS
   current_selection_    VARCHAR2(32000);
   net_dom_amount_       NUMBER;
   total_amount_         NUMBER;
   curr_net_dom_amount_  NUMBER;
   item_id_              NUMBER;
   invoice_id_           NUMBER;
   company_id_           VARCHAR2(20);
   total_cost_           NUMBER;
   total_amounts_rec_    Total_Cost_And_Dom_Amount_Struc_Rec := NULL;
     
   CURSOR get_net_amount IS
   SELECT net_dom_amount 
   FROM CUSTOMER_ORDER_INV_ITEM_JOIN 
   WHERE company = company_id_ AND invoice_id =invoice_id_ AND item_id =item_id_;
     
   CURSOR get_total IS
   SELECT Customer_Order_Inv_Item_API.Get_Total_Cost(company, invoice_id, item_id) total_cost 
   FROM CUSTOMER_ORDER_INV_ITEM_JOIN 
   WHERE company = company_id_ AND invoice_id =invoice_id_ AND item_id =item_id_
   AND  price_adjustment = 'FALSE';
   
BEGIN
   net_dom_amount_ := 0;
   total_amount_ := 0;
   
   FOR i_ IN selection_.first..selection_.last LOOP
      curr_net_dom_amount_ := 0;
      total_cost_ :=0;
      current_selection_  := selection_(i_);
      invoice_id_   := Client_SYS.Attr_Value_To_Number(Client_SYS.Get_Key_Reference_Value(current_selection_, 'INVOICE_ID'));
      company_id_   := Client_SYS.Get_Key_Reference_Value(current_selection_, 'COMPANY');
      item_id_      := Client_SYS.Attr_Value_To_Number(Client_SYS.Get_Key_Reference_Value(current_selection_, 'ITEM_ID'));

      OPEN get_net_amount;
      FETCH get_net_amount INTO curr_net_dom_amount_;
      CLOSE get_net_amount;

      OPEN get_total;
      FETCH get_total INTO total_cost_;
      CLOSE get_total;
 
      net_dom_amount_ := net_dom_amount_ + NVL(curr_net_dom_amount_, 0);
      total_amount_   := total_amount_ + NVL(total_cost_, 0);
   END LOOP;

   total_amounts_rec_.total:=total_amount_;
   total_amounts_rec_.invoice_amount:=net_dom_amount_;
   RETURN total_amounts_rec_;
END Get_Total_Cost_And_Dom_Amount___ ; 

-----------------------------------------------------------------------------
--
--  Logical unit: ReserveCustomerOrderLines
--  Component:    ORDER
--
--  IFS Developer Studio Template Version 3.0
--
--  Date    Sign    History
--  ------  ------  ---------------------------------------------------------
-- 2018-10-04   RaVdlk   SCUXXW4-1369, Created
-----------------------------------------------------------------------------

layer Core;

PROCEDURE  Reserve_Order___ (
   selection_ IN CLOB,
   info$_     OUT VARCHAR2) 
IS
   selected_records_     CLOB := CONCAT(selection_, ';');
   current_selection_    VARCHAR2(32000); 
   order_no_             VARCHAR2 (12);
   line_no_              VARCHAR2 (4);
   rel_no_               VARCHAR2 (4);
   line_item_no_         NUMBER;
   contract_             VARCHAR2 (5);
   part_no_              VARCHAR2 (25);
   attr_consol_          VARCHAR2(32000);
   temp_attr_consol_     VARCHAR2(32000);
   counter_              NUMBER:= 0;
   temp_info_            VARCHAR2(4000);

BEGIN 
   WHILE (INSTR(selected_records_, ';') > 0) LOOP

      current_selection_  := substr(selected_records_, 0, INSTR(selected_records_, ';'));
      order_no_           := Client_SYS.Get_Key_Reference_Value(current_selection_, 'ORDER_NO');
      line_no_            := Client_SYS.Get_Key_Reference_Value(current_selection_, 'LINE_NO');
      rel_no_             := Client_SYS.Get_Key_Reference_Value(current_selection_, 'REL_NO');
      line_item_no_       := Client_SYS.Attr_Value_To_Number(Client_SYS.Get_Key_Reference_Value(current_selection_, 'LINE_ITEM_NO')); 
      contract_           := Customer_Order_API.Get_Contract(order_no_);
      part_no_            := Customer_Order_Line_API.Get_Part_No(order_no_,line_no_,rel_no_,line_item_no_);
      
      Customer_Order_Line_Initial_Check___(order_no_, line_no_, rel_no_, line_item_no_);
      
      Client_SYS.Add_To_Attr('ORDER_NO',order_no_, temp_attr_consol_);
      Client_SYS.Add_To_Attr('LINE_NO',line_no_, temp_attr_consol_);
      Client_SYS.Add_To_Attr('REL_NO',rel_no_, temp_attr_consol_);
      Client_SYS.Add_To_Attr('LINE_ITEM_NO',line_item_no_, temp_attr_consol_);
      Client_SYS.Add_To_Attr('CONTRACT',contract_, temp_attr_consol_);
      Client_SYS.Add_To_Attr('PART_NO',part_no_, temp_attr_consol_);
                  
      IF (LENGTH(attr_consol_||temp_attr_consol_ ) <= 32000) THEN 
         attr_consol_      := attr_consol_ || temp_attr_consol_;
         temp_attr_consol_ := NULL;
      ELSE   
         IF counter_ = 0 THEN
            Client_SYS.Add_Warning(lu_name_, 'MANYROWSSELECTED: Due to marking too many rows, more than one Pick List per Order might be created');
            counter_ := counter_ + 1;
            temp_info_ := temp_info_ || Client_SYS.Get_All_Info;
         END IF;
         IF (attr_consol_ IS NOT NULL) THEN
            Customer_Order_Flow_API.Start_Plan_Picking__(attr_consol_);
            attr_consol_      := temp_attr_consol_;
            temp_attr_consol_ := NULL;
         END IF;   
      END IF;       
      selected_records_  := substr(selected_records_, INSTR(selected_records_, ';')+1);       

   END LOOP;
   
   IF (attr_consol_ IS NOT NULL) THEN
      Customer_Order_Flow_API.Start_Plan_Picking__(attr_consol_);
   END IF;   
   info$_ := temp_info_;
END Reserve_Order___ ;

-----------------------------------------------------------------------------
--
--  Logical unit: UpdateSalesPriceLists
--  Component:    ORDER
--
--  IFS Developer Studio Template Version 3.0
--
--  Date       Sign    History
--  ------     ------  ---------------------------------------------------------
-- 2019-06-12  MaEelk  SCUXXW4-21911, Removed fetching % to Price List No, Sales Price Group , Owning Comapany, Catalog no and Base Price Site from Fetch_Defaults___..
-- 2018-08-27  MaEelk  SCUXXW4-8892, Created.
-----------------------------------------------------------------------------

layer Core;

FUNCTION Fetch_Defaults___(sales_price_type_db_ IN VARCHAR2) RETURN Default_Value_Structure_Rec
IS
   default_value_rec_ Default_Value_Structure_Rec;   
BEGIN
   
   IF NVL(sales_price_type_db_, 'SALES PRICES') = 'SALES PRICES' THEN
      default_value_rec_.pricing_base_selection := 'COSTING';
      default_value_rec_.add_to_sales_prices := TRUE;
   ELSIF (sales_price_type_db_ = 'RENTAL PRICES') THEN
      default_value_rec_.pricing_base_selection := 'MANUAL';
      default_value_rec_.add_to_rental_prices := TRUE;
   END IF;   
   default_value_rec_.valid_from := sysdate;
   RETURN default_value_rec_;
END Fetch_Defaults___;

FUNCTION Update_Prices___ (
   valid_from_               IN DATE,
   price_list_no_            IN VARCHAR2,
   sales_price_group_        IN VARCHAR2,
   catalog_no_               IN VARCHAR2,
   base_price_site_          IN VARCHAR2,
   owning_company_           IN VARCHAR2,
   pricing_base_selection_   IN VARCHAR2,
   execution_type_selection_ IN VARCHAR2,
   include_both_dates_       IN BOOLEAN,
   add_to_sales_prices_      IN BOOLEAN,
   add_to_rental_prices_     IN BOOLEAN ) RETURN NUMBER
IS
   number_of_updates_     NUMBER;
   sales_price_origin_db_ VARCHAR2(20);
   include_period_        VARCHAR2(5) := 'FALSE';
   update_sales_prices_   VARCHAR2(5) := 'FALSE';
   update_rental_prices_  VARCHAR2(5) := 'FALSE';
BEGIN
   IF pricing_base_selection_ IN ('MANUAL', 'COSTING') THEN
      sales_price_origin_db_ := pricing_base_selection_;   
   END IF;
   
   IF (add_to_sales_prices_) THEN
      update_sales_prices_ := 'TRUE';   
   END IF;

   IF (add_to_rental_prices_) THEN
      update_rental_prices_ := 'TRUE';   
   END IF;
   
   IF (include_both_dates_) THEN
      include_period_ := 'TRUE';
   END IF;

   IF (execution_type_selection_ = 'ONLINE') THEN
      Customer_Order_Pricing_API.Update_Part_Prices__(number_of_updates_,
                                                      valid_from_,
                                                      sales_price_origin_db_,
                                                      price_list_no_,
                                                      sales_price_group_,
                                                      catalog_no_,
                                                      base_price_site_,
                                                      owning_company_,
                                                      update_sales_prices_,
                                                      update_rental_prices_,
                                                      include_period_);  
   ELSIF (execution_type_selection_ = 'BACKGROUND') THEN
      Customer_Order_Pricing_API.Update_Part_Prices_Batch__(valid_from_,
                                                            sales_price_origin_db_,
                                                            price_list_no_,
                                                            sales_price_group_,
                                                            catalog_no_,
                                                            base_price_site_,
                                                            owning_company_,
                                                            update_sales_prices_,
                                                            update_rental_prices_,
                                                            include_period_);  
   END IF;
   RETURN NVL(number_of_updates_, 0);
END Update_Prices___;
-----------------------------------------------------------------------------
--
--  Logical unit: SalesBackordersAnalysis
--  Component:    ORDER
--
--  IFS Developer Studio Template Version 3.0
--
--  Date    Sign    History
--  ------  ------  ---------------------------------------------------------
--  190627  MaAuse  SCUXXW4-21339, Modified Calculate_Total_Net_Amount___ and Get_Unique_Currency_Code___ to handle CLOB type. Removed Get_Next_From_Attr___.
--  180823  JeLise  SCUXXW4-1296, Created for the convertion of tbwQueryBackorders.
-----------------------------------------------------------------------------

layer Core;

FUNCTION Get_Unique_Currency_Code___(
   full_selection_ IN CLOB ) RETURN VARCHAR2
IS
   selection_         json_array_t := json_array_t.parse(full_selection_);
   current_selection_ VARCHAR2(32000);
   currency_code_     VARCHAR2(3) := NULL;
   current_currency_  VARCHAR2(3) := NULL;
   order_no_          VARCHAR2(12);
   contract_          VARCHAR2(5);
BEGIN
   FOR i_ IN 0 .. selection_.get_size - 1 LOOP 
         current_selection_ := selection_.get_string(i_);
         order_no_          := Client_SYS.Get_Key_Reference_Value(current_selection_, 'ORDER_NO');
         contract_          := Customer_Order_API.Get_Contract(order_no_);
         current_currency_  := Company_Finance_API.Get_Currency_Code(Site_API.Get_Company(contract_));
         IF (NVL(currency_code_, '*') != NVL(current_currency_, '*'))THEN
            IF (currency_code_ IS NULL)THEN
               currency_code_ := current_currency_;
            ELSE
               currency_code_ := NULL;
               EXIT;
            END IF;
         END IF;
   END LOOP;
   RETURN currency_code_;
END Get_Unique_Currency_Code___;


FUNCTION Calculate_Total_Net_Amount___ (
   full_selection_ IN CLOB ) RETURN Amount_Structure_Rec
IS
   selection_         json_array_t := json_array_t.parse(full_selection_);
   current_selection_ VARCHAR2(32000);
   order_no_          VARCHAR2(12);
   line_no_           VARCHAR2(4);
   rel_no_            VARCHAR2(4);
   line_item_no_      NUMBER;
   rec_               Amount_Structure_Rec;
BEGIN
   rec_.total_value := 0;
   FOR i_ IN 0 .. selection_.get_size - 1 LOOP
         current_selection_ := selection_.get_string(i_);
         order_no_          := Client_SYS.Get_Key_Reference_Value(current_selection_, 'ORDER_NO');
         line_no_           := Client_SYS.Get_Key_Reference_Value(current_selection_, 'LINE_NO');
         rel_no_            := Client_SYS.Get_Key_Reference_Value(current_selection_, 'REL_NO');
         line_item_no_      := Client_SYS.Get_Key_Reference_Value(current_selection_, 'LINE_ITEM_NO');
         rec_.total_value   := rec_.total_value + Customer_Order_Line_API.Get_Backord_Value_Base_Curr__(order_no_, line_no_, rel_no_, line_item_no_);
   END LOOP;
   RETURN rec_;
END Calculate_Total_Net_Amount___;
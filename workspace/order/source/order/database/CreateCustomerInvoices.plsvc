-----------------------------------------------------------------------------
--
--  Logical unit: CreateCustomerInvoices
--  Component:    ORDER
--
--  IFS Developer Studio Template Version 3.0
--
--  Date       Sign    History
--  ---------  ------  ---------------------------------------------------------
-- 2019-06-20  KHVESE  SCUXXW4-22651, Changed interface of method Start_Create_Invoice___.
-- 2018-05-17  KHVESE  SCUXXW4-9785, Created,
-----------------------------------------------------------------------------

layer Core;
         
FUNCTION Rental_Trans_Due_Orders_Exist___ (
   selection_ IN VARCHAR2 ) RETURN BOOLEAN
IS
   order_no_               VARCHAR2(12);
   due_rental_trans_exist_ VARCHAR2(5);
   ptr_                    NUMBER;
   name_                   VARCHAR2(50);
   value_                  VARCHAR2(50);
BEGIN
   $IF Component_Rental_SYS.INSTALLED $THEN
      WHILE (Get_Next_From_Attr___(selection_, ptr_, name_, value_)) LOOP
         IF (name_ = 'ORDER_NO' ) THEN
            order_no_ := value_;

            due_rental_trans_exist_ := Rental_Transaction_Manager_API.Due_Trans_Exist(order_no_, NULL, NULL, NULL, 'CUST ORDER');
            EXIT WHEN due_rental_trans_exist_ = 1;
         END IF;
      END LOOP;
      RETURN due_rental_trans_exist_ = 1;
   $ELSE
      RETURN FALSE;
   $END
END Rental_Trans_Due_Orders_Exist___;


FUNCTION Get_Next_From_Attr___ (
   attr_  IN     VARCHAR2,
   ptr_   IN OUT NOCOPY NUMBER,
   name_  IN OUT NOCOPY VARCHAR2,
   value_ IN OUT NOCOPY VARCHAR2 ) RETURN BOOLEAN
IS
   from_  NUMBER;
   to_    NUMBER;
   index_ NUMBER;
   record_index_ NUMBER;
   record_separator_ VARCHAR2(1) := ';';
   field_separator_   VARCHAR2(1) := '^';
   value_separator_  VARCHAR2(1) := '=';
BEGIN
   from_ := nvl(ptr_, 1);
   to_   := instr(attr_, field_separator_, from_);
   record_index_ := instr(attr_, record_separator_, from_);
   IF (to_ > 0) THEN
      index_ := instr(attr_, value_separator_, from_);
      name_  := substr(attr_, from_, index_-from_);
      value_ := substr(attr_, index_+1, to_-index_-1);
      ptr_   := to_+1;
      IF ptr_ = record_index_ THEN 
         ptr_   := ptr_+1;
      END IF;
      RETURN(TRUE);
   ELSE
      RETURN(FALSE);
   END IF;
END Get_Next_From_Attr___;


PROCEDURE Start_Create_Invoice___ (
	order_no_                     IN VARCHAR2,
	create_rental_transactions_   IN VARCHAR2,
	start_event_                  IN NUMBER)
IS
   attr_  VARCHAR2(3500);
BEGIN
   Client_SYS.Add_To_Attr('START_EVENT', start_event_, attr_);
   Client_SYS.Add_To_Attr('ORDER_NO', order_no_, attr_);
   IF create_rental_transactions_ = 'TRUE' THEN 
      Client_SYS.Add_To_Attr('CREATE_RENTAL_TRANS', 'TRUE', attr_);
   END IF;
   Client_SYS.Add_To_Attr('END', '', attr_);
   Customer_Order_Flow_API.Start_Create_Invoice__(attr_);   
END Start_Create_Invoice___;

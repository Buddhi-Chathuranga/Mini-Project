----------------------------------------------------------------------------------------------------
-- Date        Sign    History
-- ----------  ------  -----------------------------------------------------------------------------
-- 210817      KiSalk  SCZ-15946, In the finish command, the value assigned to Result was made translatable, because it is used as the description in final step.
-- 190819      KHVESE  SCUXXW4-23672, Modified enabled condition on finish command.
-- 181204      RAKUSE  SCUXXW4-8585, Converted dialog 'dlgPsheetAggregateTransaction' using MTG Version: 1.34
----------------------------------------------------------------------------------------------------
client RunPeriodicSettlement;
component ORDER;
layer Core;
projection RunPeriodicSettlement;
include fragment ScheduledTasksCommon;
include fragment CustOrdCust2Selector;


----------------------------- NAVIGATOR ENTRIES -----------------------------

navigator {
   entry TaskAssistantNavEntry parent OrderNavigator.PricingRebates at index 700 {
      label = "Run Periodic Settlement";
      assistant TaskAssistant;
   }
}

---------------------------------- GROUPS -----------------------------------

group ParameterGroup for TaskVirtual {
   label = "Parameters";
   lov CompanyRef with ReferenceCompanyFinanceSelector {
      label = "Company";
   }
   lov AgreementRef with ReferenceRebateAgreementSelector {
      freeinput = [true];
      label = "Agreement";
      editable = [CustomerNo = null];
   }
   lov CustomerNoRef with ReferenceCustOrdCust2Selector {
      freeinput = [true];
      label = "Customer";
      editable = [AgreementId = null];
   }

   field ToDate;
}


-------------------------------- ASSISTANTS ---------------------------------

assistant TaskAssistant using TaskVirtuals {
   label = "Run Periodic Settlement";

   init command {
      execute {
         call GetParametersExt(Company) into Params;
         set ScheduleMethodId = Params.ScheduleMethodId;
         set ScheduleName = Params.Description;
         -- Init parameters
         set Company = Params.Company;
         set AgreementId = Params.AgreementId;
         set CustomerNo = Params.CustomerNo;
         set ToDate = Params.ToDate;
      }
   }

   steps {
      step {
         label = "Task Options";
         group ExecuteOptionGroup;
         group ParameterGroup;
         next command {
            enabled = [TaskOption = "Schedule"];
         }
      }
      step {
         label = "Schedule";
         group SchedulingParametersGroup;
         group SchedulingStartAndStopDateGroup;
      }
      step {
         label = "Schedule Options";
         group ScheduledTasksAssistantAdvOpGroup;
         group ScheduledTasksAssistantStreamsGroup;
      }
      final step {
         description = "${Result}";
         command NavigateToScheduledTask {
            visible = [TaskOption = "Schedule"];
         }
      }
      cancelled step {
         description = "Task was cancelled";
      }
   }

   finish command {
-- Keep the other lines "as they are" since these are all static and connected to the two Scheduling Steps.
      enabled = [(Company != null and(AgreementId != null or CustomerNo != null)) and(
      (TaskOption = "Now") or
      (TaskOption = "Schedule" and ScheduleName != null and
      (ScheduleOption = "Daily" and ScheduleTime != null) or
      (ScheduleOption = "Weekly" and ScheduleTime != null and ScheduledDays != null) or
      (ScheduleOption = "Monthly" and ScheduleTime != null and ScheduledDayNumber > 0 and ScheduledDayNumber < 32) or
      (ScheduleOption = "Date" and ScheduleDatetime != null) or
      (ScheduleOption = "Interval" and ScheduleInterval != null) or
      (ScheduleOption = "Custom" and ExecutionPlan != null)))];

      execute {
         if [TaskOption = "Now"] {
            call ExecuteTask();
            set Result = "Task '${ScheduleName}' has been successfully completed!" {
               translatable = true;
            }
         }
         else {
            call ScheduleTask() into ScheduledTask;
            set ScheduleId = ScheduledTask.ScheduleId;
            set Result = "Task '${ScheduleName}', assigned with the Id '${ScheduleId}', was successfully scheduled to start ${ScheduledTask.ScheduledDate} at ${ScheduledTask.ScheduledTime}!" {
               translatable = true;
            }
         }
      }
   }
   cancel command {
      execute {
         navigate back;
      }
   }

}

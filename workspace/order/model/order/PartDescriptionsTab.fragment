--------------------------------------------------------------------------------------
-- Date        Sign    History
-- ----------  ------  ---------------------------------------------------------------
-- 2021-11-03  Skanlk  Bug 161110(SC21R2-3223), Modified SalesPartLanguageDescGroup and SalesPartLanguageDescList by making CatalogDesc and PartCatalogPartDescription fields as multiline field.
-- 2019-05-17  fgusse  SCUXXW4-20685, Added translatable in DocumentTextCommand
-- 2018-11-26  IzShlk  SCUXXW4-9412, Converted from frmSalesPartLanguageDesc using MTG Version: 1.33
--------------------------------------------------------------------------------------

fragment PartDescriptionsTab;
component ORDER;
layer Core;
description "Use this API to enter the description of the sales part in a specific language. If the part may be sold to different customers, a foreign language description may be maintained.
If the language description exists for the language code defined for the customer, it will be selected automatically to be printed on customer order documents.
This implies that the translated description will first be retrieved to e.g. the customer order line, and thereafter used when printing the customer order.
The tab also displays the sales part and the part catalog description, and Use part catalog description as description for sales part check box which indicates if the part catalog description is used or not. It is possible to edit the sales part description";

----------------------------- CLIENT FRAGMENTS ------------------------------
--------------------------------- COMMANDS ----------------------------------
command DocumentTextCommand for SalesPartLanguageDesc {
   label = "Document Text";
   enabled = [NoteId != null];

   variable LabelTextVar;
   execute {
      set LabelTextVar = " for Sales Part Part - ${CatalogNo} ${LanguageCode}" {
         translatable = true;
      }
      assistant DocumentText(NoteId, LabelTextVar) {
         when NO {
            exit CANCEL;
         }
      }
   }
}


---------------------------------- GROUPS -----------------------------------
group SalesPartLanguageDescGroup for SalesPart {
   label = "";

   field CatalogDesc {
      multiline = true;
      size = Medium;
   }
   field PartCatalogPartDescription {
      multiline = true;
      size = Medium;
   }
   field SiteDiscomInfoRef.UsePartcaDescOrder {
      label = "Use Part Catalog Description as Description for Sales Part";
   }
}

----------------------------------- LISTS -----------------------------------
list SalesPartLanguageDescList for SalesPartLanguageDesc {
   label = "";
   field LanguageCodeRef {
      searchable = false;
   }
   field CatalogDesc {
      multiline = true;
      size = Medium;
   }
   field DocumentText;

   command DocumentTextCommand;

}

--------------------------- PROJECTION FRAGMENTS ----------------------------

@Override
entity SalesPartLanguageDesc {
   ludependencies = DocumentText;
   crud = Create, Read, Update, Delete;
   from = "sales_part_language_desc";
   exec = "SALES_PART_LANGUAGE_DESC_API";
   keys = Contract, CatalogNo, LanguageCode {
      where = "CONTRACT = :Contract and CATALOG_NO = :CatalogNo and LANGUAGE_CODE = :LanguageCode";
   }
   use attributes = CatalogDesc, NoteId;

   attribute LanguageCode Text {
      fetch = "language_code";
      label = "Language Code";
      maxlength = 2;
      required = [true];
      editable = [ETag = null];
   }
   attribute CatalogDesc Text {
      fetch = "catalog_desc";
      label = "Description";
      maxlength = 200;
      required = [true];
   }
   attribute DocumentText Boolean {
      fetch = "DECODE(Document_Text_API.Note_Id_Exist(note_id), '1', 'TRUE', 'FALSE')";
      label = "Document Text";
      editable = [false];
   }

   reference LanguageCodeRef(LanguageCode) to ApplicationLanguage(LanguageCode) {
      label = "Language Code";
   }
}
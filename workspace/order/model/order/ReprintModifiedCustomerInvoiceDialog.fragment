-----------------------------------------------------------------------------------------------------
-- Date        Sign     History
-- ----------  ------   -----------------------------------------------------------------------------
-- 2018-11-02  KHVESE   SCUXXW4-8720, Implemented dlgReprintDialog.
-----------------------------------------------------------------------------------------------------

fragment ReprintModifiedCustomerInvoiceDialog;
component ORDER;
layer Core;
description "Use this dialog box to reprint the modified customer invoice if invoice has already been printed or sent via EDI or MHS.
The original or/and copy(s) will be printed, depending on the option user selected in the dialog box. If the Duplicate check box is selected,
the word Duplicate will be printed on the copy or original.";

----------------------------- CLIENT FRAGMENTS ------------------------------

dialog ReprintModifiedInvoiceDialog for ReprintModifiedInvoiceStructure {
   label = "Reprint Invoice";
   input(MaxCopyNo) {
      command InitCommand;
   }
   output(ReprintOption, Duplicate, CopyNo);
   group {
      label = " ";
      radiogroup ReprintOption {
         validate command {
            execute {
               if [ReprintOption = "InvoiceOriginal" or MaxCopyNo <= 0] {
                  set CopyNo = 0;
               }
               else {
                  if [ReprintOption = "InvoiceCopy"] {
                     set CopyNo = 1;
                  }
                  else {
                     if [ReprintOption = "All"] {
                        set CopyNo = null;
                     }
                  }
               }
            }
         }
      }
   }
   group {
      label = "";
      visible = [ReprintOption = "InvoiceCopy"];
      field CopyNo {
         editable = [ReprintOption = "InvoiceCopy"];
         validate command {
            execute {
               if [CopyNo < 0] {
                  error("Copy No cannot be less than zero");
               }
               if [CopyNo > MaxCopyNo] {
                  alert("Copy No (${CopyNo}) cannot exceed No of Invoice Copies (${MaxCopyNo}) defined for Customer.");
               }
            }
         }
      }
   }
   group {
      label = "";
      field Duplicate {
         size = Small;
      }
   }
   command Ok {
      execute {
         if [CopyNo != null and CopyNo < 0] {
            alert("Copy No cannot be less than zero.");
         }
         else {
            if [CopyNo > MaxCopyNo] {
               alert("Copy No (${CopyNo}) cannot exceed No of Invoice Copies (${MaxCopyNo}) defined for Customer.");
            }
            else {
               exit OK;
            }
         }
      }
   }
   command Cancel {
      execute {
         exit CANCEL;
      }
   }
}

command InitCommand for ReprintModifiedInvoiceStructure {
   label = "Init Command";
   execute {
      set CopyNo = 0;
   }
}

--------------------------- PROJECTION FRAGMENTS ----------------------------

structure ReprintModifiedInvoiceStructure {
   attribute ReprintOption Enumeration(PrintOptionEnum);
   attribute CopyNo Number;
   attribute MaxCopyNo Number;
   attribute Duplicate Boolean("TRUE", "FALSE") {
      required = [true];
   }
}

enumeration PrintOptionEnum {
   enumerationtype = Custom;
   value = "Original" {
      identifier = "InvoiceOriginal";
      label = "Invoice Original";
   }
   value = "All" {
      identifier = "All";
      label = "All, Original and Copies";
   }
   value = "Copy" {
      identifier = "InvoiceCopy";
      label = "Invoice Copy";
   }
}



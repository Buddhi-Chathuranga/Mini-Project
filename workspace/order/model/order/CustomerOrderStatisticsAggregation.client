----------------------------------------------------------------------------------------------------
-- Date        Sign    History
-- ----------  ------  -----------------------------------------------------------------------------
-- 210817      KiSalk  SCZ-15946, In the finish command, the value assigned to Result was made translatable, because it is used as the description in final step.
-- 181115      RAKUSE  SCUXXW4-8660, Converted dialog 'dlgPSheetOrderStatAggregation' using MTG Version: 1.34
----------------------------------------------------------------------------------------------------
client CustomerOrderStatisticsAggregation;
component ORDER;
layer Core;
projection CustomerOrderStatisticsAggregationHandling;
include fragment ScheduledTasksCommon;

----------------------------- NAVIGATOR ENTRIES -----------------------------

navigator {
   entry StatisticsCustomerOrderStatisticsAggregationNavEntry parent OrderNavigator.OrderStatistics at index 900 {
      label = "Customer Order Statistics Aggregation";
      assistant TaskAssistant;
   }
}

---------------------------------- GROUPS -----------------------------------

group ParameterGroup for TaskVirtual {
   label = "Parameters";
   lov CompanyRef with CompanyFinanceSelector using CompanyFilter {
      label = "Company";
      size = Medium;
      description = CompanyRef.Description;
   }
   field AggIdList {
      label = "Aggregation";
      size = Medium;
      lovswitch {
         when [Company = null] {
            lov AggregateIdRef with OrdAggStatSelector using AggregationNullFilter {
               description = AggregateIdRef.Description;
            }
         }
         when [Company != null] {
            lov AggregateIdRef with OrdAggStatSelector using AggregationFilter(Company) {
               description = AggregateIdRef.Description;
            }
         }
      }
   }
}

-------------------------------- ASSISTANTS ---------------------------------

assistant TaskAssistant using TaskVirtuals {
   label = "Customer Order Statistics Aggregation";

   init command {
      execute {
         call GetParameters() into Params;
         set ScheduleMethodId = Params.ScheduleMethodId;
         set ScheduleName = Params.Description;
         set Company = Params.Company;
         set AggIdList = Params.AggIdList;
      }
   }

   steps {
      step {
         label = "Task Options";
         group ExecuteOptionGroup;
         group ParameterGroup;
         next command {
            enabled = [TaskOption = "Schedule"];
         }
      }
      step {
         label = "Schedule";
         group SchedulingParametersGroup;
         group SchedulingStartAndStopDateGroup;
      }
      step {
         label = "Schedule Options";
         group ScheduledTasksAssistantAdvOpGroup;
         group ScheduledTasksAssistantStreamsGroup;
      }
      final step {
         description = "${Result}";
         command NavigateToScheduledTask {
            visible = [TaskOption = "Schedule"];
         }
      }
      cancelled step {
         description = "Task was cancelled";
      }
   }

   finish command {
      enabled = [(Company != null and AggIdList != null) and(
      (TaskOption = "Now") or
      (TaskOption = "Schedule" and ScheduleName != null and
      (ScheduleOption = "Daily" and ScheduleTime != null) or
      (ScheduleOption = "Weekly" and ScheduleTime != null and ScheduledDays != null) or
      (ScheduleOption = "Monthly" and ScheduleTime != null and ScheduledDayNumber > 0 and ScheduledDayNumber < 32) or
      (ScheduleOption = "Date" and ScheduleDatetime != null) or
      (ScheduleOption = "Interval" and ScheduleInterval != null) or
      (ScheduleOption = "Custom" and ExecutionPlan != null)))];

      execute {
         if [TaskOption = "Now"] {
            call ExecuteTask();
            set Result = "Task '${ScheduleName}' has been successfully completed!" {
               translatable = true;
            }
         }
         else {
            call ScheduleTask() into ScheduledTask;
            set ScheduleId = ScheduledTask.ScheduleId;
            set Result = "Task '${ScheduleName}', assigned with the Id '${ScheduleId}', was successfully scheduled to start ${ScheduledTask.ScheduledDate} at ${ScheduledTask.ScheduledTime}!" {
               translatable = true;
            }
         }
      }
   }
   cancel command {
      execute {
         navigate back;
      }
   }
}

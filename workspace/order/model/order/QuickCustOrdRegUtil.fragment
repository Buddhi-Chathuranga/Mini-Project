-------------------------------------------------------------------------------------------------------
--  Date        Sign    History
--  ----------  ------  --------------------------------------------------------------------------------
--  2022-01-31  NiDalk  SC21R2-7455, Fixed some model issues in QuickCustOrdStruct.
--  2021-12-08  ChBnlk  SC21R2-1083, Added API documentation.
--  2021-10-22  ChBnlk  SC21R2-1083, Created.
--  ----------  ------  --------------------------------------------------------------------------------
fragment QuickCustOrdRegUtil;
component ORDER;
layer Core;
description "This fragment is used to define the structure used in Quick Customer Order Registration. ";

--------------------------------- FRAGMENTS ---------------------------------


----------------------------- CLIENT FRAGMENTS ------------------------------


--------------------------- PROJECTION FRAGMENTS ----------------------------

-------------------------------- STRUCTURES ---------------------------------

@apidoc {
   @description : Used this structure to hold the data related to Quick Customer Order Registration.
}
structure QuickCustOrdStruct using CustomerOrder {
   nestedwithcrud {
      crud = Create;
      excludefromschema {
         exclude OrderNo, CustRef for CustomerOrder;
         exclude OrderNo, LineNo, RelNo, LineItemNo, Contract, WantedDeliveryDate, ShipAddrNo for CustomerOrderLine;
      }

   }
   implementation = "Utility";

}

@Override
@apidoc {
   @description : Contains customer order header details.
   @attribute Customer : The short name or identification number of the customer.
   @attribute Site : The short code of the ordering site.
   @attribute OrderType : <<The order type controls the order flow for both manually-entered and generated customer orders
   by defining which steps will be automatically performed and which steps will be manually performed. When specified, the priority will be given to the value on the request payload. If no value is defined for the site or customer in IFS Cloud, then this field requires a valid order type.>>
   @attribute Coordinator : The person responsible for coordinating the handling of customer orders.
   @attribute Currency : The short code for the currency used.
   @attribute DeliveryAddress : The address to which the goods will be delivered.
   @attribute WantedDeliveryDate :  The date on which the customer wants the order to be delivered at the customer's delivery address.
   @attribute CustRef : The customer's reference.
   @navigation OrderLines : Include order lines that connects to a specific customer order record.
}
entity CustomerOrder {
   crud = None;
   use attributes = CustomerNo, Contract, OrderId, AuthorizeCode, CurrencyCode, ShipAddrNo, WantedDeliveryDate, CustRef;

   attribute CustomerNo Text {
      alias = "Customer";
   }
   attribute Contract Text {
      alias = "Site";
   }
   attribute OrderId Text {
      alias = "OrderType";
      required = [false];
   }
   attribute AuthorizeCode Text {
      alias = "Coordinator";
      required = [false];
   }
   attribute CurrencyCode Text {
      alias = "Currency";
      required = [false];
   }
   attribute ShipAddrNo Text {
      label = "DeliveryAddress";
      required = [false];
   }
   attribute WantedDeliveryDate Timestamp {
      required = [false];
   }
   array OrderLines(OrderNo) to CustomerOrderLine(OrderNo);
}


@Override

@apidoc {
   @description : Contains customer order line details.
   @attribute Site :  The short code of the ordering site.
   @attribute SalesPartNo : The identity number of the sales part the customer desires to purchase.
   @attribute SalesPartDescription : The name or description corresponding to the sales part number.
   @attribute SalesQty : The ordered quantity, expressed in sales unit of measure.
   @attribute SupplyCode :  Indicates how a part should be supplied i.e. Invent Order, Int Purch Trans, Int Purch Dir etc.
   @attribute Supplier : The identification number of the internal or external supplier.
   @attribute ConditionCode : Indicates the condition code for the part.
   @attribute BlockedForInvoicing : Indicates if the order line is blocked from being invoiced.
   @attribute WantedDeliveryDate : The date on which the customer wants the order to be delivered at the customer's delivery address.
   @attribute DeliveryAddress : The identity of the customer's delivery address to which the goods will be sent.
}
entity CustomerOrderLine {
   crud = None;
   use attributes = Contract, CatalogNo, CatalogDesc, BuyQtyDue, SupplyCode, ConditionCode, BlockedForInvoicing,
   WantedDeliveryDate, ShipAddrNo;

   attribute Contract Text {
      alias = "Site";
      required = [false];
   }
   attribute CatalogNo Text {
      alias = "SalesPartNo";
   }
   attribute CatalogDesc Text {
      alias = "SalesPartDescription";
   }
   attribute BuyQtyDue Number {
      alias = "SalesQty";
   }
   attribute ShipAddrNo Text {
      alias = "DeliveryAddress";
      required = [false];
   }
   attribute SupplyCode Enumeration(OrderSupplyType.Subset6) {
      required = [false];

   }
   attribute VendorNo Text {
      alias = "Supplier";
      required = [false];
   }
   attribute WantedDeliveryDate Timestamp {
      required = [false];
   }
   attribute BlockedForInvoicing Boolean("TRUE", "FALSE") {
      required = [false];
   }
}



--------------------------------------------------------------------------------------
-- Date        Sign    History
-- ----------  ------  ---------------------------------------------------------------
-- 2020-10-14  RaVdlk  SC2020R1-10556, Removed Notes command and added it as a column
-- 2018-07-26  IzShlk  SCUXXW4-1026, Converted from tbwOverviewAgrSalesGroupDeal using MTG Version: 1.24
--------------------------------------------------------------------------------------

client AgreementSalesGroupDeals;
component ORDER;
layer Core;
projection AgreementSalesGroupDealsHandling;

--------------------------------- FRAGMENTS ---------------------------------
include fragment CustomerTemplateLovSelector;

----------------------------- NAVIGATOR ENTRIES -----------------------------
navigator {
   entry AgreementSalesGroupDealNavEntry parent OrderNavigator.PricingCustomerAgreement at index 400 {
      label = "Agreement Sales Group Deals";
      page List;
   }
}

-------------------------------- MAIN PAGES ---------------------------------
page List using AgreementSalesGroupDealSet {
   label = "Agreement Sales Group Deals";
   startupmode = search;
   list AgreementSalesGroupDealList;
}

-------------------------------- ASSISTANTS ---------------------------------

--------------------------------- COMMANDS ----------------------------------
command CustomerAgreementCommand for AgreementSalesGroupDeal {
   label = "Customer Agreement";
   enabled = [true];
   mode = SelectedRecords;

   bulkexecute {
      bulknavigate "page/CustomerAgreement/Form?$filter=AgreementId eq $[AgreementId]";
   }
}
command DealperSalesGroupDiscountCommand for AgreementSalesGroupDeal {
   label = "Deal per Sales Group Discount";
   enabled = [true];
   mode = SingleRecord;
   variable UpdAllowedVar {
      type = Boolean;
   }
   execute {
      if [State != "Closed"] {
         set UpdAllowedVar = true;
      }
      else {
         set UpdAllowedVar = false;
      }
      navigate "assistant/CustomerAgreementDealPerSalesGroupDiscount/AgreementDealGroupDiscountAssistant?$action=SetupSourceInfo{AgreementIdVar:$[AgreementId], MinQuantityVar:$[MinQuantity], ValidFromVar:$[ValidFromDate], CatalogGroupVar:$[CatalogGroup], ValidToVar:$[ValidToDate], UpdAllowedVar:$[UpdAllowedVar]}";
   }
}

----------------------------------- LISTS -----------------------------------

list AgreementSalesGroupDealList for AgreementSalesGroupDeal {
   lov CustomerAgreementRef with ReferenceCustomerAgreementSelector {
      label = "Agreement";
      description = CustomerAgreementRef.Description;
   }
   lov CustomerNoRef with ReferenceCustomerTemplateLovSelector {
      label = "Customer";
      contactwidget {
         enabled = [CustomerNo != null];
         source = Customer;
      }
      description = CustomerName;
   }
   field CurrencyCodeRef {
      size = Small;
   }
   field State {
      size = Small;
      label = "Customer Agreement Status";
   }
   lov CatalogGroupRef with ReferenceSalesGroupSelector {
      label = "Sales Group";
      description = CatalogGroupRef.Description;
   }
   field MinQuantity;
   daterangefield {
      startdate ValidFromDate;
      enddate ValidToDate;
      label = "Validity Period";
   }
   lov DiscountTypeRef with ReferenceSalesDiscountTypeSelector using DiscountType(DiscountType) {
      size = Small;
      validate command {
         execute {
            set Discount = DiscountTypeRef.Discount;
         }
      }
   }
   field Discount;
   field NoteText {
      multiline = true;
   }

   commandgroup CommandGroupCmdGroup {

      command CustomerAgreementCommand;
      command DealperSalesGroupDiscountCommand;
   }
}

@Override
list SalesDiscountTypeLovList for SalesDiscountType {
   label = "";
   static Objstate;
}


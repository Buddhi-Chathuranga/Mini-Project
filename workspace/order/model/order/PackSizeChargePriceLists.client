--------------------------------------------------------------------------------------
-- Date        Sign    History
-- ----------  ------  ---------------------------------------------------------------
-- 2019-05-27  MaEelk  SCUXXW4-21419, Status Badge was added, Date From and Date To were grouped into a Date Range Field. Currency field was set to be small
-- 2018-05-09  IzShlk  SCUXXW4-1136, Converted from tbwOverviewPackSizeChgList using MTG Version: 1.16
--------------------------------------------------------------------------------------

client PackSizeChargePriceLists;
component ORDER;
layer Core;
projection PackSizeChargePriceListsHandling;

----------------------------- NAVIGATOR ENTRIES -----------------------------
navigator {
   entry PackSizeChgListNavEntry parent OrderNavigator.PricingPackSizeChargeDiscount at index 200 {
      label = "Pack Size Charge Price Lists";
      page List home PackSizeChargeList;
   }
}

-------------------------------- MAIN PAGES ---------------------------------
page List using PackSizeChargeLists {
   label = "Pack Size Charge Price Lists";
   startupmode = search;
   list PackSizeChgList {
      details = "page/PackSizeChargePriceList/Form?$filter=ChargeListNo eq $[ChargeListNo]";
   }
}

--------------------------------- COMMANDS ----------------------------------
@Override
command Activate for PackSizeChargeList {
   label = "Set Active";
   enabled = [Objstate != "Active"];
   mode = SelectedRecords;
}

@Override
command Close for PackSizeChargeList {
   label = "Close";
   enabled = [Objstate != "Closed"];
   mode = SelectedRecords;

   bulkexecute {
      call PackSizeChargeUsage(Selection) into UsageTypeInfo;

      if [UsageTypeInfo.OrderFound] {
         confirm("Pack Size Charge Price List is connected to customer orders in Planned, Credit Blocked, Released, Reserved, Picked, Partially Delivered states. Do you want to proceed?") {
            when CANCEL {
               exit;
            }
         }
      }

      if [UsageTypeInfo.QuotationFound and not UsageTypeInfo.OrderFound] {
         confirm("Pack Size Charge Price List is connected to sales quotations in Released, Revised states. Do you want to proceed?") {
            when CANCEL {
               exit;
            }
         }
      }

      bulkcall Close();
      refresh;
   }
}

----------------------------- VISUAL COMPONENTS -----------------------------

----------------------------------- LISTS -----------------------------------
list PackSizeChgList for PackSizeChargeList {
   field ChargeListNo;
   field Description;
   field Currency {
      size = Small;
   }
   lov SalesChargeTypeRef with ReferenceSalesChargeTypeSelector {
      label = "Charge Type";
      description = SalesChargeTypeRef.ChargeTypeDesc;
   }
   lov ContractRef with ReferenceUserAllowedSiteLovSelector;
   badge  Objstate {
      label = "Status";
      emphasis StatePlanned = [Objstate = "Planned"];
      emphasis StateActive = [Objstate = "Active"];
      emphasis StateClosed = [Objstate = "Closed"];
      style = TextOnly;
   }
   field SalesChargeTypeRef.UnitCharge {
      label = "Unit Charge";
      searchable = false;
   }
   field UsePriceInclTaxDb {
      searchable = false;
   }
   daterangefield {
      startdate ValidFrom;
      enddate ValidTo;
      label = "Valid Period";
   }
   field CreatedDate;
   field Company {
      columnexclude = [true];
   }

   commandgroup StateCmdGroup {
      label = "State";
      command Activate;
      command Close;
   }
}

@Override
list SalesChargeTypeLovList for SalesChargeType {
   label = "";
   static ChargeType;
   static ChargeTypeDesc;
   static UnitCharge;
   static Contract;
}

--------------------------------- SELECTORS ---------------------------------
@Override
selector ReferenceSalesChargeTypeSelector for SalesChargeType {
   label = "";
   static ChargeType;
   static ChargeTypeDesc;
   static UnitCharge {
      visible = [false];
   }
}





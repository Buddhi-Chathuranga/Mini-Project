client ScheduledReports;
component FNDBAS;
layer Core;

----------------------------- NAVIGATOR ENTRIES -----------------------------

navigator {
   entry OperationalReports parent SqlQuickReports.ReportingTopNode at index 3 {
      label = "Scheduled Reports";
      page List;
   }
}

-------------------------------- MAIN PAGES ---------------------------------

page List using ScheduledReportsSet {
   label = "Scheduled Reports";
   list ScheduledReportList;
}

----------------------------------- LISTS -----------------------------------

list ScheduledReportList for BatchScheduleReport {
   label = "Scheduled Reports";
   orderby = ScheduleId desc;
   field ScheduleId;
   field ScheduleName;

   field Active;
   field ReportTitle;
   field ExecutionPlan;
   field NextExecutionDate;
   field ModifiedDate;
   field Username;
   field StartDate;
   field StopDate;
   lov LangCodeRef with LanguageSelector {
      description = LangCodeRef.Description;
   }
   field Executions;

   command ShowBackgroundJobsCommand;
   command EditScheduledReportCommand;
   command DeleteScheduledReportCommand;
}

--------------------------------- SELECTORS----------------------------------

selector LanguageSelector for LanguageCode {
   field LangCode;
   field Description;
}

----------------------------------- LISTS -----------------------------------

@Overtake Core
list LanguageCodeLovList for LanguageCode {
   label = "";
   static LangCode;
   static Description;
   static LangCodeRfc3066 {
      label = "Lang Code Rfc3066";
   }
}

--------------------------------- COMMANDS ----------------------------------

command ShowBackgroundJobsCommand {
   label = "Show Background Jobs";
   enabled = [true];
   mode = SingleRecord;
   execute {
      navigate "page/BackgroundJobsHandling/BackgroundJobsOverview;$filter=ScheduleId eq $[ScheduleId]";
   }
}

command EditScheduledReportCommand {
   label = "Edit";
   // Custom pages lack generic Edit support -> Disable them!
   enabled = [CustomPage = null];
   mode = SingleRecord;
   execute {
      navigate "assistant/${ClientName}/ScheduleReport?$action=InitWithArguments{ScheduleIdPar:$[ScheduleId]}";
   }
}

command DeleteScheduledReportCommand for BatchScheduleReport {
   label = "Delete";
   enabled = [true];
   mode = SelectedRecords;
   bulkexecute {
      confirm("Selected Scheduled Report(s) will be deleted.") {
         when CANCEL {
            exit;
         }
      }
      bulkcall DeleteScheduledReports() {
         when SUCCESS {
            hideselection();
         }
      }
   }
}

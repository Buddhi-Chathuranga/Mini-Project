client BatchQueueConfigurationHandling;
component FNDBAS;
layer Core;
warningsenabled = QueueMethod;

-------------------------------- MAIN PAGES ---------------------------------
navigator {
   @DynamicComponentDependency FNDADM
   entry BatchQueueConfigurations parent FndadmNavigator.BackgroundJobsFolder at index 110 {
      label = "Batch Queue Configurations";
      page BatchQueueConfigurations;
   }
}

page BatchQueueConfigurations using BatchQueues {
   label = "Batch Queue Configurations";
   editmode = MultiField;
   selector BatchQueueConfSelector;
   group BatchQueueMainGroup;
   group ClusterSetupGroup;
   list BatchQueueMethodList(BatchQueueMethodsArray) bind BatchQueueConfSelector;

   command InitBatchQueue;
   command InitAllBatchQueues;
}
--------------------------------- COMMANDS ----------------------------------
command InitBatchQueue for BatchQueue {
   label = "Initialize Batch Queue";
   execute {
      call ExecuteInitBatchQueue();
      success("The Batch queue has been initialized successfully");
   }
}
command InitAllBatchQueues for BatchQueue {
   label = "Initialize All Batch Queues";
   execute {
      call ExecuteInitAllBatchQueues();
      success("All Batch queues have been initialized successfully");
   }
}
----------------------------- VISUAL COMPONENTS -----------------------------
selector BatchQueueConfSelector for BatchQueue {
   label = " ${Description}";
   field Description;
   field QueueId;
}

selector LanguageSelector for LanguageCode {
   orderby = Description;
   field LangCode;
   field Description;
}
@Override
selector OracleClusterNodeSelector for OracleClusterNode {
   field InstId;
}

selector MethodNameRefSelector for SchedulableMethod {
   field MethodName;
   field Module;
}

group BatchQueueMainGroup for BatchQueue {

   label = "General";

   field QueueId;
   field Description
      {
      label = "Name";
   }
   field ProcessNumber
      {
      label = "Processes";
   }
   field Active;
   field ExecutionPlan;
   lov LangCodeRef with LanguageSelector {
      label = "Language Code";
      hidekey = [true];
      description = LangCodeRef.Description;
   }
}

group ClusterSetupGroup for BatchQueue {

   label = "Cluster Setup";

   -- field NodeAttached;

   lov OracleClusterNodeRef with OracleClusterNodeSelector {
      label = "Oracle Cluster Node";
      hidekey = [true];
      description = OracleClusterNodeRef.InstId;
   }
}

list BatchQueueMethodList for QueueMethod {
   label = "Methods in the Queue";
   preselect = [false];

   lov MethodNameRef with MethodNameRefSelector
      {
      label = "Method";
      size = Large;
      advancedview MethodNameList;
   }
}

list MethodNameList for SchedulableMethod {
   preselect = [false];
   field MethodName {
      size = Large;
   }
   field PackageName {
      columnvisible = [false];
      size = Large;
   }
   field Procedures {
      columnvisible = [false];
   }
   field LuPrompt {
      columnvisible = [false];
   }
   field Module;
   field ModuleName {
      size = Large;
      columnvisible = [false];
   }
}
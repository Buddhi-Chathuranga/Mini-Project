projection BatchQueueConfigurationHandling;
component FNDBAS;
layer Core;
description "Handling Batch Queue Configurations";
category Users;

----------------------------- MAIN ENTRY POINTS -----------------------------
entityset BatchQueues for BatchQueue;
entityset SchedulableMethods for SchedulableMethod;

action ExecuteInitAllBatchQueues {
   initialcheck none;
}
------------------------------ ENTITY DETAILS -------------------------------
@Override
entity BatchQueue {
   crud = Create, Read, Update, Delete;
   array BatchQueueMethodsArray(QueueId) to QueueMethod(QueueId);
   reference LangCodeRef(LangCode) to LanguageCode(LangCode);
   reference OracleClusterNodeRef(Node) to OracleClusterNode(InstId);

   action ExecuteInitBatchQueue;
   attribute Active Boolean("TRUE", "FALSE") {
      required = [true];
   }

}

@Override
entity QueueMethod using BatchQueueMethod {
   supportwarnings = [true];
   crud = Create, Read, Update, Delete;
   reference MethodNameRef(MethodName) to SchedulableMethod(MethodName);
}

query OracleClusterNode {
   from = "ORACLE_CLUSTER_NODE";
   keys = InstId;
   attribute InstId Number;
}

query SchedulableMethod {
   from = "Batch_Schedulable_Method_Lov";
   keys = MethodName;

   attribute MethodName Text {
      fetch = "Package_Name || '.' || UPPER(Method_Name)";
      label = "Method Name";
   }
   attribute Module Text {
      label = "Component";
   }
   attribute ModuleName Text {
      label = "Component Name";
   }
   attribute LuPrompt Text {
      label = "Entity";
   }
   attribute PackageName Text {
      label = "Package";
   }
   attribute Procedures Text {
      label = "Procedure";
      fetch = "Method_Name";
   }
   attribute Description Text {
      fetch = "replace(substr(Method_Name, instr(Method_Name, '.')+1), '_', ' ')";
   }
}

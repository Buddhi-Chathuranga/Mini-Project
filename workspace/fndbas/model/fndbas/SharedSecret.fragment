fragment SharedSecret;
component FNDBAS;
layer Core;
description "Functionality to generate and get user secrets";

--------------------------------- FRAGMENTS ---------------------------------

----------------------------- CLIENT FRAGMENTS ------------------------------

list SharedSecretList for SharedSecret {
   field Secret {
      label = "Secret";
      size = FullWidth;
   }
   command GenerateNewRandomSecret;
   command InsertSecretManually;
}

command GenerateNewRandomSecret for SharedSecret {
   label = "Generate new random secret";
   variable Response Boolean;

   execute {

      confirm("Generating a new secret will erase the existing one. Do you want to proceed?") {
         when OK {
            call SetSecret(UserId, "") into Response;
            if [Response] {
               refresh;
            }
            else {
               exit CANCEL;
            }
         }
         when CANCEL {
            exit CANCEL;
         }

      }

   }

}
command InsertSecretManually for SharedSecret {
   label = "Insert secret manually";
   variable VarSS Structure(SharedSecret);
   execute {

      copy this into VarSS;
      assistant SharedSecretAssistant(VarSS) {
         when OK {
            refresh;
         }
      }
   }

}

assistant SharedSecretAssistant for SharedSecret {
   label = "Set shared secret";

   singlestep {
      label = "Set shared secret manually";
      group {
         label = "Insert a new shared secret";
         field ManualSecret {
            size = FullWidth;
         }
      }

   }

   command Ok {
      variable Response Boolean;
      execute {
         call SetSecret(UserId, ManualSecret) into Response;
         if [Response] {
            exit OK;
         }
         else {
            exit CANCEL;
         }
      }

   }
   command Cancel {
      execute {
         exit CANCEL;
      }

   }


}


--------------------------- PROJECTION FRAGMENTS ----------------------------

structure SharedSecret {
   attribute UserId Text;
   attribute Secret Text;
}

function GetSecret List < Structure(SharedSecret) > {
   implementation = "Java";
   parameter Id Text;
}

action SetSecret Boolean {
   initialcheck none;
   implementation = "Java";
   parameter Id Text;
   parameter Secret Text;
   ludependencies = FndUser;
}


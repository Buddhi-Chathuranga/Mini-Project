client UserSettings;
component FNDBAS;
layer Core;

@DynamicComponentDependency FNDADM
include fragment UserPasswordExpireHandling;

-------------------------------- MAIN PAGES ---------------------------------

page SettingsPage {
   label = "Settings";
   editmode = MultiField;
   //singleton UserSingleton using SingletonUser;

   /*
   arrange {
      group PersonPictureGroup;
      group PersonDetailsGroup;
   }*/

   group PersonDetailsGroup {
      visible = [PersonId != null];
   }
   group UserDetailsGroup;
   arrange {
      group LanguageGroup;
      group CurrencyFormattingGroup;
      group DatePickerOptionsGroup;
   }

   command MorePersonInfo {
      enabled = [PersonId != null];
   }

   command ExpirePasswordCommand bind  UserSingleton {
      label = "Expire password to Reset";
   }

}

singleton UserSingleton for UserProfile;

selector LanguageSelector for LanguageCode {
   label = "Language Selector";
   field Description;
}

group PersonPictureGroup for UserProfile {
   imagefield {
      imagedata MapUrl;
      editable = [false];
   }
}

group PersonDetailsGroup for UserProfile {
   label = "Personal Information";

   @DynamicComponentDependency ENTERP
   imagefield using CurrentPerson {
      imagedata PersonImage;
      editable = [ImageEditable = true];
      size = Medium;
   }

   field PersonId {
      editable = [false];
   }

   field Name {
      editable = [false];
   }

   field  Mobile {
      editable = [false];
   }

   field Tel {
      editable = [false];
   }

   field Email {
      editable = [false];
   }
}

group UserDetailsGroup for UserProfile {
   label = "User Information";
   collapsed = [PersonId != null];

   field UserId {
      editable = [false];
   }

   field Name {
      editable = [false];
      visible = [PersonId = null];
   }

   field SmtpEmail {
      label = "Email";
      editable = [false];
   }

   field MobilePhone {
      label = "Mobile";
      editable = [false];
   }

}

group LanguageGroup for UserProfile {
   label = "Language";

   lov Languageref with  LanguageSelector using EnumerateLanguages(Id) {
      label = "Language";
      description = Languageref.Description;
      hidekey = [true];
      editable = [true];
   }
}

group CurrencyFormattingGroup for UserProfile {
   label = "Currency Formatting Options";
   field MinDecimalDigits {
      label = "Decimal Places";
      required = [true];
      maxlength = 2;
      validate command {
         execute {
            if[MinDecimalDigits <= 0] {
               set MinDecimalDigits = 0;
            }
            if[MinDecimalDigits > 20] {
               set MinDecimalDigits = 20;
               alert("Decimal Places cannot be greater than 20 ");
            }
            set MaxDecimalDigits = MinDecimalDigits;
         }
      }
   }

   field UseGrouping {
      required = [true];
   }
}

group DatePickerOptionsGroup for UserProfile {
   label = "Date options";
   field ShowWeekNumbers {
      required = [true];
   }
}


--------------------------------- COMMANDS ----------------------------------
command MorePersonInfo {
   label = "More Personal Information";
   execute {
      navigate "page/EmployeeSelfEdit/EditPersonalInfo";
   }
}

command ExpirePasswordCommand {
   label = "Expire Password";
   execute {
      call GetDirectoryId(UserId) into DirectoryId;
      @DynamicComponentDependency FNDADM
      call ExpireUserPassword(DirectoryId);
      success("User password expired. Please login again to reset.");
   }
}
----------------------------- VISUAL COMPONENTS -----------------------------

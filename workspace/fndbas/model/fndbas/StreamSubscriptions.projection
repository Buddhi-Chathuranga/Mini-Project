projection StreamSubscriptions;
component FNDBAS;
layer Core;
description "Projection contains Stream notification Subscriptions and related functionality.";
category Users, ExternalB2B;

----------------------------- MAIN ENTRY POINTS -----------------------------
entityset FndStreamMessages for FndStream;
entityset Subscriptions for FndObjSubscription;
entityset SubcriptionColumn for FndObjSubscripColumn;

------------------------------ ENTITY DETAILS -------------------------------
@Override
entity FndObjSubscription {
   crud = Read, Update, Delete;

   array SubcriptionColumns(SubscriptionId) to FndObjSubscripColumn(SubscriptionId);
}

@Override
entity FndObjSubscripColumn {
   crud = Read, Update, Delete;
}

@Override
entity FndStream {
   attribute ItemId Text {
      fetch = "Message_sys.Find_Attribute(Reference, 'ITEM_ID', '')";
   }

   attribute WebUrl Text {
      label = "URL";

   }
}

---------------------------- SUBSCRIPTION DIALOG ----------------------------

entityset SubscriptionVirtualSet for SubscriptionVirtual;

virtual SubscriptionVirtual {
   attribute       SubscriptionId     Number;
   attribute       LuName             Text;
   attribute       KeyRef             Text;
   attribute       ClientView         Text;
   attribute       SubObjkey          Text;
   attribute       Username           Text;
   attribute       SubscriptionHeader Text;
   attribute       Notes              Text;
   attribute       PageUrl            Text;
   attribute       SendEmail          Boolean {
      required = [true];
   }
   attribute       ExpiryDate         Date;
   attribute       Disabled           Boolean {
      required = [true];
   }
   attribute       SelfNotify         Boolean {
      required = [true];
   }
   attribute       OneTime            Boolean {
      required = [true];
   }
   attribute SysDate Date {
      fetch = "TRUNC(SYSDATE)";
   }

   attribute ColumnData LongText {
      inlined = [true];
      maxlength = 32000;
   }

   array ColumnArray() to SubscriptionColumnVirtual();

}

virtual SubscriptionColumnVirtual {
   attribute SubscriptionId Number;
   attribute SubscriptionColumn Text;
   attribute DisplayName Text;
   attribute Selected Boolean {
      required = [true];
   }
   attribute ConditionClient Text;

}

action SaveSubscription {
   initialcheck implementation;
   parameter Objkey Text;
   parameter ParentKeyRef Text;
   ludependencies = FndObjSubscription, FndObjSubscripColumn;
}

action FillSubscriptionColumns {
   initialcheck none;
   parameter Objkey Text;
   ludependencies = SubscriptionColumnVirtual;
}

action RemoveSubscription {
   initialcheck implementation;
   parameter SubscriptionId Number;
   ludependencies = FndObjSubscription, FndObjSubscripColumn;
}

action SelectAllFields {
   initialcheck none;
   parameter Objkey Text;
   ludependencies = SubscriptionColumnVirtual;
}
action SelectNoFields {
   initialcheck none;
   parameter Objkey Text;
   ludependencies = SubscriptionColumnVirtual;
}

------------------------ NOTIFICATIONS PAGE COMMANDS ------------------------

action DeleteNotifications {
   initialcheck none;
   parameter DeleteCategory Text;
}

function GetNewMessageCount Number {
   parameter LastReceivedDate Timestamp;

}
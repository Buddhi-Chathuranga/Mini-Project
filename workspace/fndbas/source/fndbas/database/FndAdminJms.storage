-----------------------------------------------------------------------------
--
--  Logical unit: FndAdminJms
--  Component:    FNDBAS
--
--  IFS Developer Studio Template Version 3.0
--
--  Date        Sign    History
--  ----------  ------  -----------------------------------------------------
--  2016-04-21  MADRSE  TEMWS-500: Recreating and starting JSF Admin JMS AQ queues
-----------------------------------------------------------------------------

layer Core;

-------------------- TABLE DEFINITIONS --------------------------------------


-------------------- OTHER DEFINITIONS --------------------------------------

DECLARE
   --
   -- Recreating and starting JSF Admin JMS AQ queues
   --
   main_table_ CONSTANT VARCHAR2(100) := 'FND_ADMIN_MAIN_QUEUE_TAB';
   main_queue_ CONSTANT VARCHAR2(100) := 'FND_ADMIN_MAIN_QUEUE';

   int_table_  CONSTANT VARCHAR2(100) := 'FND_ADMIN_INT_QUEUE_TAB';
   int_queue_  CONSTANT VARCHAR2(100) := 'FND_ADMIN_INT_QUEUE';

   PROCEDURE Drop_Queue_Table (table_name_ VARCHAR2) IS
      no_table EXCEPTION;
      PRAGMA   EXCEPTION_INIT(no_table, -24002);
   BEGIN
      Dbms_Aqadm.Drop_Queue_Table(table_name_, true);
   EXCEPTION
      WHEN no_table THEN
         NULL;
      WHEN OTHERS THEN
         RAISE;
   END;

   PROCEDURE Create_Queue_Table (table_name_ VARCHAR2) IS
   BEGIN
      Dbms_Aqadm.Create_Queue_Table
        (queue_table        => table_name_,                  -- max 24 characters
         storage_clause     => 'TABLESPACE &IFSAPP_DATA',    -- indexes will be also created in this tablespace
         queue_payload_type => 'SYS.AQ$_JMS_BYTES_MESSAGE',  -- corresponds to DataInputStream in java
         multiple_consumers => FALSE);                       -- FALSE for queues, TRUE for topics
   END Create_Queue_Table;

BEGIN
   Drop_Queue_Table(main_table_);
   Create_Queue_Table(main_table_);
   Dbms_Aqadm.Create_Queue
     (queue_name  => main_queue_,
      queue_table => main_table_);
   Dbms_Aqadm.Start_Queue (main_queue_);

   Drop_Queue_Table(int_table_);
   Create_Queue_Table(int_table_);
   Dbms_Aqadm.Create_Queue
     (queue_name  => int_queue_,
      queue_table => int_table_);
   Dbms_Aqadm.Start_Queue (int_queue_);
END;



-----------------------------------------------------------------------------
--
--  Fragment:     InputUnitOfMeasure
--  Component:    PARTCA
--
--  IFS Developer Studio Template Version 3.0
--
--  Date    Sign    History
--  ------  ------  ---------------------------------------------------------
--  190701  SucPlk  SCUXXW4-20576, Modified Get_Direct_Output_Qty___() to merge corrections from support bug SCZ-3466.
--  190621  RasDlk  SCUXXW4-15881, Corrected few issues related to InputUoM.
--  190416  RasDlk  SCUXXW4-4704, Created.
-----------------------------------------------------------------------------

layer Core;

FUNCTION Get_Uom_Values___ (
   input_uom_group_id_ IN VARCHAR2,
   component_          IN VARCHAR2) RETURN Objid_Arr
IS
   base_collection_  Objid_Arr := Objid_Arr();
   
   CURSOR get_value_with_input_uom_group_id IS
      SELECT objid
        FROM INPUT_UNIT_MEAS
       WHERE input_unit_meas_group_id = input_uom_group_id_
         AND (purch_usage_allowed = (CASE WHEN (component_ = 'PURCH') THEN 1 END) OR
              cust_usage_allowed = (CASE WHEN (component_  = 'ORDER') THEN 1 END) OR
              manuf_usage_allowed = (CASE WHEN (component_ = 'MANUF') THEN 1 END));
   
   CURSOR get_value_without_input_uom_group_id IS
      SELECT objid
        FROM INPUT_UNIT_MEAS
        WHERE (purch_usage_allowed = (CASE WHEN (component_ = 'PURCH') THEN 1 END) OR
              cust_usage_allowed = (CASE WHEN (component_  = 'ORDER') THEN 1 END) OR
              manuf_usage_allowed = (CASE WHEN (component_ = 'MANUF') THEN 1 END));
BEGIN
   IF (input_uom_group_id_ IS NOT NULL AND component_ IS NOT NULL) THEN
      FOR rec_ IN get_value_with_input_uom_group_id LOOP
         base_collection_.extend;
         base_collection_(base_collection_.last) := rec_.objid;
      END LOOP;
   ELSE
      FOR rec_ IN get_value_without_input_uom_group_id LOOP
         base_collection_.extend;
         base_collection_(base_collection_.last) := rec_.objid;
      END LOOP;
   END IF;
   RETURN base_collection_;
END Get_Uom_Values___;


FUNCTION Show_Input_Uom_Dialog___ (
   input_uom_group_id_ IN VARCHAR2,
   input_uom_          IN VARCHAR2) RETURN VARCHAR2
IS
   dummy_ NUMBER;
   CURSOR variables IS
      SELECT 1
        FROM input_unit_meas_variable_tab
       WHERE input_unit_meas_group_id = input_uom_group_id_
         AND unit_code = input_uom_
         AND value_source IN ('USER', 'DEFAULT');
BEGIN
	OPEN variables;
   FETCH variables INTO dummy_;
   IF variables%NOTFOUND THEN
      CLOSE variables;
      RETURN 'FALSE';
   END IF;
   CLOSE variables;
   RETURN 'TRUE';
END Show_Input_Uom_Dialog___;


FUNCTION Get_Direct_Output_Qty___ (
   input_qty_          IN NUMBER,
   input_uom_          IN VARCHAR2,
   input_uom_group_id_ IN VARCHAR2,
   input_conversion_   IN NUMBER) RETURN Input_Uom_Info_Structure_Rec
IS
   input_uom_info_structure_rec_   Input_Uom_Info_Structure_Rec;
   rounding_decimals_              NUMBER;
   multiplier_                     NUMBER;
BEGIN
	input_uom_info_structure_rec_.input_value_string := Input_Unit_Meas_API.Get_Input_Value_String(input_qty_, input_uom_);
   rounding_decimals_  := Input_Unit_Meas_API.Get_Rounding_Decimals(input_uom_group_id_, input_uom_);   
   
   multiplier_ := POWER(10, TRUNC(rounding_decimals_));
   input_uom_info_structure_rec_.output_qty_with_precision := input_qty_ * input_conversion_;
   
   input_uom_info_structure_rec_.output_qty := ROUND(input_uom_info_structure_rec_.output_qty_with_precision, TRUNC(rounding_decimals_) + 2);
   IF (input_uom_info_structure_rec_.output_qty != TRUNC(input_uom_info_structure_rec_.output_qty * multiplier_) / multiplier_) THEN
      input_uom_info_structure_rec_.output_qty := (TRUNC(input_uom_info_structure_rec_.output_qty * multiplier_) / multiplier_) + POWER(10, TRUNC(rounding_decimals_ * -1));
   END IF;   
   
   RETURN input_uom_info_structure_rec_;
END Get_Direct_Output_Qty___;


FUNCTION Get_Conversion_Factor___ (
   input_uom_group_id_ IN VARCHAR2,
   input_uom_          IN VARCHAR2) RETURN NUMBER
IS
BEGIN
	RETURN Input_Unit_Meas_API.Get_Conversion_Factor(input_uom_group_id_, input_uom_);
END Get_Conversion_Factor___;
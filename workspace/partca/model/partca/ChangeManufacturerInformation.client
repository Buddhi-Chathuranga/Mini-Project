----------------------------------------------------------------------------------------------------------------------------------
-- Date        Sign    History
-- ----------  ------  -----------------------------------------------------------------------------------------------------------
-- 2020-11-24  JaThlk  SC2020R1-11283, Added the enable condition in Ok command to check whether the manufacturer information has been changed.
-- 2020-10-13  JaThlk  SC2020R1-10433, Overrode the selectors PartManufacturerSelector, ReferencePartRevManufPartLovSelector and PartManuPartNoSelector to modify the fields.
-- 2020-10-06  JaThlk  SC2020R1-10321, Added conditions to check whether it is a serial-lot tracked part, in such a case the user is informed that the relevant record in Part Lot Batch should be updated and vice versa.
-- 2020-08-24  JaThlk  SC2020R1-1185, Modified the assistant ChangeManufacturerInfoAssistant to use it in Part Lot Batch window.
-- 2020-08-18  JaThlk  SC2020R1-1191, Created to give grants based on different user roles for changing manufacturer information.
----------------------------------------------------------------------------------------------------------------------------------

client ChangeManufacturerInformation;
component PARTCA;
layer Core;
description "Use this assistant to change the manufacturer information for the selected serial";

--------------------------------- FRAGMENTS ---------------------------------
@DynamicComponentDependency PDMCON
include fragment PartRevManufacturerLovSelector;
@DynamicComponentDependency PDMCON
include fragment PartRevManufPartLovSelector;

warningsenabled = SetPartSerialManufInfo, SetLotBatchManufInfo;

----------------------------- NAVIGATOR ENTRIES -----------------------------

-------------------------------- MAIN PAGES ---------------------------------

-------------------------------- ASSISTANTS ---------------------------------
assistant ChangeManufacturerInfoAssistant using ChangeManufacturerInfoVirtualSet {
   label = "Change Manufacturer Info";

   input(PartNo, SerialNo, LotBatchNo, ManufacturerNo, ManufacturerPartNo, ManufacturedDate, ManufacturerSerialNo, ManufacturerLotBatchNo, EngPartRevision, Source);

   singlestep {
      group ChangeManufacturerInfoGroup;
   }

   command Ok {
      enabled = [(ManufacturerNo != original.ManufacturerNo) or(ManufacturerPartNo != original.ManufacturerPartNo) or(ManufacturedDate != original.ManufacturedDate) or(ManufacturerSerialNo != original.ManufacturerSerialNo) or(ManufacturerLotBatchNo != original.ManufacturerLotBatchNo)];
      variable SerialLotTrackedVar Boolean;
      execute {
         call CheckSerialAndLotTracked(PartNo) into SerialLotTrackedVar;
         if [Source = "PARTSERIAL"] {
            if [SerialLotTrackedVar] {
               confirm("This serial number is also connected to a lot batch number which also have manufacturer information. Please review the changes made and consider if the manufacturer information on the lot batch record also needs to be updated to keep the information in sync") {
                  when OK {
                     call SetPartSerialManufInfo(PartNo, SerialNo, ManufacturerNo, ManufacturerPartNo, ManufacturedDate, ManufacturerSerialNo);
                  }
                  when CANCEL {
                     exit CANCEL;
                  }
               }
            }
            else {
               call SetPartSerialManufInfo(PartNo, SerialNo, ManufacturerNo, ManufacturerPartNo, ManufacturedDate, ManufacturerSerialNo);
            }
         }
         else {
            if [SerialLotTrackedVar] {
               confirm("This lot batch is also connected to one or several serial numbers which also have manufacturer information. Please review the changes made and consider if the manufacturer information on the serials also needs to be updated to keep the information in sync") {
                  when OK {
                     call SetLotBatchManufInfo(PartNo, LotBatchNo, ManufacturerNo, ManufacturerPartNo, ManufacturedDate, ManufacturerLotBatchNo);
                  }
                  when CANCEL {
                     exit CANCEL;
                  }
               }
            }
            else {
               call SetLotBatchManufInfo(PartNo, LotBatchNo, ManufacturerNo, ManufacturerPartNo, ManufacturedDate, ManufacturerLotBatchNo);
            }
         }
         exit OK;
      }
   }
   command Cancel;
}

--------------------------------- COMMANDS ----------------------------------


--------------------------------- SELECTORS ---------------------------------
@Override
selector PartManufacturerSelector for PartManufacturer {
   static PartNo {
      visible = [false];
   }
   static Name {
      label = "Manufacturer Name";
   }
}

@Override
@DynamicComponentDependency PDMCON
selector ReferencePartRevManufPartLovSelector for PartRevManufPartLov {
   static PartNo;
}

@Override
selector PartManuPartNoSelector for PartManuPartNo {
   fieldorder = ManuPartNo, ManufacturerNo, PartNo;
}

---------------------------------- GROUPS -----------------------------------
group ChangeManufacturerInfoGroup for ChangeManufacturerInfoVirtual {
   label = "New Manufacturer Information";
   field ManufacturerNo {
      lovswitch {
         @DynamicComponentDependency PDMCON
         when [PartRevExist = "TRUE"] {
            lov PartRevManufacturerRef with ReferencePartRevManufacturerLovSelector {
               description = ManufacturerInfoRef.Name;
               label = "Manufacturer";
            }
         }
         when [true] {
            lov ManufacturerNoRef with PartManufacturerSelector {
               description = ManufacturerInfoRef.Name;
               label = "Manufacturer";
            }
         }
      }
   }
   field ManufacturerPartNo {
      lovswitch {
         @DynamicComponentDependency PDMCON
         when [PartRevExist = "TRUE"] {
            lov ManuPartRevNoRef with ReferencePartRevManufPartLovSelector {
               label = "Manufacturer's Part No";
            }
         }
         when [true] {
            lov ManuPartNoRef with PartManuPartNoSelector {
               label = "Manufacturer's Part No";
            }
         }
      }
   }
   field ManufacturedDate;
   field ManufacturerSerialNo {
      visible = [Source = "PARTSERIAL"];
   }
   field ManufacturerLotBatchNo {
      visible = [Source = "LOTBATCH"];
   }
}

----------------------------------- LISTS -----------------------------------


---------------------------------- DIALOGS ----------------------------------


-------------------------------- SINGLETONS ---------------------------------


---------------------------------- CHARTS -----------------------------------


--------------------------------- CALENDERS ---------------------------------


---------------------------------- SHEETS -----------------------------------


----------------------------- STATE INDICATORS ------------------------------


----------------------------------- TREES -----------------------------------


---------------------------------- PLUGINS ----------------------------------


------------------------------- IMAGE VIEWERS -------------------------------
